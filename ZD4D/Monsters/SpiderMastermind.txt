//================================================================================================================
//================================================================================================================
//
//	"D4D mode" version
//	
//================================================================================================================
//================================================================================================================

/*
Rainbow Doom Monster Schematics
* Gains 1000 health per tier.

Uncommon
* Shoots two plasma bolts instead of a single stream.
* Attacks slightly faster with lasers, including delayed rotation.
	
Rare
* 3 plasma bolts
* Charged laser can slowly track its target
* Gains armored legs and base
	
Epic
* 4 plasma bolts
* Increased charge laser tracking speed
* Rotating lasers are quicker
	
Legendary
* Rotating lasers are now weak-charged lasers with no range limits
* Occasionally unleashes a small laser with its torrent of plasma bolts
	
Relic
* Faster target tracking
* Can leap at or away from target
* Attacks cannot be interrupted

CURSED (Black)
* Lasers last longer
* Spinning lasers are fully charged

BLESSED (White)
* All attacks replaced by shotgun laser
* Much more aggressive
* Easily distracted by infighting
* Attacks can insta-kill most enemies
* Deals extra damage to demon runes
*/
const SMMLaserRange = 32766;
Class D4SpiderMastermind : D4AbstractBoss //replaces SpiderMastermind
{
	const SRailFlags = RGF_SILENT|RGF_FULLBRIGHT|RGF_NORANDOMPUFFZ|RGF_CENTERZ;
	const CBAFlags = CBAF_AIMFACING|CBAF_PUFFTRACER|CBAF_NORANDOMPUFFZ|CBAF_NORANDOM;
	int UAngle;
	int counter;
	double UPitch;
	int direction;
	int count;
	
	
	override void Die(Actor source, Actor inflictor, int dmgflags)
	{
		if (DamageTypeReceived == 'BerserkKill' || 
			DamageTypeReceived == 'MeleePunch' || 
			DamageTypeReceived == 'ChainsawKill' || 
			DamageTypeReceived == 'ChainsawPinata')
		{
			for (int i = 0; i < 5; i++)
				A_DropItem("D4PinataHealth");
		}
		if (Event) Event.DestroyBeams(self);
		Super.Die(source, inflictor, dmgflags);
	}
	
	Default 
	{
		D4AbstractMonster.Replacing 'SpiderMastermind';
		D4AbstractMonster.ResurrectCvar 'D4D_ResSpiderMastermind';
		D4AbstractMonster.Rune "MastermindRune", RuneChance_Master;
		D4AbstractMonster.UpgradeDropChance 5;
		D4AbstractMonster.CrateDropAmount 3;
		D4AbstractMonster.CrateDropChance 192;
		D4AbstractMonster.CrateDropAlways 2;
		D4AbstractMonster.CashAmount 400;
		D4AbstractMonster.CashChainsawFinisher 2000;
		+BOSS
		+MISSILEMORE
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		+BOSSDEATH
		+DONTCORPSE
		Health 3000;
		Radius 96;
		Height 100;
		Mass 1000;
		Speed 12;
		PainChance 30;
		Monster;
		MinMissileChance 160;
		MissileHeight 40;
		SeeSound "Doom4/SpiderMastermind/Sight";
		//AttackSound "spider/attack";
		PainSound "Doom4/SpiderMastermind/Pain";
		DeathSound "Doom4/SpiderMastermind/Death";
		ActiveSound "Doom4/SpiderMastermind/Idle";
		Obituary "$OB_SPIDER";
		DamageFactor "BFGExploder", 0.0;
		DamageFactor "CyberdemonFireWave", 0.2;	//for if there's infight, otherwise cyberdemons rape this guy so easily
		PainChance "GloryKill", 255;
		WoundHealth 150;
		Tag "Spider Mastermind";
	}
	States
	{
	Spawn:
	Idle:
		SPID A 0 A_StopSound(6);
		SPID AB 10 A_Look();
		Goto Idle+1;
	See:
		SPID A 3 { D4Chase(); A_PlaySound("Player/Demon/MastermindStep"); }
		SPID ABB 3 D4Chase();
		SPID C 3 { D4Chase(); A_PlaySound("Player/Demon/MastermindStep"); }
		SPID CDD 3 D4Chase();
		SPID E 3 { D4Chase(); A_PlaySound("Player/Demon/MastermindStep"); }
		SPID EFF 3 D4Chase();
		Loop;
		
		
	Missile:
	DecideAllAttacks:
		SPID A 0 
		{
			UAngle = counter = 0;
			if (target)
			{
			//	double dist = Distance3DSquared(target);
				if (!(target is "PlayerPawn"))
				{
					bNOPAIN = true;
					return ResolveState("LaserShotFast");
				}
			}
			return A_Jump(256,"PlasmaAttack","LaserShot");
		}
		
	
	//---------------------------------------------------
	// Plasma attack
	PlasmaAttack:
		SPID A 20 BRIGHT A_FaceTarget();
	PlasmaLoop:
		SPID GH 4 BRIGHT 
		{
			A_FaceTarget();
			A_SpawnProjectile("SMMPlasma",40,0,0);
		}
		TNT1 A 0 A_MonsterRefire(10,"See");
		Loop;
	//---------------------------------------------------
	// Laser attack
	LaserShot:
		SPID AAAA 5 A_FaceTarget();
	// Skip the delay and try to murder the enemy if not a player.
	LaserShotFast:
		SPID A 0 
		{
			A_FaceTarget(0,0,0,0,FAF_MIDDLE);
			A_SpawnItemEx("LaserChargeIndicator",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION);
			A_PlaySound("Doom4/SpiderMastermind/LaserCharge",5,1,0,(target is "PlayerPawn" ? ATTN_NONE : ATTN_NORM));
			A_StartBeam("SMMBeamWarning",(10,0,40),1,"PacifistPuff", dist: 32766);
		}
		SPID GGGGGGGGGG 2 
		{
			A_FaceTarget(0,0,0,0,FAF_MIDDLE);
		//	A_CustomBulletAttack(0,0,1,0,"PacifistPuff",32767,CBAFlags,TargetPtr,"WhiteLaser",40,0);
			A_UpdateBeamTracer("PacifistPuff", dist: 32766);
		}
		SPID G 3 //gives you a tiny bit of chance to run away
		{
			A_FaceTarget(0,0,0,0,FAF_MIDDLE);
			int attn = (target is "PlayerPawn" ? ATTN_NONE : ATTN_NORM);
			A_PlaySound("Doom4/Monsters/MastermindLaserFire",5,1,0,attn);
			A_PlaySound("Doom4/SpiderMastermind/LaserLoop",6,1,1,attn);
			A_PlaySound("Doom4/SpiderMastermind/LaunchPlasma",7,1,0,attn);
			UPitch = pitch;
			count = 0;
			A_UpdateBeamTracer("PacifistPuff", dist: 32766);
		}
		SPID H 1 bright
		{
			A_StopBeam();
			if (!count)
				A_CustomBulletAttack(0,0,1,0,"PacifistPuffSMM",SMMLaserRange,CBAFlags,TargetPtr,"BigRedLaserDrillSMMSpawner",40,0);
			
			int dmg = ((count <= 28) ? 5 : 15) * random(1,8); 
			A_CustomRailgun(dmg,0,"","",SRailFlags,0,0,"NonPacifistPuffExtreme",0,0,32766,4,1.0,3.0,null,8);
			
			StateLabel next = null;
			if (count++ >= 56)	next = "EndLaserShot";
			return ResolveState(next);
		}
		Wait;
	EndLaserShot:
		SPID G 4 
		{
			A_StopBeam();
			bNOPAIN = false; 
			A_StopSound(6); 
			count = 0; 
		}
		Goto See;
		
	//---------------------------------------------------
	// Electro beams attack
	EletricBeams:
		SPID A 0 A_PlaySound("Doom4/SpiderMasterMind/LaserCharge",5,1,0,(target is "PlayerPawn" ? ATTN_NONE : ATTN_NORM));
		SPID AAAA 5 A_FaceTarget(0,0,0,0,FAF_MIDDLE);	//aims at target
		SPID A 0	//attack setup
		{
			direction = randompick(-1,1); //if 1, means will turn left, -1 means right
			UAngle = 0;
			UPitch = pitch;
			A_PlaySound("Doom4/SpiderMastermind/LaserLoop",6,1,1,(target is "PlayerPawn" ? ATTN_NONE : ATTN_NORM));
			A_PlaySound("Doom4/Monsters/MastermindBeamFire",7,1);
		}
		
		SPID AAAAAAAAAAAAA 2
		{
			A_SpawnProjectile("SMMElectroBeam",40,0,0,CMF_AIMDIRECTION,UPitch);
			A_SpawnProjectile("SMMElectroBeam",40,0,60,CMF_AIMDIRECTION,UPitch);
			A_SpawnProjectile("SMMElectroBeam",40,0,-60,CMF_AIMDIRECTION,UPitch);
			A_SpawnProjectile("SMMElectroBeam",40,0,120,CMF_AIMDIRECTION,UPitch);
			A_SpawnProjectile("SMMElectroBeam",40,0,-120,CMF_AIMDIRECTION,UPitch);
			A_SpawnProjectile("SMMElectroBeam",40,0,180,CMF_AIMDIRECTION,UPitch);
		}
	EleBeamLoop:
		SPID G 2
		{
			if (counter >= 40)
			{
				UAngle = counter = 0;
				return ResolveState("EndBeamLoop");
			}
			A_SpawnProjectile("SMMElectroBeam2",40,0,0+UAngle,CMF_AIMDIRECTION,UPitch);
			A_SpawnProjectile("SMMElectroBeam2",40,0,60+UAngle,CMF_AIMDIRECTION,UPitch);
			A_SpawnProjectile("SMMElectroBeam2",40,0,-60+UAngle,CMF_AIMDIRECTION,UPitch);
			A_SpawnProjectile("SMMElectroBeam2",40,0,120+UAngle,CMF_AIMDIRECTION,UPitch);
			A_SpawnProjectile("SMMElectroBeam2",40,0,-120+UAngle,CMF_AIMDIRECTION,UPitch);
			A_SpawnProjectile("SMMElectroBeam2",40,0,-180+UAngle,CMF_AIMDIRECTION,UPitch);
			
			UAngle += (4 * direction);
			counter++;
			return ResolveState(null);
		}
		Loop;
	EndBeamLoop:
		SPID A 10 A_StopSound(6);
		Goto See;
	
	Pain:
		SPID I 3 
		{
			A_StopBeam();
			if (Event) Event.DestroyBeams(self);
			A_StopSound(6);
		}
		SPID I 3 A_Pain();
		Goto See;
	Death:
		SPID J 20 
		{	
			bThruActors = true;
			A_StopSound(6);	A_Scream(); 
		}
		SPID K 10 
		{	A_NoBlocking();	A_PlaySound("Doom4/SpiderMastermind/GunExplode",7);	}
		SPID L 10;
		SPID M 10
		{
			A_SpawnItemEx("SpiderMasterMindDeathFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_SETTRACER|SXF_ORIGINATOR|SXF_TRANSFERTRANSLATION);
			A_SpawnItemEx("BossExploderSFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETTRACER|SXF_ORIGINATOR);
		}
		SPID NOPQR 10;
		SPID S 30;
	Dead:
		---- A -1 CanRaise 
		{
			bCorpse = true;
			A_BossDeath();
		}
		Stop;
		
	Raise:
		---- A 0 DoResurrect();
		SPID SRQPONMLJI 10;
		---- A 0 DoResurrect(1);
		Goto Spawn;
		
	GloryEnd:
		TNT1 A 0 A_Die("MeleePunch");
	Death.BerserkKill:
	Death.MeleePunch:
	Death.ChainsawPinata:
	XDeath:
	
		SPID J 0;
		SPID J 20 
		{
			bThruActors = true;
			XDeathGush();
			A_StopSound(6);	
			A_Scream(); 
		}
		SPID K 10 
		{	A_NoBlocking();	A_PlaySound("Doom4/SpiderMastermind/GunExplode",7);	}
		SPID L 10 ;
		SPID M 10 
		{
			if (CountInv("DeathByMassacre") == 0)
			{
				A_SpawnItemEx("SpiderMasterMindDeathFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_SETTRACER|SXF_ORIGINATOR|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("BossExploderSFX",0,0,0,0,0,0,0,32|SXF_SETTRACER|SXF_ORIGINATOR);
		}	}
		SPID NOPQR 10;
		SPID S 30;
		Goto Dead;
	//------------------------------------------------------------------------
	// Start of Stagger / Glory Kill states
	//------------------------------------------------------------------------
	Wound:
		"####" "#" 1
		{
			A_StopBeam();
			if(GetCvar("D4D_ExecutionsON"))
			{
				if(countinv("FinisherToken")==0) //if it isnt healing already
				{								//will heal and give the token that indicates it's executable
					A_SpawnItemEx("MastermindHighlight",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION); //to highlight if ON, and to remove the token after the Monster; heals
					A_GiveInventory("FinisherToken",1);	//token to allow melee attack to execute
				}
				
				if(GetCvar("D4D_MonsterStagger")==1) //will the monsters stagger at all?
				{
					return ResolveState("Stagger");	//stays there for a few seconds
				}
				else
				{
					return ResolveState(null);
				}
			}
			return ResolveState(null);
		}
		Goto See;
	Stagger:
		SPID I 0 
		{
			if(GetCvar("D4D_ExecutionHighlight")==1)
			{	return ResolveState("PrepareStaggerHighlight");}
			else
			{	return ResolveState("StagLooper");	}
		}
		Goto StagLooper;
	PrepareStaggerHighlight:
		SPID U 0;
		Goto StagLooper;
	
	StagLooper:
		"####" "#" 1
		{
			if(!countinv("FinisherToken"))
			{
				return ResolveState("See");
			}
			if(GetCvar("D4D_NoisyStagger") && countinv("PainSoundToken"))
			{ 
				A_Pain(); 
				A_TakeInventory("PainSoundToken");
			}
			
			return ResolveState(null);
		}
		Loop;
		
	// Finisher attack(s) with animations ON
	Death.GloryKill:
		TNT1 A 0 { bSolid = false; }
		SPID AA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION) ;
		"####" A 0
		{
			if(countinv("GloryKill1Token"))
			{	return ResolveState("GloryEnd1"); }
			
			if(countinv("GloryKill2Token"))
			{	return ResolveState("GloryEnd2"); }
			
			if(countinv("GloryKill3Token"))
			{	return ResolveState("GloryEnd3"); }
			return ResolveState(null);
		}
	GloryEnd1:
		"####" AAAAAAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		"####" A 0 A_Pain();
		"####" IIIIII 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		"####" AAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		Goto GloryEnd;
	GloryEnd2:
		"####" AAAAAAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		"####" A 0 A_Pain();
		"####" IIIIII 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		"####" AAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		Goto GloryEnd;
	GloryEnd3:
		"####" AAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		"####" I 0 A_ChangeVelocity(-8,0,10,CVF_RELATIVE);
		Goto GloryEnd;
	}
}

Class BossExploderSFX : Actor 
{
	Default 
	{
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 AAAA 10 NODELAY
		{
			A_PlaySound("Doom4/Weapon/RocketLauncher/Q4Explo");
			A_SetTics(random(10,20));
		}
		Stop;
	}
}

Class PacifistPuffSMM : PacifistPuff
{
	States
	{
	Spawn:
		TNT1 A 60;
		Stop;
	}
}

Class SpiderMastermindDeathFX : Actor 
{
	const MinX = 20;
	const MaxX = 45;
	const MinZ = 8;
	const MaxZ = 30;
	int UMax;
	int c;
	
	Default 
	{
		+NOINTERACTION
		+NOTONAUTOMAP
		RenderStyle "Add";
		Alpha 0.75;
		Translation "0:255=%[0,0,0]:[2,0,0]";
	}
	States
	{
	Spawn:
		TNT1 A 1 NoDelay
		{
			A_Warp(MasterPtr,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_COPYVELOCITY);
			if (UMax <= 0)
			{	UMax = 50;	}
			
			if (!A_CheckSight("Null"))
			{
				if (c == 8 ||
					c == 12 ||
					c == 16 ||
					c == 20 ||
					c == 24 ||
					c == 28 ||
					c == 32 ||
					c == 36)
				{	
					A_SpawnItemEx("D4RocketExplosionNoGL",random(20,80),0,random(10,60),0,0,0,random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERALPHA);	
					A_SpawnItemEx("D4RocketExplosionNoGL",random(20,80),0,random(10,60),0,0,0,random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERALPHA);
					A_SpawnItemEx("D4RocketExplosionNoGL",random(20,80),0,random(10,60),0,0,0,random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERALPHA);
				}
				
				if (c == 20)
				{
					A_SpawnItemEx("BloodGibDropper",random(MinX,MaxX),0,random(MinZ,MaxZ),vel.x,vel.y,vel.z,random(0,359),SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION);
					A_SpawnItemEx("BloodGibDropper",random(MinX,MaxX),0,random(MinZ,MaxZ),vel.x,vel.y,vel.z,random(0,359),SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION);
					A_SpawnItemEx("BloodGibDropper",random(MinX,MaxX),0,random(MinZ,MaxZ),vel.x,vel.y,vel.z,random(0,359),SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION);
				}
			}
			
			if (c == 20)
			{
				A_PlaySound("Doom4/Other/GibExplode1",5);
				A_PlaySound("Doom4/Other/GibExplode2",6);
				A_PlaySound("Doom4/Other/GibExplode3",7);
			}
			c++;
			
			if (c > UMax)
			{	return ResolveState("Null");	}
			return ResolveState(null);
		}
		Loop;
	}
}

//------------------------------------------------------------------
// Red big laser static
Class SMMLaserWeak : D4D_Projectile	//main beam aimed at you, fires a few weak ones
{
	Default 
	{
		Speed 300;
		MissileType "SMMLaserTrail";
		MissileHeight 8;
		Radius 3;
		Height 6;
		Damage 5;
		Decal "RedElectricDecalBig";
	}
	States
	{
	Spawn:
		TNT1 A 4;
		Loop;
	Death:
		TNT1 A 3;
		Stop;
	}
}
Class SMMLaserWeakInvis : D4D_Projectile
{
	Default 
	{
		Speed 300;
		Radius 3;
		Height 6;
		Damage 5;
		Decal "RedElectricDecalBig";
	}
	States
	{
	Spawn:
		TNT1 A 4;
		Loop;
	Death:
		TNT1 A 3;
		Stop;
	}
}
Class SMMLaserStrong : SMMLaserWeak	//fires this one after the weak ones, get the hell away from the attack
{
	Default 
	{
		Damage 15;
	}
}
Class SMMLaserStrongInvis : SMMLaserWeakInvis
{
	Default 
	{
		Damage 15;
	}
}
Class SMMLaserTrail : Actor 
{
	Default 
	{
		+CLIENTSIDEONLY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		Scale 0.4;
		//Translation "0:255=%[0,0,0]:[2,0.0,0.0]"
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"Type1","Type2","Type3","Type4");
	Type1:
		HSPL ABCDEG 1 Bright;
		Stop;
	Type2:
		HSPL DEGHBD 1 Bright;
		Stop;
	Type3:
		HSPL HBDCGE 1 Bright;
		Stop;
	Type4:
		HSPL CGEABC 1 Bright;
		Stop;

	}
}

Class LaserChargeIndicator : Actor 
{
	Default 
	{
		RenderStyle "Add";
		+NOINTERACTION
		+FORCEXYBILLBOARD
		Translation "0:255=%[0,0,0]:[2.0,0.2,0.2]";
		Scale 2.0;
		Alpha 0.01;
	}
	States
	{
	Spawn:
		X060 AAAAAAAAAAAAAAAAAAAA 1 Bright NODELAY
		{
			A_FadeIn(0.05);
			A_SetScale(Scale.X-0.1);
			A_Warp(MasterPtr,30,4,45,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		}
		Stop;
	}
}

//------------------------------------------------------------------
// Multiple yellow lasers, moving in circle
Class SMMElectroBeam : D4D_Projectile	//main beam aimed at you
{
	Default 
	{
		Speed 280;
		MissileType "SMMElectroBeamTrail";
		MissileHeight 8;
		Radius 3;
		Height 6;
		DamageFunction (random(1,4));
		+BLOODLESSIMPACT
		+RIPPER
		Decal "OrangeElectricDecal";
	}
	States
	{
	Spawn:
		TNT1 A 4;
		Stop;
	Death:
		TNT1 A 3;
		Stop;
	}
}
Class SMMElectroBeam2 : SMMElectroBeam //stronger version, since it's not aimed at you, if it connects, will deal better damage
{
	Default 
	{
		Damage 3;
	}
}


Class SMMElectroBeamTrail : Actor 
{
	Default 
	{
		+CLIENTSIDEONLY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		Scale 0.35;
		Translation "0:255=%[0,0,0]:[1.2,0.7,0.0]";
	}
	States
	{
	Spawn: 
		TNT1 A 0 NoDelay A_Jump(256,"Type1","Type2","Type3","Type4");
	Type1:
		HSPL ABC 1 Bright;
		Stop;
	Type2:
		HSPL DEG 1 Bright;
		Stop;
	Type3:
		HSPL HBD 1 Bright;
		Stop;
	Type4:
		HSPL CGE 1 Bright;
		Stop;

	}
}


//------------------------------------------------------------------
Class SMMPlasma : D4AbstractMissile
{
	Default 
	{
		Projectile;
		Radius 7;
		Height 12;
		Scale 0.27;
		RenderStyle "Add";
		Alpha 0.99;
		Speed 25;
		FastSpeed 30;
		Damage 6;
		SeeSound "Doom4/Monsters/PlasmaShot";
		Translation "0:255=%[0,0,0]:[1.3,0.7,0.3]";
		+FORCEXYBILLBOARD
		Decal "D4PlasmaMonsterBig";
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY;
		Goto Looper;
	Looper:
		SPIR P 2 Bright Light("OrangeSmallPoint2")
		{
			if (HQ)
			{
				A_SpawnItemEx("SSMPlasmaTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
			}
		}
		Loop;
	Death:
		TNT1 A 0 A_SpawnItemEx("SMMPlasmaBallPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		TNT1 A 3 Light("OrangeSmallPoint1");
		TNT1 A 3 Light("OrangeSmallPoint2");
		TNT1 A 3 Light("OrangeSmallPoint3");
		Stop;
	}
}

Class SSMPlasmaTrail : Actor 
{
	Default 
	{
		+NOINTERACTION
		RenderStyle "Add";
		Scale 0.1;
		Translation "0:255=%[0,0,0]:[1.3,0.6,0.4]";
		+FORCEXYBILLBOARD
	}
	States
	{
	Spawn:
		DB19 ABCDEFGHUIJKLMNOPQRSTUVWXYZ 1 Bright
		{
			A_FadeOut(0.05);
			A_SetScale(Scale.X-0.005);
		}
		Loop;
	}
}
Class SMMPlasmaBallPuff : Actor 
{
	const Scalar = 0.035;
	double scaler;	//this one is to easily tweak the inherited versions
	
	Default 
	{
		+CLIENTSIDEONLY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		+PUFFONACTORS
		Scale 0.1;
		RenderStyle "Add";
		Alpha 0.99;
		Translation "0:255=%[0,0,0]:[1.3,0.7,0.3]";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NODELAY
		{
			if (GetCvar("HQEffects"))
			{
				A_SpawnParticle("FF974D",SPF_FULLBRIGHT,random(15,30),random(2,3),0,0,0,0,frandom(0.85,-0.85),frandom(0.85,-0.85),frandom(-0.3,1.4),0,0,vel.z-0.08);
				A_SpawnParticle("FF974D",SPF_FULLBRIGHT,random(15,30),random(2,3),0,0,0,0,frandom(0.85,-0.85),frandom(0.85,-0.85),frandom(-0.3,1.4),0,0,vel.z-0.08);
				A_SpawnParticle("FFAD29",SPF_FULLBRIGHT,random(15,30),random(2,3),0,0,0,0,frandom(0.85,-0.85),frandom(0.85,-0.85),frandom(-0.3,1.4),0,0,vel.z-0.08);
				A_SpawnParticle("FFAD29",SPF_FULLBRIGHT,random(15,30),random(2,3),0,0,0,0,frandom(0.85,-0.85),frandom(0.85,-0.85),frandom(-0.3,1.4),0,0,vel.z-0.08);
				A_SpawnParticle("FF8000",SPF_FULLBRIGHT,random(15,30),random(2,3),0,0,0,0,frandom(0.85,-0.85),frandom(0.85,-0.85),frandom(-0.3,1.4),0,0,vel.z-0.08);
				A_SpawnParticle("FF8000",SPF_FULLBRIGHT,random(15,30),random(2,3),0,0,0,0,frandom(0.85,-0.85),frandom(0.85,-0.85),frandom(-0.3,1.4),0,0,vel.z-0.08);
			}
		}
		TNT1 A 0 A_Jump(256,"Type1","Type2","Type3","Type4","Type5","Type6","Type7","Type8");
	Type1:
		DB21 ABCDEFGHI 1 Bright A_SetScale(Scale.X+Scalar);
		Stop;
	Type2:
		DB21 JKLMNOPQR 1 Bright A_SetScale(Scale.X+Scalar);
		Stop;
	Type3:
		DB22 ABCDEFGHI 1 Bright A_SetScale(Scale.X+Scalar);
		Stop;
	Type4:
		DB22 JKLMNOPQR 1 Bright A_SetScale(Scale.X+Scalar);
		Stop;
	Type5:
		DB23 ABCDEFGHI 1 Bright A_SetScale(Scale.X+Scalar);
		Stop;
	Type6:
		DB23 JKLMNOPQR 1 Bright A_SetScale(Scale.X+Scalar);
		Stop;
	Type7:
		DB24 ABCDEFGHI 1 Bright A_SetScale(Scale.X+Scalar);
		Stop;
	Type8:
		DB24 JKLMNOPQR 1 Bright A_SetScale(Scale.X+Scalar);
		Stop;
	}
}



//================================================================================================================
//================================================================================================================
//
//	Regular Doom version 
//
//================================================================================================================
//================================================================================================================
Class D4OldSpiderMastermind : D4AbstractBoss // : SpiderMastermind
{
	
	
	Default 
	{
		D4AbstractMonster.Replacing 'SpiderMastermind';
		D4AbstractMonster.Rune "MastermindRune", RuneChance_Master;
		D4AbstractMonster.UpgradeDropChance 5;
		D4AbstractMonster.CrateDropAmount 3;
		D4AbstractMonster.CrateDropChance 192;
		D4AbstractMonster.CrateDropAlways 2;
		D4AbstractMonster.CashAmount 400;
		D4AbstractMonster.CashChainsawFinisher 2000;
		+BOSS
		+MISSILEMORE
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		+BOSSDEATH
		+DONTCORPSE
		Health 3000;
		Radius 128;
		Height 100;
		Mass 1000;
		Speed 12;
		PainChance 40;
		Monster;
		MinMissileChance 160;
		SeeSound "spider/sight";
		AttackSound "spider/attack";
		PainSound "spider/pain";
		DeathSound "spider/death";
		ActiveSound "spider/active";
		Obituary "$OB_SPIDER";
		DamageFactor "BFGExploder", 0.0;
		PainChance "GloryKill", 255;
		WoundHealth 150;
	}
	States
	{
	Spawn:
		SPID AB 10 A_Look();
		Loop;
	See:
		SPID A 3 A_Metal;
		SPID ABB 3 D4Chase();
		SPID C 3 A_Metal;
		SPID CDD 3 D4Chase();
		SPID E 3 A_Metal;
		SPID EFF 3 D4Chase();
		Loop;
	Missile:
		SPID A 20 BRIGHT A_FaceTarget();
		SPID G 4 BRIGHT A_SPosAttackUseAtkSound;
		SPID H 4 BRIGHT A_SposAttackUseAtkSound;
		SPID H 1 BRIGHT A_SpidRefire;
		Goto Missile+1;
	Pain:
		SPID I 3;
		SPID I 3 A_Pain();
		Goto See;
	GloryEnd:
		TNT1 A 0 A_Die("MeleePunch");
	Death.BerserkKill:
	Death.MeleePunch:
	Death.ChainsawPinata:
	XDeath:
		SPID J 0 XDeathGush();
	Death:
		SPID J 20 
		{
			bThruActors = true;
			A_Scream();
		}
		SPID K 10 A_NoBlocking();
		SPID LMNOPQR 10;
		SPID S 30;
		SPID S -1 
		{			
			bCorpse = true;
			A_BossDeath();
		}
		Stop;
	//------------------------------------------------------------------------
	// Start of Stagger / Glory Kill states
	//------------------------------------------------------------------------
	Wound:
		"####" "#" 1
		{
			if(GetCvar("D4D_ExecutionsON"))
			{
				if(countinv("FinisherToken")==0) //if it isnt healing already
				{								//will heal and give the token that indicates it's executable
					A_SpawnItemEx("MastermindHighlight",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION); //to highlight if ON, and to remove the token after the Monster; heals
					A_GiveInventory("FinisherToken",1);	//token to allow melee attack to execute
				}
				
				if(GetCvar("D4D_MonsterStagger")==1) //will the monsters stagger at all?
				{
					return ResolveState("Stagger");	//stays there for a few seconds
				}
				else
				{
					return ResolveState(null);
				}
			}
			return ResolveState(null);
		}
		Goto See;
	Stagger:
		SPID I 0 
		{
			if(GetCvar("D4D_ExecutionHighlight")==1)
			{	return ResolveState("PrepareStaggerHighlight");}
			else
			{	return ResolveState("StagLooper");	}
		}
		Goto StagLooper;
	PrepareStaggerHighlight:
		SPID U 0;
		Goto StagLooper;
	
	StagLooper:
		"####" "#" 1
		{
			if(!countinv("FinisherToken"))
			{
				return ResolveState("See");
			}
			if(GetCvar("D4D_NoisyStagger") && countinv("PainSoundToken"))
			{ 
				A_Pain(); 
				A_TakeInventory("PainSoundToken");
			}
			
			return ResolveState(null);
		}
		Loop;
		
	// Finisher attack(s) with animations ON
	Death.GloryKill:
		TNT1 A 0 { bSolid = false; }
		SPID AA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		"####" A 0
		{
			if(countinv("GloryKill1Token"))
			{	return ResolveState("GloryEnd1"); }
			
			if(countinv("GloryKill2Token"))
			{	return ResolveState("GloryEnd2"); }
			
			if(countinv("GloryKill3Token"))
			{	return ResolveState("GloryEnd3"); }
			return ResolveState(null);
		}
	GloryEnd1:
		"####" AAAAAAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		"####" A 0 A_Pain();
		"####" IIIIII 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		"####" AAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		Goto GloryEnd;
	GloryEnd2:
		"####" AAAAAAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		"####" A 0 A_Pain();
		"####" IIIIII 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		"####" AAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		Goto GloryEnd;
	GloryEnd3:
		"####" AAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION);
		"####" I 0 A_ChangeVelocity(-8,0,10,CVF_RELATIVE);
		Goto GloryEnd;
	}

}

