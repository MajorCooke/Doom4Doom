//================================================================================================================
//================================================================================================================
//
//	"D4D mode" version
//	Lost soul, sprites ...
//================================================================================================================
//================================================================================================================
Class D4LostSoulNoCash : D4AbstractMonster 		//see at the bottom of the file for the version that DO give cash
{
	int user_chasecount;
	int user_wanderdelay;
	int user_wanderstopped;
	Default 
	{
		D4AbstractMonster.CrateDropAmount 1;
		D4AbstractMonster.CrateDropChance 2;
		Health 50;
		Radius 16;
		Height 56;
		Mass 50;
		Speed 8;
		Damage 0;
		PainChance 256;
		Monster;
		+FLOAT  +NOGRAVITY +MISSILEMORE +DONTFALL +NOICEDEATH
		SeeSound "Doom4/Monsters/LostSoul/Sight";
		//AttackSound "skull/melee";
		PainSound "Doom4/Monsters/LostSoul/Pain";
		DeathSound "skull/death";
		ActiveSound "Doom4/Monsters/LostSoul/Idle";
		Obituary "$OB_SKULL";
		PainChance "StunBomb", 255;
		+DONTCORPSE
		//Cash drops = 1
		DropItem "C1";
	}
	
	States
	{
	Spawn:
		PSUL A 2 BRIGHT Light("BlueSmallFlicker1")
		{
			if(bFriendly)
			{
				A_Wander();
			}
			
			A_Look();
			if(GetCvar("HQEffects"))
			{
				A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,85);
			}
		}
		Loop;
	See:
		PSUL AAA 2 BRIGHT Light("BlueSmallFlicker1")
		{
			if(user_chasecount==1)
			{	D4Chase(); user_chasecount=0;	}
			else
			{	D4Chase(null,null); user_chasecount++;	}
			
			if(GetCvar("HQEffects"))
			{
				A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,85);
				A_SpawnItemEx("LostSoulBlueFlare",-8,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION);	
			}			
		}
		Loop;
		
	Melee:
		PSUL A 0 A_Die("SkullExplo");
		Goto Death;
	Missile:
		PSUL A 0 
		{
			A_FaceTarget();
			if (bFriendly)	{	return ResolveState("SkipWarning");	}
			A_PlaySound("Doom4/Monsters/LostSoul/Attack",CHAN_AUTO,0.6);
			return ResolveState(null);
		}
		PSUL AAAAAAAAAA 2 BRIGHT Light("OrangeMediumFlicker3")
		{ 
			A_FaceTarget();
			if(GetCvar("HQEffects"))
			{
				A_SpawnItemEx("LostSoulRedFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,85);
				A_SpawnItemEx("LostSoulRedFlare",-5,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION);
			}
			else
			{
				A_SpawnItemEx("LostSoulRedFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,160);
				A_SpawnItemEx("LostSoulRedFlare",-5,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION,128);
			}
		}
	SkipWarning:
		PSUL B 0 BRIGHT A_SkullAttack;	//attack frame
		PSUL B 2 BRIGHT Light("OrangeMediumFlicker3")
		{
			if (bFRIENDLY)
			{
				A_FaceTarget(0,0,0,0,FAF_MIDDLE);
				A_ChangeVelocity(cos(pitch) * 20, 0, -sin(pitch) * 20,CVF_RELATIVE|CVF_REPLACE);
			}
			if(GetCvar("HQEffects"))
			{
				A_SpawnItemEx("LostSoulRedFlame",random(-10,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,85);
				A_SpawnItemEx("LostSoulRedFlare",-20,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION);
			}
			else
			{
				A_SpawnItemEx("LostSoulRedFlame",random(-10,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,160);
				A_SpawnItemEx("LostSoulRedFlare",-20,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION,128);
			}
		}
		Wait;
	Pain: 
		PSUL CC 2 BRIGHT Light("BlueSmallFlicker1")
		{
			if(GetCvar("HQEffects"))
			{
				A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,85);
				A_SpawnItemEx("LostSoulBlueFlare",-8,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION);
			}
		}
		PSUL C 2 BRIGHT Light("BlueSmallFlicker1")
		{
			A_Pain();
			if(GetCvar("HQEffects"))
			{
				A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,85);
				A_SpawnItemEx("LostSoulBlueFlare",-8,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION);	
			}
		}
		Goto See;
	Pain.StunBomb:
		"####" "#" 0 { bNoPain = true; }
		"####" "#######" 5 A_SpawnItemEx("StunBombSpark",0,random(10,-10),random(10,50),0,frandom(2,-2),frandom(0.1,2),0,SXF_NOCHECKPOSITION);
		"####" "#######" 5 A_SpawnItemEx("StunBombSpark",0,random(10,-10),random(10,50),0,frandom(2,-2),frandom(0.1,2),0,SXF_NOCHECKPOSITION);
		"####" "#######" 5 A_SpawnItemEx("StunBombSpark",0,random(10,-10),random(10,50),0,frandom(2,-2),frandom(0.1,2),0,SXF_NOCHECKPOSITION);
		"####" "#######" 5 A_SpawnItemEx("StunBombSpark",0,random(10,-10),random(10,50),0,frandom(2,-2),frandom(0.1,2),0,SXF_NOCHECKPOSITION);
		"####" "#######" 5 A_SpawnItemEx("StunBombSpark",0,random(10,-10),random(10,50),0,frandom(2,-2),frandom(0.1,2),0,SXF_NOCHECKPOSITION);
		"####" "#" 0 { bNoPain = false; }
		Goto See;
	Death.Massacre:
		TNT1 A 0 A_GiveInventory("DeathByMassacre", 1);
		Goto Death;
	Death.SkullExplo:	
		PSUL A 0 
		{
			if (bFRIENDLY)
			{	
				int dmg = random(50,150);
				A_DamageTarget(dmg,"Melee",DMSS_AFFECTARMOR);
				
				if (master && master.FindInventory("PowerDrain",true) != null)
				{
					A_DamageMaster(-(dmg * 0.05));
				}
			} //if lost soul is friendly, deals more damage
			else
			{	A_Explode(random(30,50),64,XF_NOTMISSILE);	}
		}
	Death:
		PSUL D 0 
		{
			bThruActors = true;
			A_Scream();
			A_NoBlocking();
			if (A_CheckSight("Null"))
			{
				Destroy();
				return;
			}
			if (!CountInv("DeathByMassacre"))
			{
				if(GetCvar("HQEffects"))
				{
					A_SpawnItemEx("LostSoulExploParticle",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION);
					A_SpawnItemEx("LostSoulExploParticle",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION);
					A_SpawnItemEx("LostSoulExploParticle",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION);
				}
				A_SpawnItemEx("LostSoulExploParticle",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("LostSoulExploParticle",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION);
			}
		}
		TNT1 A 2 Light("BlueMediumFlicker1");
		TNT1 A 2 Light("BlueMediumFlicker2");
		TNT1 A 2 Light("BlueMediumFlicker3");
		TNT1 A 2 Light("BlueSmallFlicker1");
		TNT1 A 2 Light("BlueSmallFlicker2");
		TNT1 A 2 Light("BlueSmallFlicker3");
		Stop;
	}
}







//*********************************************
// 
Class LostSoulExploParticle : Actor 
{
	Default 
	{
		+NOINTERACTION
		Alpha 0.6;
		Scale 0.4;
		RenderStyle "Add";
		Alpha 0.7;
		Translation "0:255=%[0,0,0]:[0.4,0.4,1.5]";
		+FORCEXYBILLBOARD
	}
	States
	{
	Spawn:
		FX58 ABCDEFGHIJKLMNOPQ 1 BRIGHT NODELAY
		{
			A_FadeOut(0.035);
			A_SetScale(Scale.X+0.075);
		}
		Wait;
	}
}




Class LostSoulRedFlame2 : Actor 
{
	Default 
	{
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		Scale 0.3;
	}
	States
	{
	Spawn:
		TNT1 A 1;
		R031 PQRSTUVWXYZ 2 A_FadeOut(0.05);
		Wait;
	}
}
Class LostSoulRedFlame : Actor 
{
	Default 
	{
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		Alpha 0.8;
		XScale 0.6;
		YScale 0.4;
		Translation "0:255=%[0,0,0]:[2,0.2,0]";
	}
	States
	{
	Spawn:
		TNT1 A 1 NODELAY A_Jump(256,"Type1","Type2","Type3","Type4","Type5");
	Type1:
		TNT1 A 0 A_Jump(220,5,6,7,8,9,10);
		X052 ABCDEFGHIJKLMNOPRS 1 Bright 
		{
			A_SetScale(Scale.X-0.03,Scale.Y-0.03);
			A_FadeOut(0.05);
		}
		Stop;
	Type2:
		TNT1 A 0 A_Jump(220,5,6,7,8,9,10);
		X053 ABCDEFGHIJKLMNOPRSTU 1 Bright 
		{
			A_SetScale(Scale.X-0.03,Scale.Y-0.03);
			A_FadeOut(0.05);
		}
		Stop;
	Type3:
		TNT1 A 0 A_Jump(220,5,6,7,8,9,10);
		X054 ABCDEFGHIJKLMNOPRS 1 Bright 
		{
			A_SetScale(Scale.X-0.03,Scale.Y-0.03);
			A_FadeOut(0.05);
		}
		Stop;
	Type4:
		TNT1 A 0 A_Jump(220,5,6,7,8,9,10);
		X055 ABCDEFGHIJKLMNOPRS 1 Bright 
		{
			A_SetScale(Scale.X-0.03,Scale.Y-0.03);
			A_FadeOut(0.05);
		}
		Stop;
	Type5:
		TNT1 A 0 A_Jump(220,5,6,7,8,9,10);
		X056 ABCDEFGHIJKLMNOPR 1 Bright 
		{
			A_SetScale(Scale.X-0.03,Scale.Y-0.03);
			A_FadeOut(0.05);
		}
		Stop;
	}
}

Class LostSoulBlueFlame : LostSoulRedFlame
{
	Default 
	{
		Translation "0:255=%[0,0,0]:[0.7,0.9,2.0]";
	}
}

Class LostSoulRedFlare : Actor 
{
	Default 
	{
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		Alpha 0.8;
		Scale 0.9;
		Alpha 0.7;
		//Translation "0:255=%[0,0,0]:[2,0.5,0]"
	}
	States
	{
	Spawn:
		TNT1 A 2 NODELAY A_Jump(256,"Type1","Type2");
	Type1:
		PROJ DEFGHI 1 Bright;
		Stop;
	Type2:
		PROJ JKLMNO 1 Bright;
		Stop;
	}
}

Class LostSoulBlueFlare : LostSoulRedFlare
{
	Default 
	{
		Translation "0:255=%[0,0,0]:[0.7,0.9,2.0]";
	}
}


//================================================================================================================
//================================================================================================================
//
//	Regular Doom version 
//
//================================================================================================================
//================================================================================================================


Class D4OldLostSoulNoCash : D4AbstractMonster
{
	Default 
	{
		D4AbstractMonster.CrateDropAmount 1;
		D4AbstractMonster.CrateDropChance 2;
		Health 100;
		Radius 16;
		Height 56;
		Mass 50;
		Speed 8;
		Damage 3;
		PainChance 256;
		Monster;
		+FLOAT  +NOGRAVITY +MISSILEMORE +DONTFALL +NOICEDEATH
		AttackSound "skull/melee";
		PainSound "skull/pain";
		DeathSound "skull/death";
		ActiveSound "skull/active";
		RenderStyle "SoulTrans";
		Obituary "$OB_SKULL";
		
		PainChance "StunBomb", 255;
		
		//Cash drops = 1
		DropItem "C1";
	}
	States
	{
	Spawn:
		SKUL AB 10 BRIGHT A_Look();
		Loop;
	See:
		SKUL AB 6 BRIGHT D4Chase();
		Loop;
	Missile:
		SKUL C 10 BRIGHT A_FaceTarget();
		SKUL D 4 BRIGHT A_SkullAttack;
		SKUL CD 4 BRIGHT;
		Goto Missile+2;
	Pain:
		SKUL E 3 BRIGHT;
		SKUL E 3 BRIGHT A_Pain();
		Goto See;
	Death:
		SKUL F 6 BRIGHT;
		SKUL G 6 BRIGHT A_Scream();
		SKUL H 6 BRIGHT;
		SKUL I 6 BRIGHT A_NoBlocking();
		SKUL J 6;
		SKUL K 6;
		Stop;
		
	Pain.StunBomb:
		"####" "#" 0 { bNoPain = true; }
		"####" "#######" 5 A_SpawnItemEx("StunBombSpark",0,random(10,-10),random(10,50),0,frandom(2,-2),frandom(0.1,2),0,SXF_NOCHECKPOSITION);
		"####" "#######" 5 A_SpawnItemEx("StunBombSpark",0,random(10,-10),random(10,50),0,frandom(2,-2),frandom(0.1,2),0,SXF_NOCHECKPOSITION);
		"####" "#######" 5 A_SpawnItemEx("StunBombSpark",0,random(10,-10),random(10,50),0,frandom(2,-2),frandom(0.1,2),0,SXF_NOCHECKPOSITION);
		"####" "#######" 5 A_SpawnItemEx("StunBombSpark",0,random(10,-10),random(10,50),0,frandom(2,-2),frandom(0.1,2),0,SXF_NOCHECKPOSITION);
		"####" "#######" 5 A_SpawnItemEx("StunBombSpark",0,random(10,-10),random(10,50),0,frandom(2,-2),frandom(0.1,2),0,SXF_NOCHECKPOSITION);
		"####" "#" 0 { bNoPain = false; }
		Goto See;
	Death:
		SKUL F 3 Bright A_Stop();
		SKUL G 3 Bright A_Scream();
		SKUL H 3 Bright;
		TNT1 A 0 
		{
			{ bThruActors = true; }
			if(GetCvar("HQEffects"))
			{
				A_SpawnItemEx("LostSoulExploParticleOrange",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("LostSoulExploParticleOrange",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("LostSoulExploParticleOrange",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("LostSoulExploParticleOrange",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("LostSoulExploParticleOrange",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION);
			}
		}
		SKUL I 3 Bright A_NoBlocking();
		SKUL J 3;
		SKUL K 3;
		Stop;
	}
}


Class LostSoulExploParticleOrange : Actor 
{
	int user_counter;
	Default 
	{
		+NOINTERACTION
		Alpha 0.6;
		Scale 0.4;
		RenderStyle "Add";
		Alpha 0.7;
		Translation "0:255=%[0,0,0]:[1.4,0.4,0]";
		+FORCEXYBILLBOARD
	}
	States
	{
	Spawn:
		FX58 ABCDEFGHIJKLMNOPQ 1 BRIGHT NODELAY
		{
			A_FadeOut(0.035);
			A_SetScale(Scale.X+0.075);
		}
		Wait;
	}
}





//================================================================================================================
//================================================================================================================
//
//	Lost souls WITH CASH, these are the ones used in the spawners
//
//================================================================================================================
//================================================================================================================

Class D4LostSoul : D4LostSoulNoCash
{
	Default 
	{
		D4AbstractMonster.CrateDropAmount 1;
		D4AbstractMonster.CrateDropChance 4;
		Health 50;
		+DONTCORPSE
		//Cash drops = 10
		DropItem "C10";
	}
}

Class D4OldLostSoul : D4OldLostSoulNoCash
{
	Default 
	{
		D4AbstractMonster.CrateDropAmount 1;
		D4AbstractMonster.CrateDropChance 4;
		//Cash drops = 10
		DropItem "C10";
		+DONTCORPSE
	}
}