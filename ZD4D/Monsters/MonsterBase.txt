//===================================================================================
//===================================================================================
//
//	Chance to spawn demon rune
//  This value is "x". Formula to spawn any rune is 
//   "if(random(1,X) == 1)" 
//===================================================================================
//===================================================================================
enum RuneChances
{
	RuneChance_Knight = 18,
	RuneChance_Baron = 15,
	RuneChance_Rev = 18,
	RuneChance_Caco = 18,
	RuneChance_Fatso = 15,
	RuneChance_PainEl = 12,
	RuneChance_Cyber = 6,
	RuneChance_Master = 3,
	RuneChance_Arach = 15,
	RuneChance_Archie = 10,
}

Class D4AbstractMonster : Actor 
{
	Array<D4DNewBeamBase> Beams;
	Name PowerColor, ResurrectCvar;
	Class<DemonRuneBase> RuneItem;
	int RuneDropChance;
	D4DHandler Event;
	uint PrevWoundHealth;
	uint UpgradeDropChance, UpgradeDropAlways;
	uint CrateDropChance, CrateDropAmount, CrateDropAlways;
	uint CashAmount, CashChainsawPinata;
	uint CashMultiplier;
	bool ResurrectCheck;
	uint ResurrectCheckTimer;
	Name MOD;
	Actor Spawner;
	Color BC;
	bool HQ;
	transient CVar HighQuality, EnhancedChase, ExecuteCheck, StaggerCheck;
	
	Property ResurrectCvar: ResurrectCvar;
	Property PowerColor: PowerColor;
	Property Rune: RuneItem, RuneDropChance;
	property BC: BC;
	property UpgradeDropChance: UpgradeDropChance;
	property UpgradeDropAlways: UpgradeDropAlways;
	property CrateDropChance: CrateDropChance;
	property CrateDropAmount: CrateDropAmount;
	property CrateDropAlways: CrateDropAlways;
	property CashAmount: CashAmount;
	property CashChainsawFinisher: CashChainsawPinata;
	Default
	{
		D4AbstractMonster.ResurrectCvar '';
		D4AbstractMonster.PowerColor 'D4D';
		D4AbstractMonster.UpgradeDropChance -1;
		D4AbstractMonster.UpgradeDropAlways 0;
		D4AbstractMonster.CrateDropAmount 0;
		D4AbstractMonster.CrateDropChance 0;
		D4AbstractMonster.CrateDropAlways 0;
		D4AbstractMonster.CashAmount -1;
		D4AbstractMonster.CashChainsawFinisher -1;
		D4AbstractMonster.BC "Red";
		BloodColor "Red";
		DamageFactor "Crush", 15.0;
	}
	
	override void BeginPlay()
	{
		Super.BeginPlay();
		PrevWoundHealth = WoundHealth;
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
	//	ExecuteCheck = Cvar.FindCvar("D4D_ExecutionsON");
	//	StaggerCheck = Cvar.FindCvar("D4D_MonsterStagger");
		oldLeap = false;
	}
	
	override bool CanResurrect(Actor other, bool passive)
	{
		if (!passive)
		{
			if (ResurrectCvar == '')
				return true;
			
			Cvar check = Cvar.FindCVar(ResurrectCvar);
			return (check && check.GetBool());
		}
		return true;
	}
	
	void DoResurrect(int stage = 0)
	{
		if (stage == 0)
			bNOPAIN = true;
		else
		{
			PostResurrect();
			bNOPAIN = false;
		}
	}
	
	bool CheckResurrectable()
	{
		return (ResurrectCvar != '' && GetCvar(ResurrectCvar));
	}
	
	void AimHead(Actor mo)
	{
		if (!mo)	return;
		double zoff;
		zoff = (mo.player) ? mo.player.viewz : mo.GetCameraHeight();
		A_Face(mo, 0, 0, 0, 0, FAF_BOTTOM, zoff);
	}
	
	void A_StartBeam(
		Class<D4DNewBeamBase> BeamName, Vector3 Off = (0,0,0), int Count = 1,
		Class<Actor> PuffName = "D4BulletPuff", int dmg = 0, double ang = 0, double pit = 0,
		double dist = 8192)
	{
		Count = Max(1, Count);
		
		Vector3 x, y, z, origin;
		[x, y, z] = Matrix4.GetAxes(Pitch,Angle,Roll);
		origin = Vec2OffsetZ(0,0,MissileHeight) + Off.x * x + Off.y * y + Off.z * z;
		
		if (dist <= 0.0)	dist = 8192.0;
		Actor Puff = LineAttack(angle + ang, dist, pitch + pit, dmg, 'None', PuffName, LAF_NOINTERACT);
		for (int i = 0; i < Count; i++)
		{
			let Beam = D4DNewBeamBase(Spawn(beamname,origin));
			Beam.Angle = Angle;
			Beam.Pitch = Pitch;
			Beam.Target = self;
			Beam.Master = self;
			Beam.Tracer = Puff;
			Beam.OffX = Off.x;
			Beam.OffY = Off.y;
			Beam.OffZ = Off.z;
			Beam.Parent = null;
			Beams.Push(Beam);
		}
	}
	
	void A_UpdateBeamTracer(
		Class<Actor> PuffName = "D4BulletPuff", int dmg = 0, double ang = 0, double pit = 0,
		double dist = 8192)
	{
		int flags = LAF_NORANDOMPUFFZ;
		if (dmg <= 0)	flags |= LAF_NOINTERACT;
		Actor Puff = LineAttack(angle + ang, dist, pitch + pit, dmg, '', PuffName, flags);
		
		if (Beams.Size() < 1)	return;
		for (int i = 0; i < Beams.Size(); i++)
		{
			let mo = Beams[i];
			if (!mo)	continue;
			
			if (Puff)	mo.Tracer = Puff;
		}
	}
	
	void A_SetBeamTranslation(Name transname)
	{
		if (Beams.Size() < 1)	return;
		for (int i = 0; i < Beams.Size(); i++)
		{
			if (Beams[i]) Beams[i].A_SetTranslation(transname);
		}
	}
	
	void A_StopBeam(Sound StopSound = "")
	{
		if (Beams.Size() > 0)
		{
			A_StopSound(CHAN_WEAPON);
			A_PlaySound(StopSound, CHAN_WEAPON);
			for (int i = 0; i < Beams.Size(); i++)
			{
				let mo = Beams[i];
				if (mo)	mo.Die(null, null);
			}
			Beams.Clear();
		}
	}
	
	//--------------------------------------------------------------------------
	// SetupPowerLevel
	// 
	// Randomizes and sets up the power level with the help of the overridable
	// function, ChoosePowerLevel. 
	// Customizable power level RNG that can be overridden for specific monsters.
	virtual int ChoosePowerLevel()
	{
		int rng = random(1, 2000);
		if (rng <= 1)	return 7;	
		if (rng <= 5)	return 6;	
		if (rng <= 10)	return 5;	
		if (rng <= 30)	return 4;	
		if (rng <= 60)	return 3;	
		if (rng <= 100)	return 2;	
		if (rng <= 200)	return 1;
		return 0;
	}
	int SetupPowerLevel(int amt = -1, bool nochange = false)
	{	
		int PrevAmt = CountInv("D4MonsterUpgrader");
		if (nochange) amt = PrevAmt;
		else if (amt < 0) amt = ChoosePowerLevel();
		
		A_SetInventory("D4MonsterUpgrader",Max(0,amt));
			
		if (amt > 0)
		{
			int hp = GetSpawnHealth();
			int amp = amt + 1;
			if (hp < 1000)	amp += 1;
			if (hp <= 500) 	amp += 1;
			if (hp <= 400) 	amp += 1;
			if (hp <= 300) 	amp += 1;
			if (hp <= 200) 	amp += 1;
			if (hp <= 100) 	amp += 1;
			A_SetHealth(hp * amp);
			Mass = Default.Mass * (amt + 1);
		}
		Name suffix = PowerColor;
		Switch(amt)
		{
			Default: 
			{
				if (PrevAmt > 0 && amt < 1)
				{
					Translation = Default.Translation;
					BC = Default.BC;
				}
				return amt;
			}
			Case 1:	suffix = suffix.."_Green";	BC = "Green";	break;
			Case 2: suffix = suffix.."_Blue";	BC = "Blue";	break;
			Case 3: suffix = suffix.."_Purple";	BC = "Purple";	break;
			Case 4: suffix = suffix.."_Orange";	BC = "Orange";	break;
			Case 5: suffix = suffix.."_Red";	BC = "Red";		break;
			Case 6: suffix = suffix.."_Black";	BC = "Black";	break;
			Case 7:	suffix = suffix.."_White";	BC = "White";	break;
		}
		A_SetTranslation(suffix);
		return amt;
	}
	
	void PostResurrect()
	{
		SetupPowerLevel(-1, true);
		if (bFRIENDLY)
		{
			for (int i = 0; i < MAXPLAYERS; i++)
			{
				if (!playeringame[i]) continue;
				let plr = players[i].mo;
				if (!plr || plr.CountInv("D4MonsterUpgrader")) continue;
				
				if (plr is "PlayerArchvile")
				{
					species = plr.species;
					bTHRUSPECIES = true;
					bMTHRUSPECIES = true;
				}
			}
		}
		else
		{
			Species = Default.Species;
			bTHRUSPECIES = false;
			bMTHRUSPECIES = false;
		}
	}
	
	//--------------------------------------------------------------------------
	// Checks to see if HQ monster effects cvar is on or off.
	bool CheckEffects()
	{
		if (!HighQuality)	HighQuality = CVar.FindCvar("HQEffects");
		return (HighQuality && HighQuality.GetBool());
	}
	
	// Do not check for staggering if the monster dies or still has health above
	// the threshold.
	override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		// Don't run this if dead or not shootable.
		if (health < 1 || (!bSHOOTABLE && !bVULNERABLE))	return 0;
		
		// Do nothing further if no wound state is found, let the engine handle it instead.
		let WoundState = ResolveState("Wound");
		if (!WoundState)	return Super.DamageMobj(inflictor, source, damage, mod, flags, angle);
		
		// We have a wound state to deal with. Temporarily remove the wound health
		// threshold and do the damage. Then restore it and check to see if under
		// that threshold. Otherwise, the internal engine would often bug out
		// and constantly make enemies go to the wound state and this instigates
		// a LOT of quirkiness.
		if (InStateSequence(CurState, ResolveState("StagLooper")))	
			flags |= DMG_NO_PAIN;
		
		int temp = WoundHealth;		WoundHealth = -1;
		damage = Super.DamageMobj(inflictor, source, damage, mod, flags, angle);
		WoundHealth = temp;
		
		if (Health > 0 && Health <= WoundHealth && damage > 0)
		{	
			// If not already in the looping state, go to the initializing state.
			if (GetCvar("D4D_ExecutionsON") && GetCvar("D4D_MonsterStagger"))
			{
				WoundHealth = PrevWoundHealth;
				if (!InStateSequence(CurState, ResolveState("StagLooper")))
					SetState(WoundState);
			}
			else
			{
				PrevWoundHealth = WoundHealth;
				WoundHealth = -1;
			}
		}
		return damage;
	}
	
	override void Deactivate(Actor activator)
	{
		A_StopBeam();
		Super.Deactivate(activator);
	}
	
	override void Die(Actor source, Actor inflictor, int dmgflags)
	{
		SpawnRune();
		A_StopBeam();
		bTHRUACTORS = true;
		if (Spawner && Spawner.health > 0)
		{
			Spawner.bVULNERABLE = true;
			Spawner.A_Die();
		}
		Super.Die(source, inflictor, dmgflags);
		
		if (DamageTypeReceived == 'Crush')
		{
			let next = FindState("XDeath", true);
			if (next)	SetState(next);
		}
	}
	
	// Don't let removing scripts leave gaps.
	override void OnDestroy()
	{
		if (health > 1)
		{
			if (Spawner && Spawner.health > 0)
			{
				Spawner.bVULNERABLE = true;
				Spawner.A_Die();
			}
		}
		A_StopBeam();
		Super.OnDestroy();
	}
	
	//--------------------------------------------------------------------------
	//
	// D4Chase(melee, missile, flags, max turn)
	//
	// A_Chase enhanced, + new parameter: Max turn.
	// Specifies how much in degrees the monster should turn towards its target.
	// -1 will not turn it.
	//--------------------------------------------------------------------------
	void D4Chase(statelabel melee = "Melee", statelabel missile = "Missile", int flags = 0, double maxturn = 30.0)
	{
		int newflags = 0;
		
		if (!EnhancedChase)
		{
			EnhancedChase = Cvar.FindCvar("D4D_EnhancedChase");
		}
		
		if (EnhancedChase && EnhancedChase.GetBool())
		{
			// Get a target if invalid.
			
			if (!Target || Target.bKilled)
			{
				A_Chase(null, null);
				return;
			}
			
			if (Target && Target.Health > 0 && CheckSight(Target,SF_IGNOREVISIBILITY)) // Use regular A_Chase if cannot see.
			{
				bool good = false;
				double ang = angle;
				A_Face(Target, Max(0.01, maxturn), 0, flags: FAF_MIDDLE);
				if (Target)
				{
					if (bFLOAT)
					{	
						
						double c = cos(Pitch) * Speed;
						double s = -sin(Pitch) * Speed;
						Vector3 old = pos;
						Vector2 new = Vec2Angle(c, angle);
						
						SetZ(pos.z + s);
						if (TryMove(new, bDROPOFF))
							good = true;
						else SetXYZ(old);
						
					}
					else
					{
						Vector2 nmpos = Vec2Angle(speed,angle);
						if (TryMove(nmpos, bDROPOFF))
							good = true;
					}
					
					// Move was good. Do the chase but without actually moving. 
					if (good)
					{
						newflags = CHF_DONTMOVE|CHF_DONTTURN|CHF_NODIRECTIONTURN|CHF_NOPOSTATTACKTURN;
						if (maxturn <= 0.0)
							angle = ang;
					}	
					else
						angle = ang;
				}
			}
		}
		A_Chase(melee, missile, flags|newflags);
	}
	
	//==========================================================================
	//
	// SpawnRune
	//
	// Spawns the rune after calling RuneChance, 
	//==========================================================================
	
	void SpawnRune()
	{
		if (!RuneItem || RuneDropChance < 1)
			return;
			
		if (RuneChance(RuneDropChance))
		{
			let Rune = DemonRuneBase(Spawn(RuneItem, pos, ALLOW_REPLACE));
			Class<Inventory> Token = "D4MonsterUpgrader";
			int Count = CountInv(Token);
			if (Rune && Token && Count > 0)
			{
				Rune.PowerLevel = Count;
			}
		}
	}
	
	int RuneChance(int id)
	{
		int rarity = int(GetCvar("D4D_DemonRunes"));
		if (rarity < 1)	return 0;
		
		int upgrade = CountInv("D4MonsterUpgrader");
		
		// Black and white monsters always drop demon runes.
		if (upgrade >= 6)	return 1;
		int originalID = id;
		id = Max(1, id - (upgrade * 2));
		// Spider masterminds are the rarest of enemies. Give them special 
		// treatment so players can enjoy playing the hell out of them.
		if (self is "D4SpiderMastermind" || self is "D4OldSpiderMastermind" || 
			self is "D4Cyberdemon" || self is "D4OldCyberdemon")
		{
			// Only check for live ones.
			// The event handler automatically transfers dead monsters from the
			// Monsters to DeadMonsters array inside of it.
			int howmany = 1;
			if (!Event)	Event = D4DHandler.Fetch();
			if (Event)
			{
				Array<Actor> Monsters; Monsters.Copy(Event.Monsters);
				for (int i = 0; i < Monsters.Size(); i++)
				{
					Actor mo = Monsters[i];
					if (mo && (mo is GetClass()) && mo.CountInv("D4MonsterUpgrader") == upgrade)
					{
						if (howmany++ >= originalID)
							break;
					}
				}
			}
			
			return (random(1, Max(1, howmany / rarity)) == 1) ? 1 : 0;
		}
		/*
		if (self is "D4SpiderMastermind" || self is "D4OldSpiderMastermind")
		{
			int howmany = Max(1,CountProximity("D4SpiderMastermind",32767) + CountProximity("D4OldSpiderMastermind",32767) + 1);
			howmany -= upgrade;
			howmany = Clamp(howmany,1,RuneChance_Master);
			if (rarity == 2)
			{
				return (random(1,howmany) == 1) ? 1 : 0;
			}
			else if (rarity == 1)
			{
				return (random(1,howmany * 2) == 1) ? 1 : 0;
			}
		}
		else if (CheckClass("D4Cyberdemon") || CheckClass("D4OldCyberdemon"))
		{
			int howmany = Max(1,CountProximity("D4Cyberdemon",32767) + CountProximity("D4OldCyberdemon",32767) + 1);
			howmany -= upgrade;
			howmany = Clamp(howmany,1,RuneChance_Cyber);
			if (rarity == 2)
			{
				return (random(1,(howmany / 2)) == 1) ? 1 : 0;
			}
			else if (rarity == 1)
			{
				return (random(1,howmany) == 1) ? 1 : 0;
			}
		}
		*/
		else if (rarity == 2)		// Common
		{
			return (random(1, (id / 2)) == 1) ? 1 : 0;
		}
		else if (rarity == 1) 	// Rare
		{
			return (random(1, id) == 1) ? 1 : 0;
		}
		return 0;
	}
	
	//==========================================================================
	//
	// XDeathGush(int bcolor)
	//
	// Splatters the enemy. Accept 0, Color_Green, Color_Blue or Color_Black
	// for blood colors on the gib dropper.
	//==========================================================================
	
	void XDeathGush(int bcolor = 0, bool simple = false)
	{
		MOD = DamagetypeReceived;
		if ((MOD != 'Massacre'))
		{
			
			if (simple)
			{
				//A_SpawnItemEx("D4BloodCloud",random(15,-15),random(15,-15),random(20,40),0,0,0,0,CloudFlags);
				let cloud = D4BloodCloud(Spawn("D4BloodCloud", pos + (0,0, height * 0.5), ALLOW_REPLACE));
				if (cloud)
				{
					cloud.tracer = self;
					cloud.Translation = (CountInv("D4MonsterUpgrader") ? Translation : BloodTranslation);
				}
			}
			else
			{
				let gibs = BloodGibDropper(Spawn("BloodGibDropper", pos));
				
				if (gibs)
				{
					gibs.tracer = self;
					gibs.vel = vel;
					int tier = CountInv("D4MonsterUpgrader");
					int toSend = (tier) ? Translation : BloodTranslation;
					gibs.BC = BC;
					gibs.Translation = ((tier) ? Translation : BloodTranslation);
				}
			}
			if (!simple)	A_XScream();
			A_PlaySound("Doom4/Other/GibExplode",CHAN_AUTO);
		}
	}
	
	//==========================================================================
	// Leaping
	//==========================================================================
	enum EJumpFlags
	{
		JF_FORWARD = (1<<0),
		JF_UP = (1<<1),
		
		JF_ALL = JF_FORWARD|JF_UP
	}
	
	

	int oldLeap;
	double leapDistance;

	
	
	double GetGroundHeight()
	{
		double _floorz = GetZAt();
		bool ok; Actor pOnmobj;
		[ok, pOnmobj] = TestMobjZ(true);
		if (!pOnmobj) return _floorz;
		double _actorz = pOnmobj.pos.z + pOnmobj.height;
		return max(_floorz, _actorz);
	}
	
	// [MC] Do not call subfunctions. Graf has made it specifically clear they're
	// quite speed hindering.
	bool CheckOnGround()
	{
		bool ok; Actor pOnmobj;
		
		if (vel.z != 0)	return false;	// Remember, moving up and down lifts is not velocity.
		double _floorz = GetZAt();
		
		[ok, pOnmobj] = TestMobjZ(true);
		if (pOnmobj) return true;
		double _actorz = pOnmobj.pos.z + pOnmobj.height;
		double GroundHeight = max(_floorz, _actorz);
		return (abs(GroundHeight - pos.z) < 1);
	}
	
	double A_PrepareDrop()
	{
		vector2 aimbak = (angle, pitch);
		A_FaceTarget(0,0);
		angle = aimbak.x;
		pitch = aimbak.y;
		
		double targdist = (target ? Distance3D(target) : 4096);
		vector2 leadoffset;
		
		if (targdist > 2000) 
			leadoffset = (frandom(-2.,2),frandom(-1.,1.));
		else 
			leadoffset = (deltaangle(aimbak.x, aimbak.x), deltaangle(aimbak.y, aimbak.y)) * targdist * frandom(0.055,0.067);

		angle += leadoffset.x;
		pitch += leadoffset.y;
		return targdist;
	}

	void A_DropAdjust(class<actor> MissileType, class<actor> MissileType2,double offX, double offY, double offZ,
						double Distance = 0, double speedmult = 1.0, double gravity = -1.0, actor target=null)
	{
		if (!target) target = self.target;
		if (!target) return;
		double ceil = CeilingZ - pos.z;
		
		double tarDist = A_PrepareDrop();
		if (ceil < 80 || tarDist > 1000)
		{
			A_SpawnProjectile(MissileType2, offZ);
			return;
		}
		let defs = GetDefaultByType(MissileType);
		if (Distance < 1) 
			Distance = Max(1, (target ? Distance2D(target) : 1));
		if (Gravity < 0.0) 
			Gravity = defs.Gravity;
			
		double NewSpeed = defs.Speed * speedmult;
		if (defs.Gravity && Distance > NewSpeed)
		{
			int TicDistance = Distance / NewSpeed;
            int DropAmount = 0;
            for (int i = 1; i <= TicDistance; i++)
			{
                DropAmount += i;
            }
            pitch -= min(atan(DropAmount * Gravity / Distance), 30);
        }

        //because we don't shoot from height 32 but 42
        if (Distance > 0) Pitch += atan(10 / Distance);
		
		A_DropProjectile(missiletype,offX,offY,offZ,tarDist);
    }
	
	void A_DropProjectile(class<actor> missiletype, double offX, double offY, double offZ, double targdist)
	{
		A_SpawnItemEx(
			missiletype, offX, offY, offZ,
			cos(pitch) * offY, 0, -sin(pitch) * offY,
			atan(offY / targdist),
			flags: SXF_NOCHECKPOSITION|SXF_SETTARGET|SXF_TRANSFERPITCH
		);
	}
	
	/*
	void A_Leap(StateLabel EndState = "See")
	{
		// if grounded, go to see.
		if (CheckOnGround())
		{
			if (!oldLeap)
			{
				A_FaceTarget();
				double vertical = 12;
				if (target.pos.z-pos.z > 64 && ceilingz-pos.z > 128) vertical = 18;
				double horizontalMult = leapDistance / 480;
				double horizontal = 1;
				for (int i = 0; i < 4; i++) horizontal *= horizontalMult; // pow is not defined
				vector3 newdir = (cos(angle)*horizontal, sin(angle)*horizontal, vertical);
				vel += newdir;
				oldLeap = frame;
			}
			else
			{
				State next = ResolveState(EndState);
				if (!next)
					next = ResolveState("See");
				if (next)
					SetState(next);
				return;
			}
		}
		else if (vel.z > 0)
		{
			vector3 newdir = (cos(angle)*2, sin(angle)*2, 0);
			vel += newdir;
			//frame = oldLeap+1;
		}
		//else if (vel.z < 0)	frame = oldLeap+2;
		
	}
	
	virtual double GetJumpDistance()
	{
		double dst = 480;
		if (ceilingz-pos.z > 128) dst *= 2;
		return dst;
	}

	bool CheckPitBeforeTarget(in out double dst)
	{
		if (!target || !CheckSight(target)) return false;
		
		int step = int(Max(1,radius/2));
		vector3 CheckDirection = (target.pos-pos).Unit()*step;
		
		// Get the jump distance.
		//dst = 480;
		if (ceilingz-pos.z > 128) dst *= 2;
		
		int steps = min(Distance2D(target), dst)/step;
		double curz = pos.z;
		SetOrigin((pos.x, pos.y, pos.z+64), false);
		for (int i = 0; i < steps; i++)
		{
			double zat = GetZAt(pos.x + CheckDirection.x * i, pos.y + CheckDirection.y * i, 0, GZF_ABSOLUTEPOS|GZF_ABSOLUTEANG);
			//A_LogFloat(zat);
			if (curz - zat > MaxStepHeight * 2 || zat - curz > MaxStepHeight)
			{
				SetOrigin((pos.x, pos.y, curz), false);
				return true;
			}
		}
		
		SetOrigin((pos.x, pos.y, curz), false);
		return false;
	}

	bool TryLeap(StateLabel leapstate = null, int flags = 0)
	{
		if (!target) return false;
		
		double BaseDistance = 480;
		double PassDistance = BaseDistance;
		
		if (CheckOnGround() && // if we are standing
			// !random(0, 16) && // and we don't do this all the time
			(((flags & JF_UP) && (abs(target.pos.z-pos.z) > Default.MaxStepHeight)) || // and target has more height difference than maxstepheight
			 ((flags & JF_FORWARD) && CheckPitBeforeTarget(PassDistance)))) // and there's a deep pit in front of us
		{
			if (Distance2D(target) < PassDistance) // and target is within max jump radius
			{
				oldLeap = 0;
				leapDistance = Distance2D(target);
				
				// A leapstate is not required -- one can use it for simple checking.
				if (leapstate)
				{
					State next = FindState(leapstate, true);
					if (next)	SetState(next);
				}
				return true;
			}
		}
		
		return false;
	}
	*/
}


Class D4AbstractBiggie : D4AbstractMonster {}
Class D4AbstractBoss : D4AbstractBiggie 
{
	Default
	{
		DamageFactor "BFGBlast", 0.0;
	}
}


Class PainSoundToken : Inventory
{}

// debug actor
Class DecoTest : Actor
{
	Default
	{
		+NOINTERACTION
		Scale 2.0;
		alpha 0.4;
	}
	
	States
	{
	Spawn:
		TROO A 2 Bright;
		Stop;
	}
}



//==================================================================================================
//	Wound health values, just for reference
// 
// MONSTER NAME       HEALTH    WOUND    |     HEALTH-D4D   WOUND-D4D    |  HEAL TO
// Zombieman            20       10      |        20          10         |     15          
// ShotgunGuy           30       10      |        30          10         |     15   
// ChaingunGuy          70       30      |        70          30         |     40   
// DoomImp              60       25      |        60          25         |     30    
// Demon/spectre       150       50      |       150          50         |     60   
// LostSoul            N/A      N/A      |       N/A         N/A         |    N/A   
// Cacodemon           300       80      |       400          80         |    100          
// PainElemental       400      100      |       400         100         |    101
// Revenant            300       80      |       400          80         |    100
// HellKnight          500      100      |       500         100         |    130
// BaronOfHell        1000      150      |      1000         150         |    180
// Fatso               600      110      |       800         120         |    130
// Arachnotron         500      100      |       500         100         |    130	
// Archvile            700      120      |      1000         150         |    130 - 180
// Cyberdemon         4000      150      |      4000         150         |    201
// SpiderMastermind   3000      150      |      3000         150         |    201
//
//==================================================================================================

//===================================================================================
//	Base actor from which they inherit
//===================================================================================
Class HighlightBase : Actor
{
	int MainCounter;
	int woundvalue;
	int RecoverToAmount;
	int StgrPainCounter;
	int StaggerLength;
	bool Highlight;
	double TimeMultiplier;
	Property TimeMultiplier: TimeMultiplier;
	Property RecoverToAmount: RecoverToAmount;
	Default
	{
		HighlightBase.TimeMultiplier 1.0;
		HighlightBase.RecoverToAmount 15;
		+NOINTERACTION
		RenderStyle "Add";
		Scale 1.1;
		Translation "0:255=%[0.2,0.2,0.2]:[1.5,0.75,0.0]";
		Alpha 0.4;
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		StaggerLength = int((double(GetCvar("D4D_StaggerLenght")) * 35.0) * TimeMultiplier);
		Highlight = GetCvar("D4D_ExecutionHighlight");
		if (Master)
			bSPRITEFLIP = Master.bSPRITEFLIP;
	}
	
	override void OnDestroy()
	{
		if (Master)
		{
			Master.A_SetInventory("FinisherToken",0);	// Ends the effect
			if (MainCounter >= StaggerLength && Master.Health > 0)
				Master.A_SetHealth(RecoverToAmount);	// Heals up
		}
		Super.OnDestroy();
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		Goto Looper;
	Looper:
		---- A 1 Bright //by default, is invisible
		{
			if (!Master || Master.Health < 1 || MainCounter >= StaggerLength)
				return Destroy();
			
			if (Highlight)	//if the highlight is ON
				A_CopySpriteFrame(MasterPtr,0);		//copies the sprite
			
			Warp(Master,1,0,0,0,WARPF_NOCHECKPOSITION);
			
			if ((MainCounter % 35) <= 0) //if the stagger just started or one second has passed
				Master.A_GiveInventory("PainSoundToken",1);  //makes the parent actor play pain sound
				
			MainCounter++;	//increases the main counter
		}
		Loop;
	}
}


//===================================================================================
//	Zombieman & ShotgunGuy
//===================================================================================
Class ZombieHighlight : HighlightBase
{
	Default
	{
		HighlightBase.RecoverToAmount 15;
		Scale 1.05;
	}
}

//===================================================================================
//	ChaingunGuy
//===================================================================================
Class ChaingunnerHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 40;
		Scale 1.1;
	}
}

//===================================================================================
//	Imp
//===================================================================================
Class ImpHighlight : HighlightBase
{
	Default
	{
		HighlightBase.RecoverToAmount 30;
		Scale 1.1;
	}
}

//===================================================================================
//	Demon & spectre
//===================================================================================
Class DemonHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 60;
		Scale 0.765;
	}
}
Class OldDemonHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 60;
		Scale 1.1;
	}
}

//===================================================================================
//	Cacodemon
//===================================================================================
Class CacoHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 100;
		Scale 1.1;
	}
}
//===================================================================================
//	Pain Elemental
//===================================================================================
Class D4DPainElementalHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 101;
		Scale 1.1;
	}
}
Class OldPainElementalHighlight : D4DPainElementalHighlight	{}

//===================================================================================
//	Revenant
//===================================================================================
Class D4DRevenantHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 100;
		Scale 0.95;
	}
}
Class OldRevenantHighlight : D4DRevenantHighlight
{
	Default
	{	
		Scale 1.1;
	}
}

//===================================================================================
//	Hell knight
//===================================================================================
Class D4DHellKnightHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 130;
		Scale 0.69;
	}
}
Class OldHellKnightHighlight : D4DHellKnightHighlight
{
	Default
	{	
		Scale 1.1;
	}
}

//===================================================================================
//	Baron Of Hell
//===================================================================================
Class D4DBaronHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 180;
		Scale 0.77;
	}
}
Class BaronHighlight : D4DBaronHighlight
{
	Default
	{	
		Scale 1.1;
	}
}
//===================================================================================
//	Both Fatso variations
//===================================================================================
Class D4DFatsoHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 130;
		Scale 1.2;
	}
}
Class OldFatsoHighlight : D4DFatsoHighlight
{
	Default
	{	
		Scale 1.1;
	}
}
//===================================================================================
//	Arachnotron
//===================================================================================
Class ArachnoHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 130;
		Scale 1.1;
	}
}

//===================================================================================
//	Archvile
//===================================================================================
Class D4DArchHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 180;
		Scale 1.1;
	}
}
Class OldArchHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 130;
		Scale 1.1;
	}
}

//===================================================================================
//	Cyberdemon
//===================================================================================
Class CyberdemonHighlight : HighlightBase
{
	Default
	{	
		HighlightBase.RecoverToAmount 201;
		HighlightBase.TimeMultiplier 0.5;
		Scale 1.1;
	}
}

//===================================================================================
//	Mastermind
//===================================================================================
Class MastermindHighlight : CyberdemonHighlight	{}

//==============================================================================
// Rainbow Doom
// 
// Concept borrowed with permission from Cactus Hege's Colorful Hell.
// Developed by Major Cooke
//==============================================================================

Class D4MonsterUpgrader : Inventory
{
	Default
	{
		Inventory.MaxAmount MaxRunePower;
	}
}


Class D4StealthArachnotron : D4Arachnotron { Default { +STEALTH } }
Class D4StealthArchvile : D4Archvile { Default { +STEALTH } }
Class D4StealthBaronOfHell : D4BaronOfHell { Default { +STEALTH } }
Class D4StealthHellKnight : D4HellKnight { Default { +STEALTH } }
Class D4StealthCacoDemon : D4CacoDemon { Default { +STEALTH } }
Class D4StealthChaingunGuy : D4ChaingunGuy { Default { +STEALTH } }

Class D4StealthSergeantShield : D4SergeantShield { Default { +STEALTH } }
Class D4StealthDemon : D4Demon { Default { +STEALTH } }
Class D4StealthDoomImp : D4DoomImp { Default { +STEALTH } }
Class D4StealthFatsoTech : D4FatsoTech { Default { +STEALTH } }
Class D4StealthFatsoHell : D4FatsoHell { Default { +STEALTH } }
Class D4StealthRevenant : D4Revenant { Default { +STEALTH } }
Class D4StealthShotgunGuy : D4ShotgunGuy { Default { +STEALTH } }
Class D4StealthZombieMan : D4ZombieMan { Default { +STEALTH } }