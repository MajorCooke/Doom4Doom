const CHAN_POWERPICKUP = 15;
const CHAN_POWEREFFECT = 17;
const CHAN_POWEREXPIRE = 16;

mixin class D4PowerupShared
{
	meta Name	Light;
	Sound 		ExpiringSound, StartSound;
	String 		ExpiringColor;
	double 		FadeAlpha;
	int 		FadeDuration;
	
	Property StartSound: StartSound;
	Property Expiring: ExpiringSound, ExpiringColor, FadeAlpha, FadeDuration;
	Property Light: Light;
	void DoStartUp()
	{
		if (Owner)
		{
			if (StartSound != "")
				Owner.A_StartSound(StartSound, CHAN_POWERPICKUP, CHANF_OVERLAP);
			if (Light != '' && Light != 'None')
				Owner.A_AttachLightDef(Light, Light);
		}
	}
	
	void DoEnd()
	{
		if (Owner && Light != '' && Light != 'None')
			Owner.A_RemoveLight(Light);
	}
	
	void ExpireWarning()
	{
		double vol = 0.0;
		Switch (EffectTics)
		{
			Case 105:	vol = 1.0;	break;
			Case 70:	vol = 0.75;	break;
			Case 35:	vol = 0.5;	break;
			Default:	break;
		}
		if (vol > 0.0)
		{
			if (ExpiringSound != "")
				Owner.A_StartSound(ExpiringSound, CHAN_POWEREXPIRE, CHANF_OVERLAP, vol);
			if (FadeDuration > 0 && FadeAlpha > 0.0)
				Owner.A_SetBlend(Color(ExpiringColor), FadeAlpha, FadeDuration);				
		}
	}
	
	override void OnDestroy()
	{
		DoEnd();
		Super.OnDestroy();
	}
}

//=======================================================================
//=======================================================================
// MegaHealth
//=======================================================================
//=======================================================================
Class D4MegaSphereReplacer : RandomSpawner //replaces Megasphere
{
	Default 
	{
		DropItem "D4Megasphere", 	255, 6;
		DropItem "D4DoomDoll", 		255, 1;
		DropItem "D4ExtraLife",		255, 1;
	}
}

Class D4BlueArmorMegasphere : ArmorBonus //replaces BlueArmorForMegasphere 
{
	Default 
	{
		+INVENTORY.IGNORESKILL
		Armor.SavePercent 100;
		Armor.SaveAmount 200;
		Armor.MaxAbsorb 200;
		Armor.MaxFullAbsorb 200;
		DistanceCheck "D4D_MaxDrawDistance";
	}
}

Class D4Megasphere : Megasphere 
{
	int bonus;
	property AllowBonus: bonus;
	Default 
	{
		D4Megasphere.AllowBonus 1;
		Inventory.PickUpSound "Doom4/Pickups/MegaHealth";
		Inventory.PickupMessage "Megacharge";
		Translation "0:255=%[0.3,0.15,0.05]:[2,1,0.3]";
		+INVENTORY.FANCYPICKUPSOUND
		+INVENTORY.TRANSFER
		-Inventory.ALWAYSPICKUP
		+INVENTORY.BIGPOWERUP
		+COUNTITEM
		+BRIGHT
		+FLOATBOB
		+ROLLSPRITE
		+ROLLCENTER
		+FORCEXYBILLBOARD
		Scale 0.17;
		RenderStyle "Translucent";
	}
	
	States
	{
	Spawn:
		M680 D 1 Light("MegaSphereLightWorld") 
		{
			if (!A_CheckSight("Null"))
			{
				A_SpawnItemEx("PentaShadow",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION);
			}
			A_SetRoll(roll + 1,SPF_INTERPOLATE);
		}
		Wait;
	Pickup:
		TNT1 A 0 
		{
			let plr = PlayerPawn(self);
			if (!plr)	return false;
			
			int maxhp = Max(plr.GetMaxHealth() + stamina,200);
			if (health < maxhp)
				A_SetHealth(Clamp(health, health + 200, maxhp));
			
			if (!inv)
			{
				if (A_GiveInventory("ArmorStripper",1))
					A_GiveInventory("D4BlueArmorMegasphere",1);
			}
			else
			{
				A_GiveInventory("D4BlueArmor200",1);
			}
				
			if (!invoker.bonus)	return true;
			let item = plr.FindInventory("D4MegasphereDamageReducer");
			if (CountInv("D4MegachargeUpgrade1") && (!item || item.Amount < item.MaxAmount))
			{
				A_GiveInventory("D4MegasphereDamageReducer",1);
			}
			if (CountInv("D4MegachargeUpgrade2"))
			{
				let inv = PowerTimeFreezer(FindInventory("PowerTimeFreezer", true));
				if (inv)
				{
					if (inv.GetClass() != "D4TimeFreeze")
					{
						inv.EndEffect();
						A_GiveInventory("D4TimeFreeze",1);
					}
					else
						inv.EffectTics = 35 * 5;
				}
				else
					A_GiveInventory("D4TimeFreeze",1);
			}
				
			return true;
		}
		Stop;
	}
}

Class D4MegaSphereNoBonus : D4MegaSphere 
{
	Default
	{
		D4MegaSphere.AllowBonus 0;
	}
}
Class D4MegasphereDamageReducer : Inventory 
{
	Default 
	{
		+INVENTORY.UNTOSSABLE;
		+INVENTORY.UNDROPPABLE;
		Inventory.MaxAmount 50;
		Inventory.InterhubAmount 50;
	}
}

Class D4TimeFreezeGiver : PowerupGiver
{
	Default
	{
		+INVENTORY.NOSCREENBLINK
		Powerup.Type "D4TimeFreeze";
		Powerup.Duration -5;
	}
}

Class NoTimeFreezeToken : Inventory { }

Class D4TimeFreeze : PowerTimeFreezer
{
	Default
	{
		Powerup.Duration -5;
	}
	
	static void FreezeEverything()
	{
		let it = ThinkerIterator.Create("Actor", Thinker.STAT_DEFAULT);
		Actor mo;
		while (mo = Actor(it.Next()))
		{
			if (!mo || mo is 'MCBeam' || mo is 'D4DBeamBase' || mo is 'MCBeam' || mo is 'PentaShadow')
				continue;
				
			if (mo.player)
				continue;
				
			if (mo.bNOTIMEFREEZE)
			{
				mo.SetInventory("NoTimeFreezeToken",1);
				mo.bNOTIMEFREEZE = false;
			}
		}
	}
	
	static void UnfreezeEverything()
	{
		let it = ThinkerIterator.Create("Actor", Thinker.STAT_DEFAULT);
		Actor mo;
		while (mo = Actor(it.Next()))
		{
			if (mo && mo.CountInv("NoTimeFreezeToken"))
			{
				mo.SetInventory("NoTimeFreezeToken",0);
				mo.bNOTIMEFREEZE = true;
			}
		}
	}
	
	override void InitEffect()
	{
		D4TimeFreeze.FreezeEverything();
		Super.InitEffect();
	}
	
	override void EndEffect()
	{
		D4TimeFreeze.UnfreezeEverything();
		Super.EndEffect();
	}
	
	override void DoEffect()
	{
		Super.DoEffect();
		Level.SetFrozen(true);
	}
}

//==============================================================================
// Extra Life
//==============================================================================

Class D4ExtraLife : Inventory
{
	Default 
	{
		Inventory.PickUpSound "Doom4/Pickups/MegaHealth";
		Inventory.PickupMessage "Extra life!";
		Inventory.MaxAmount 1000000;
		Inventory.InterhubAmount 1000000;
		Translation "0:255=%[0.05,0.3,0.05]:[0.3,2.0,0.3]";
		+INVENTORY.FANCYPICKUPSOUND
		+INVENTORY.TRANSFER
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.UNDROPPABLE
		+INVENTORY.UNTOSSABLE
		+INVENTORY.BIGPOWERUP
		+INVENTORY.NEVERRESPAWN //CHEATER CHEATER PUMPKIN TWEETER
		+COUNTITEM
		+BRIGHT
		+FLOATBOB
		+ROLLSPRITE
		+ROLLCENTER
		+FORCEXYBILLBOARD
		Scale 0.17;
		RenderStyle "Translucent";
	}
	States
	{
	Spawn:
		M680 D 1 Light("ExtraLifeLightWorld") 
		{
			A_SpawnItemEx("PentaShadow",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION);
			A_SetRoll(roll + 1,SPF_INTERPOLATE);
		}
		Wait;
	}
}

Class D4ExtraLife2 : D4ExtraLife
{	Default	{	Inventory.Amount 2;	}	}

Class LifeRedFlash : Inventory
{
	Default
	{
		Inventory.MaxAmount 1000000;
		Inventory.InterhubAmount 0;
	}
}

//==============================================================================
//==============================================================================
// Radsuit
//==============================================================================
//==============================================================================

Class PowerRadSuit : PowerProtection
{
	Default 
	{
		DamageFactor "PlasmaPool",			0.1;
		DamageFactor "PlasmaExploder",		0.5;
		DamageFactor "Plasma", 				0.75;
		DamageFactor "CyberdemonFireWave",	0.9;
		DamageFactor "Slime",				0.0;
		RadiusDamageFactor					0.9;
		Powerup.Duration -60;
	}
}

Class D4RadSuit : CustomInventory //replaces RadSuit
{
	Default 
	{
		Scale 0.85;
		+COUNTITEM
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.FANCYPICKUPSOUND
		Inventory.PickupMessage "Radiation Suit";
		Inventory.PickupSound "misc/p_pkup";
	}
	
	States
	{
	Spawn:
		SUIT A -1 NoDelay A_SetAngle(random(0,359));
		Stop;
	Pickup:
		TNT1 A 0
		{
			A_GiveInventory("D4RadBoots",1);
			A_GiveInventory("PowerRadSuit",1);
		}
		Stop;
	}
}

Class D4RadBoots : RadSuit { States { Spawn: TNT1 A -1; Stop; } }

//=======================================================================
//=======================================================================
// Allmap
//=======================================================================
//=======================================================================
Class D4AllMap : CustomInventory //AllMap //replaces AllMap
{
	Default 
	{
		Scale 0.6;
		+COUNTITEM
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.FANCYPICKUPSOUND
		+INVENTORY.BIGPOWERUP
		Inventory.PickupMessage "$GOTMAP";
		Inventory.PickupSound "misc/p_pkup";
	}
	
	override void PostBeginPlay()
	{
		angle = random(0,359);
		Super.PostBeginPlay();
	}
	
	States
	{
	Spawn:
		PMAP ABCDCB 6 Bright;
		Loop;
	Pickup:
		TNT1 A 0
		{
			A_GiveInventory("MapScanner",1);
			A_GiveInventory("Allmap",1);
		}
		Stop;
	}
}

Class MapScanner : PowerupGiver //replaces Scanner
{
	Default 
	{
		+INVENTORY.AUTOACTIVATE
		+INVENTORY.ALWAYSPICKUP
		+DONTGIB
		Powerup.Type "PowerScanner";
		Powerup.Duration 0x7FFFFFFD;
	}
}

//=======================================================================
//=======================================================================
// light amp
//=======================================================================
//=======================================================================
Class D4Infrared : Infrared //replaces Infrared
{
	Default 
	{
		Scale 0.6;
	}
	States
	{
	Spawn:
		PVIS A 0 Bright NODELAY A_SetAngle(random(0,359));
		PVIS A 6 Bright;
		PVIS B 6;
		Goto Spawn+1;
	}
}


//=======================================================================
//=======================================================================
// Quad Damage
//=======================================================================
//=======================================================================

Class D4BerserkSpawner : RandomSpawner //replaces Berserk
{
	Default 
	{
		DropItem "D4DBerserk", 255, 10;
		DropItem "D4QuadDamage", 255, 5;
	}
	
	override void PostSpawn(Actor spawned)
	{
		if (spawned is "D4DBerserk")
		{
			for (int i = 0; i < MAXPLAYERS; i++)
			{
				if (!playeringame[i])	continue;
				let plr = players[i].mo;
				if (!plr)	continue;
				
				if (plr.CountInv("D4QuadUpgrade1") > 0)
				{
					D4DHandler.ConvertBerserkToQuad(D4DBerserk(spawned));
					return;
				}
			}
		}
	}
}

Class PowerD4QuadDamage : PowerDamage
{
	mixin D4PowerupShared;
	Inventory Penta;
	int DelayTimer;
	bool hasMelee, hasMissile, PentaDamage;
	Default 
	{
		PowerD4QuadDamage.Expiring "QuadDamage/Warning", "Purple", 0.4, 12;
		PowerD4QuadDamage.Light "QuadLight";
		DamageFactor "Normal", 4;
	}
	
	override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive)
	{
		if (!passive && damage > 0)
		{
			newdamage = max(1, ApplyDamageFactors(CheckPentaDamage(), damageType, damage, damage * 4));
		//	if (Owner != null && newdamage > damage) Owner.A_PlaySound(ActiveSound, CHAN_AUTO, 1.0, false, ATTN_NONE);
		}
	}
	
	override void InitEffect()
	{
		Super.InitEffect();
		if (Owner)
		{
			DoStartUp();
			hasMelee = (Owner.MeleeState != null);
			hasMissile = (Owner.MissileState != null);
			Penta = Owner.FindInventory("D4QuadUpgrade2");
		}
	}
	
	Class<Inventory> CheckPentaDamage()
	{
		if (Owner)
		{
			if (!Penta)
				Penta = Owner.FindInventory("D4QuadUpgrade2");
			
			if (Penta && Penta.Amount > 0)
				return Penta.GetClass();
		}
		return GetClass();
	}
	
	override void DoEffect()
	{
		if (!Owner || Owner.Health < 1)
		{	EndEffect();	return;	}
		
		if (IsFrozen())
		{
			EffectTics++; //'Freeze' the timer.
		}
		else
		{
			SetOrigin(Owner.pos,false);
			
			if (DelayTimer < 1)
			{
				if ((hasMelee && InStateSequence(Owner.CurState, Owner.MeleeState)) ||
					(hasMissile && InStateSequence(Owner.CurState, Owner.MissileState)))
				{
					DelayTimer = 36;
					Owner.A_StartSound("QuadDamage/Firing",CHAN_POWEREFFECT,CHANF_OVERLAP, 0.5);
				}
			}
			else DelayTimer--;
			
			if (EffectTics <= 105)
				ExpireWarning();
		}
		Super.DoEffect();
	}
	
	override void EndEffect()
	{
		DoEnd();
		Super.EndEffect();
	}
}

Class QuadDamageGiver : PowerupGiver
{
	Default 
	{
		Inventory.MaxAmount 0;
		Powerup.Type "PowerD4QuadDamage";
		Powerup.duration -30;
		+INVENTORY.AUTOACTIVATE
		+INVENTORY.ALWAYSPICKUP
	}
}

Class D4QuadDamage : CustomInventory
{
	Default 
	{
		Inventory.PickupSound "QuadDamage/Pickup";
		Inventory.PickupMessage "Quad Damage!";
		+INVENTORY.FANCYPICKUPSOUND
		+INVENTORY.BIGPOWERUP
		+COUNTITEM
		+BRIGHT
		+FLOATBOB
		Scale 0.17;
		RenderStyle "Translucent";
	}
	states
	{
	Spawn:
		M680 B 1 Light("QuadDamageLightWorld") NoDelay
		{
			if (!A_CheckSight("Null"))
				A_SpawnItemEx("PentaShadow",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
		}
		Loop;
	Pickup:
		TNT1 A 0
		{	
			if (CountInv("D4QuadUpgrade1"))
				A_GiveInventory("D4DBerserk",1);
			let inv = PowerD4QuadDamage(FindInventory("PowerD4QuadDamage"));
			if (inv)
			{
				A_StartSound(invoker.PickupSound, CHAN_POWERPICKUP, CHANF_OVERLAP);
				inv.EffectTics += 1050;
				return;
			}
			A_GiveInventory("QuadDamageGiver", 1);
		}
		Stop;
	}
}


//=======================================================================
//=======================================================================
// Regeneration
//=======================================================================
//=======================================================================

Class SoulSphereHP : SoulSphere 
{ 
	Default 
	{ 
		+INVENTORY.IGNORESKILL 
		Inventory.Amount 100;
	}
}

Class SoulSphereHPUpgraded : SoulSphere 
{ 
	Default 
	{ 
		+INVENTORY.IGNORESKILL 
		Inventory.Amount 150;
		Inventory.MaxAmount 300;
	}
}

Class PowerD4Regen : PowerRegeneration 
{
	mixin D4PowerupShared;
	int GiverTic;
	bool switcharoo;
	Default 
	{
		PowerD4Regen.Expiring "Regen/Warning", "Cyan", 0.4, 12;
		PowerD4Regen.Light "RegenLight";
		+INVENTORY.NOSCREENBLINK;
		Inventory.InterhubAmount 0; 
	}
	
	void GiveHealth()
	{
		if (Owner)
		{
			if (Owner.FindInventory("PowerMorph",true) || !Owner.CountInv("D4RegenUpgrade4"))	
				Owner.GiveBody(int(Strength));
			else
				Owner.A_GiveInventory("D4HealthBonusUpgraded");
		}
	}
	
	override void InitEffect()
	{
		Super.InitEffect();
		DoStartUp();
	}
	
	override void DoEffect()
	{
		if (!Owner || Owner.Health < 1)
		{	EndEffect();	return;	}
		
		if (Owner.CountInv("D4RegenUpgrade3"))
			GiverTic = 15;
		else
			GiverTic = 31;
			
		if (!(level.time & GiverTic))
		{
			if (Owner.CountInv("D4RegenUpgrade2"))
			{
				Owner.A_GiveInventory("D4ArmorBonus");
				GiveHealth();
			}
			else if (Owner.CountInv("D4RegenUpgrade1") && Owner.health >= (100 + Owner.stamina))
				Owner.A_GiveInventory("D4ArmorBonus");
			else
				GiveHealth();
		}
		
		if (IsFrozen())					EffectTics++; //'Freeze' the timer.
		else if (EffectTics <= 105)		ExpireWarning();
	}
	
	override void EndEffect()
	{
		DoEnd();
		Super.EndEffect();
	}
}

Class RegenerationGiver : PowerupGiver
{
	Default 
	{
		Inventory.MaxAmount 0;
		Powerup.Type "PowerD4Regen";
		Powerup.Duration 1050;
		+INVENTORY.AUTOACTIVATE
		+INVENTORY.ALWAYSPICKUP
	}
}

Class D4SoulSphereReplacer : RandomSpawner //replaces SoulSphere
{
	Default
	{
		DropItem "D4Regeneration",	255, 20;
		DropItem "D4ExtraLife",		255, 1;
	}
}

Class D4Regeneration : CustomInventory
{
	Default 
	{
		Inventory.PickupMessage "Supercharge!";
		Inventory.PickupSound "Regen/Pickup";
		translation "0:255=%[.30,.30,.50]:[.10,1.99,1.99]";
		+INVENTORY.FANCYPICKUPSOUND
		+INVENTORY.BIGPOWERUP
		+COUNTITEM
		+BRIGHT
		+FLOATBOB
		+ROLLSPRITE
		+ROLLCENTER
		+FORCEXYBILLBOARD
		Scale 0.17;
		RenderStyle "Translucent";
	}
	states
	{
	Spawn:
		M680 D 1 Light("RegenLightWorld") 
		{
			A_SpawnItemEx("PentaShadow",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL|SXF_TRANSFERSPRITEFRAME);
			A_SetRoll(roll + 1,SPF_INTERPOLATE);
		}
		Wait;
	Pickup:
		TNT1 A 0
		{
			if (CountInv("D4RegenUpgrade4"))	A_GiveInventory("SoulSphereHPUpgraded",1);
			else 								A_GiveInventory("SoulSphereHP",1);
			
			let inv = PowerD4Regen(FindInventory("PowerD4Regen"));
			if (inv)
			{
				A_StartSound(invoker.PickupSound, CHAN_POWERPICKUP, CHANF_OVERLAP);
				inv.EffectTics += 1050;
				return;
			}
			A_GiveInventory("RegenerationGiver", 1);
		}
		Stop;
	}
}

//=======================================================================
//=======================================================================
// Quad Money
//=======================================================================
//=======================================================================

Class PowerD4Money : Powerup 
{
	mixin D4PowerupShared;
	int GiverTic;
	bool switcharoo;
	Default 
	{
		PowerD4Money.Expiring "Regen/Warning", "Green", 0.4, 12;
		PowerD4Money.Light "MoneyLight";
		+INVENTORY.NOSCREENBLINK;
	}
	
	
	override void InitEffect()
	{
		Super.InitEffect();
		if (!Owner)	return;
		
		Owner.A_GiveInventory("CashPercentBonusTemp",300);
		DoStartUp();
	}
	
	override void DoEffect()
	{
		if (!Owner || Owner.Health < 1)
		{	EndEffect();	return;	}

		if (IsFrozen())					EffectTics++; //'Freeze' the timer.
		else if (EffectTics <= 105)		ExpireWarning();
	}
	
	override void EndEffect()
	{
		if (Owner) 
		{
			Owner.A_TakeInventory("CashPercentBonusTemp",300);
			DoEnd();
		}
			
		Super.EndEffect();
	}
}

Class QuadMoneyGiver : PowerupGiver
{
	Default 
	{
		Powerup.Type "PowerD4Money";
		Powerup.duration -30;
		+INVENTORY.AUTOACTIVATE
		+INVENTORY.ALWAYSPICKUP
	}
}

Class D4QuadMoney : CustomInventory
{
	Default 
	{
		Inventory.PickupSound "Regen/Pickup";
		Inventory.PickupMessage "Quad Credits!";
		Translation "0:255=%[.00,.00,.10]:[.10,.99,.10]";
		+INVENTORY.FANCYPICKUPSOUND
		+INVENTORY.BIGPOWERUP
		+COUNTITEM
		+BRIGHT
		+FLOATBOB
		+ROLLSPRITE
		+ROLLCENTER
		+FORCEXYBILLBOARD
		Scale 0.17;
		RenderStyle "Translucent";
	}
	states
	{
	Spawn:
		M680 C 1 Light("QuadMoneyLightWorld") 
		{
			if (!A_CheckSight("Null"))
			{
				A_SpawnItemEx("PentaShadow",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL|SXF_TRANSFERSPRITEFRAME);
			}
			A_SetRoll(roll + 1,SPF_INTERPOLATE);
		}
		Wait;
	Pickup:
		TNT1 A 0
		{			
			let inv = PowerD4Money(FindInventory("PowerD4Money"));
			if (inv)
			{
				inv.EffectTics += 1050;
				A_StartSound(invoker.PickupSound, CHAN_POWERPICKUP, CHANF_OVERLAP);
				return;
			}
			A_GiveInventory("QuadMoneyGiver", 1);
		}
		Stop;
	}
}


//=======================================================================
//=======================================================================
// Haste
//=======================================================================
//=======================================================================

Class D4BlurSphere : BlurSphere {}
Class PowerD4Invis : PowerInvisibility {}
Class PowerD4DFS : PowerDoubleFiringSpeed {}
Class PowerD4Haste : PowerSpeed
{
	mixin D4PowerupShared;
	int Timer;
	PowerDoubleFiringSpeed DFS;
	PowerD4Invis Invis;
	
	Default
	{
		PowerD4Haste.Light "HasteLight";
		PowerD4Haste.Expiring "Haste/Warning", "Yellow", 0.4, 12;
	}
	
	override void InitEffect()
	{
		Super.InitEffect();
		
		if (!Owner || Owner.Health < 1)
			return;
		
		DoStartUp();
		
		let pwr = FindInventory("PowerDoubleFiringSpeed", true);
		if (!pwr)
		{
			Owner.A_GiveInventory("PowerD4DFS");
			DFS = PowerDoubleFiringSpeed(Owner.FindInventory("PowerD4DFS"));
		}
		else DFS = PowerDoubleFiringSpeed(pwr);
		
		if (DFS)
		{
			if (DFS is "PowerD4DFS")
				DFS.EffectTics = EffectTics;
		}
		
		if (Owner.CountInv("D4HasteUpgrade1"))
		{
			Owner.A_GiveInventory("PowerD4Invis");
			Invis = PowerD4Invis(Owner.FindInventory("PowerD4Invis"));
		}
	}
	
	override void DoEffect()
	{
		if (!Owner || Owner.Health < 1)
		{	EndEffect();	return;	}
		
		// Ensure the owner has the double firing speed at all times when haste
		// is active, and invisibility if the upgrade is found.
		if (!DFS)	
		{
			DFS = PowerDoubleFiringSpeed(Owner.FindInventory("PowerDoubleFiringSpeed", true));
			if (!DFS)		
			{
				let pwr = Owner.FindInventory("PowerDoubleFiringSpeed",true);
				if (!pwr)
					Owner.A_GiveInventory("PowerD4DFS");
			}
		}
		else if (DFS is "PowerD4DFS")
			DFS.EffectTics = EffectTics;
		
		
		if (Owner.CountInv("D4HasteUpgrade1") > 0)
		{
			if (!Invis)	
			{
				Invis = PowerD4Invis(Owner.FindInventory("PowerD4Invis"));
				if (!Invis)			Owner.A_GiveInventory("PowerD4Invis");
			}
			else		Invis.EffectTics = EffectTics;
		}
		
		if (Owner.CountInv("D4HasteUpgrade2"))
			Speed = 2.0; //Internal default is 1.5.
		
		
		if (IsFrozen())
			EffectTics++; //'Freeze' the timer.
		else
		{
			SetOrigin(Owner.pos, false);
			if (EffectTics > 105)
			{
				// Whispering from Quake's Ring of Shadows
				if (Timer-- < 1)
				{
					if (random(1,15) == 1)
					{	// Intentionally not playing it on the owner.
						A_StartSound("Haste/Whisper",CHAN_POWEREFFECT,CHANF_OVERLAP);
						Timer = random(35 * 2, 35 * 10);
					}
				}
			}
			else 
			{
				A_StopSound(CHAN_POWEREFFECT);
				ExpireWarning();
			}
		}
		
		Super.DoEffect();
	}
	
	override void EndEffect()
	{
		if (Owner)
		{
			DoEnd();
			let inv = PowerD4DFS(Owner.FindInventory("PowerD4DFS"));
			if (inv) inv.EndEffect();
			
			let in2 = PowerD4Invis(Owner.FindInventory("PowerD4Invis"));
			if (in2)	in2.EndEffect();
		}
		Super.EndEffect();
	}
}


Class D4HasteGiver : PowerupGiver
{
	Default 
	{
		Powerup.Type "PowerD4Haste";
		Powerup.Duration -30;
		+INVENTORY.AUTOACTIVATE
		+INVENTORY.ALWAYSPICKUP
	}
}

Class D4DoubleFiringSpeedGiver : D4HasteGiver
{	Default {	Powerup.Type "PowerD4DFS";	}}

Class D4Haste : CustomInventory //replaces BlurSphere
{
	Default 
	{
		Inventory.PickupSound "Haste/Pickup";
		Inventory.PickupMessage "Haste!";
		Translation "0:255=%[.00,.00,.10]:[.99,.99,.10]";
		+INVENTORY.FANCYPICKUPSOUND
		+INVENTORY.BIGPOWERUP
		+COUNTITEM
		+BRIGHT
		+FLOATBOB
		+FORCEXYBILLBOARD
		Scale 0.17;
		RenderStyle "Translucent";
	}
	states
	{
	Spawn:
		M680 E 1 Light("HasteLightWorld") 
		{
			if (!A_CheckSight("Null"))
				A_SpawnItemEx("PentaShadow",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL|SXF_TRANSFERSPRITEFRAME);
		}
		Wait;
	Pickup:
		TNT1 A 0
		{
			let inv = PowerD4Haste(FindInventory("PowerD4Haste"));
			if (inv)
			{
				inv.EffectTics += 1050;
				A_StartSound(invoker.PickupSound, CHAN_POWERPICKUP, CHANF_OVERLAP);
				return;
			}
			A_GiveInventory("D4HasteGiver", 1);
			A_GiveInventory("D4DoubleFiringSpeedGiver", 1);
		}
		Stop;
	}
}

//==============================================================================
//==============================================================================
// Pentagram of Protection (Invulnerability)
//==============================================================================
//==============================================================================

Class PentaShadow : Actor 
{
	Default 
	{
		+NOINTERACTION
		+ROLLSPRITE
		+ROLLCENTER
		+NOTIMEFREEZE
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		"####" "#" 1 A_FadeOut(0.2);
		Wait;
	}
}

Class D4PentagramOfProtection : CustomInventory //replaces InvulnerabilitySphere
{
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.BIGPOWERUP
		+INVENTORY.FANCYPICKUPSOUND
		+DONTGIB
		+FLOATBOB
		+BRIGHT
		+ROLLSPRITE
		+ROLLCENTER
		+FORCEXYBILLBOARD
		Inventory.PickupMessage "Pentagram Of Protection";
		Inventory.PickupSound "PentagramOfProtection/Pickup";
		Inventory.MaxAmount 0;
		Inventory.InterhubAmount 0;
		Scale 0.17;
	}
	States
	{
	Spawn:
		M680 A 1 Light("InvulLightWorld") 
		{
			if (!A_CheckSight("Null"))
				A_SpawnItemEx("PentaShadow",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL|SXF_TRANSFERSPRITEFRAME);
			
			A_SetRoll(roll + 1,SPF_INTERPOLATE);
		}
		Wait;
	Pickup:
		TNT1 A 0
		{
			let inv = PowerInvulBase(FindInventory("PowerInvulBase"));
			if (inv)
			{
				inv.EffectTics += 1050;
				A_StartSound(invoker.PickupSound, CHAN_POWERPICKUP, CHANF_OVERLAP);
				return;
			}
			A_GiveInventory("Invulnerability");
		}
		Stop;
	}
}

Class Invulnerability : PowerupGiver
{	
	Default 
	{
		Inventory.InterhubAmount 0;
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.AUTOACTIVATE
		+INVENTORY.QUIET
		+INVENTORY.ADDITIVETIME
		+INVENTORY.NOSCREENBLINK
		Powerup.Color "Gold", 0.15;
		Powerup.Type "PowerInvulBase";
		Powerup.Duration 1050;
	}
}

Class D4ExtraLifeProtection : PowerProtection { Default { Powerup.Duration -5; DamageFactor "Normal", 0.0; } }
Class Invul1s : Invulnerability { Default { Powerup.Duration -1;	} }
Class Invul3s : Invulnerability	{ Default {	Powerup.Duration 107;	} }
Class Invul5s : Invulnerability	{ Default {	Powerup.Duration -5;	} }
Class InvulEndMorph : Invul3s { }
Class PowerInvulBase : PowerInvulnerable
{
	mixin D4PowerupShared;
	const WaitTimer = 70;
	private uint PentaNoiseTimer;
	
	Default 
	{
		PowerInvulBase.StartSound "PentagramOfProtection/Pickup";
		PowerInvulBase.Expiring "PentagramOfProtection/Warning", "Gold", 0.45, 12;
		ActiveSound "PentagramOfProtection/Protect";
		+INVENTORY.NOSCREENBLINK;
		+INVENTORY.ADDITIVETIME
		Inventory.InterhubAmount 0; 
	}
	
	override void InitEffect()
	{
		Super.InitEffect();
		if (!Owner)	return;
		DoStartUp();
		if (Owner.CountInv("D4PentagramUpgrade1"))	Owner.bREFLECTIVE = true;
		if (Owner.CountInv("D4PentagramUpgrade4"))	Owner.bNODAMAGE = true;
		bNOINTERACTION = true;
		PentaNoiseTimer = 0;
		if (Owner.Player)	Owner.Player.DamageCount = 0;
	}
	
	override void DoEffect()
	{
		if (Owner)
		{
			if (PentaNoiseTimer > 0)	PentaNoiseTimer--;
			SetOrigin(Owner.pos, false);
			Prev = Owner.Prev;
			Vel = Owner.Vel;
			Owner.bINVULNERABLE = true;
			if (Owner.CountInv("D4PentagramUpgrade1"))	Owner.bREFLECTIVE = true;
			if (Owner.CountInv("D4PentagramUpgrade4"))	Owner.bNODAMAGE = true;
			if (IsFrozen())
				EffectTics++; //'Freeze' the timer.
			else if (EffectTics <= 105)
				ExpireWarning();
		}
		Super.DoEffect();
	}
	
	override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive)
	{
		if (passive && damage > 0)
		{
			newdamage = damage;
			if (Owner && PentaNoiseTimer < 1)
			{
				PentaNoiseTimer = WaitTimer;
				Owner.A_StartSound(ActiveSound, 15, CHANF_OVERLAP);
			}
		}
	}
	
	override void EndEffect()
	{
		if (Owner)	
		{
			if (Owner.Player)	Owner.Player.DamageCount = 0;
			Owner.bREFLECTIVE = false;
			Owner.bNODAMAGE = false;
			
			let plr = Doom4Player(Owner);
			if (plr)	plr.bUNMORPHINVUL = false;
			
			
			DoEnd();
		}
		Super.EndEffect();
	}
}
/*
Class PowerInvulNoStartSound : PowerInvulBase
{
	Default
	{
		PowerInvulBase.StartSound "";
	}
}

Class PowerInvulNoExpireSound : PowerInvulBase
{
	Default
	{
		PowerInvulBase.Expiring "", "Gold", 0, 0;
	}
}

Class PowerInvulNoStartExpireSound : PowerInvulBase
{
	Default
	{
		PowerInvulBase.Expiring "", "Gold", 0, 0;
		PowerInvulBase.StartSound "";
	}
}
*/