enum SpawnChances
{
	NoSpawnChanceProgressive =	150,
	NoSpawnChanceRandom = 		192,
};

//==============================================================================
//		Chainsaw
//==============================================================================
Class ChainsawSpawner : CustomInventory replaces Chainsaw	//Do not use randomspawner.
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			A_SpawnItemEx("D4Chainsaw",0,0,0,0,0,0,random(0,359),PFlags,0,tid); 
			
			if (GetCvar("MPWeapons") && !GetCvar("MPSpawnProgressive"))
			{
				A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,64);
			}
		}
		Stop;
	}
}

//===================================================================================================
//		Shotgun
//===================================================================================================
Class ShotgunSpawner : CustomInventory replaces Shotgun
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.Amount 0;
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			if (GetCvar("MPWeapons"))
			{
				if (GetCvar("MPSpawnProgressive"))	
				{
					if (CountInv("D4Shotgun",AAPTR_PLAYER1) && random(1,2)==2 && CountInv("D4StaticRifle",AAPTR_PLAYER1)==0)
					{	A_SpawnItemEx("D4StaticRifle",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION);	}
					else
					{	A_SpawnItemEx("D4Shotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
				}
				else
				{	
					A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);	
					A_SpawnItemEx("D4Shotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);
				}
			}
			else
			{	A_SpawnItemEx("D4Shotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
		}
		Stop;
	}
}

//===================================================================================================
//		Super Shotgun
//===================================================================================================
Class SuperShotgunSpawner : CustomInventory replaces SuperShotgun	//Do not use randomspawner.
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	//int rng;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.Amount 0;
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			if (GetCvar("MPWeapons"))
			{
				if (GetCvar("MPSpawnProgressive"))	
				{
					if (CountInv("D4SuperShotgun",AAPTR_PLAYER1) && random(1,2)==1)
					{	A_SpawnItemEx("D4VortexRifle",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION);	}
					else
					{	A_SpawnItemEx("D4SuperShotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
				}
				else	//if MP weps == "random"
				{	
					A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);	
					A_SpawnItemEx("D4SuperShotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);
				}
			}
			else
			{	A_SpawnItemEx("D4SuperShotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
		}
		Stop;
	}
}

//===================================================================================================
//		Chaingun
//===================================================================================================
Class ChaingunSpawner : CustomInventory replaces Chaingun	//Do not use randomspawner.
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	int rng;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			if (GetCvar("MPWeapons"))
			{
				//if MP weps == "Progressive"
				if (GetCvar("MPSpawnProgressive"))	
				{
					if (CountInv("D4AssaultRifle",AAPTR_PLAYER1))
					{	
						if(countinv("D4Repeater",AAPTR_PLAYER1))	//if player has both HAR and repeater, chaingun will appear rarely
						{
							//random from 1 to 7
							// 1,2 & 3 = HAR
							// 4 & 5 = repeater
							// 6 = chaingun
						
							invoker.rng = random(1,6);
							if 		(invoker.rng == 6) { A_SpawnItemEx("D4Chaingun",0,0,0,0,0,0,random(0,359),PFlags,0,tid); }
							
							else if (invoker.rng >= 1 && invoker.rng <= 3) 
									{ A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid); }
									
							else	{ A_SpawnItemEx("D4Repeater",0,0,0,0,0,0,random(0,359),PFlags,0,tid); }
							
						}
						else
						{
							if(random(1,3)==3)
							{	A_SpawnItemEx("D4Repeater",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
							else
							{	A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
						}
					}
					else
					{	
						A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	
					}
				}
				//if MP weps == "random"
				else	
				{	
					A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);	
					if (random(1,8) == 1)	{	A_SpawnItemEx("D4Chaingun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
					else					{	A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
				}
			}
			//if MP weps OFF
			else	
			{	
				if(countinv("D4AssaultRifle",AAPTR_PLAYER1))
				{
					if (random(1,4) == 1)	{	A_SpawnItemEx("D4Chaingun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
					else					{	A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
				}
				else
				{
					A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	
				}
			}
		}
		Stop;
	}
}

//===================================================================================================
//		Rocketlauncher
//===================================================================================================
Class RocketLauncherSpawner : CustomInventory replaces RocketLauncher
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			if (GetCvar("MPWeapons"))
			{
				Class<Inventory> wep;
				int rng = random[rng](0,1);
				Switch(rng)
				{
					Default:	wep = "D4RocketLauncher";	break;
					Case 1: 	wep = "D4GrenadeLauncher";	break;
				}
				A_SpawnItemEx(wep,0,0,0,0,0,0,random(0,359),PFlags,0,tid);
				A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);	//sometimes
			}
			//if MP weps OFF
			else
			{
				
				A_SpawnItemEx("D4RocketLauncher",0,0,0,0,0,0,random(0,359),PFlags,0,tid);
			}
		}
		Stop;
	}
}

//===================================================================================================
//		Plasma rifle
//===================================================================================================
Class PlasmaRifleSpawner : CustomInventory replaces PlasmaRifle
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			if (GetCvar("MPWeapons"))
			{
				//if MP weps == "Progressive"
				if (GetCvar("MPSpawnProgressive"))	
				{
					if (CountInv("D4PlasmaRifle",AAPTR_PLAYER1) && random(1,3)==1)
					{	A_SpawnItemEx("D4LightningGun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
					else
					{	A_SpawnItemEx("D4PlasmaRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
					A_SpawnItemEx("D4GaussCannon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);
				}
				//if MP weps == "random"
				else	
				{	
					A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);	
					A_SpawnItemEx("D4PlasmaRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);
					A_SpawnItemEx("D4GaussCannon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);
				}
			}
			//if MP weps OFF
			else
			{	
				A_SpawnItemEx("D4PlasmaRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);
				A_SpawnItemEx("D4GaussCannon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom,tid);
			}
		}
		Stop;
	}
}

//==============================================================================
//		BFG 9000
//==============================================================================
Class BFGSpawner : CustomInventory replaces BFG9000
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			A_SpawnItemEx("D4BFG",0,0,0,0,0,0,random(0,359),PFlags,0,tid); 
			
			if (GetCvar("MPWeapons"))
			{
				if (GetCvar("MPSpawnProgressive"))
				{
					//nothing
				}
				else
				{	A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,64);	}
			}
		}
		Stop;
	}
}

//==============================================================================
//		Multiplayer Weapon Spawner
//==============================================================================
Class D4MPWeapon : Actor 
{
	int rng;
	Default 
	{
		+NOINTERACTION
		+NOTELESTOMP
		+NOBLOCKMAP
		RenderStyle "None";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			rng = (CountInv("D4StaticRifle",AAPTR_PLAYER1) ? random(1,3) : random(1,4));
				 if (rng == 1)	{	A_SpawnItemEx("D4LightningGun",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);	}
			else if (rng == 2)	{	A_SpawnItemEx("D4VortexRifle",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);	}
			else if (rng == 3)	{	A_SpawnItemEx("D4Repeater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);		}
			else 					{	A_SpawnItemEx("D4StaticRifle",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);	}
		}
		Stop;
	}
}
