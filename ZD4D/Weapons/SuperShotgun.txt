//==============================================================================
//==============================================================================
//
//								Super Shotgun
//
//	Sprites: Neccronixis
//	MuzzleFlash sprite edit: DBThanatos
//	Original Code: DBThanatos
//	Code Overhaul + Layers: Major Cooke
//	Offset tweaks: Xaser
//
//==============================================================================
//==============================================================================
const PitchKick = 4.0;
Class D4SuperShotgunDuo : Inventory {	Default { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 2;		Inventory.InterhubAmount 2; } }

const SSGReloadVolume = 0.3;
Class D4SuperShotgun : Doom4Weapon //replaces SuperShotgun
{
	transient Cvar DT;
	enum SuperShotgun
	{
		DmgBase = 		5,
		DmgMulMin = 	1,
		DmgMulMax = 	3,
		FCMFlags =		FPF_NOAUTOAIM,
		GibRange =		512
	};
	const SpreadXY = 11.2;
	const SpreadZ = 7.1;
	
	Default 
	{
		Doom4Weapon.CvarMode 'D4D_DTMode', 'Single Mode', 'Double Mode';
		+WEAPON.NOAUTOFIRE
		+WEAPON.NOALERT
		+INVENTORY.UNDROPPABLE
		+INVENTORY.UNTOSSABLE
		Weapon.SlotNumber 1;
		Inventory.PickupMessage "Super Shotgun! Just like the old times!";
		Obituary "%o ate %k's double dose of buckshot for breakfast.";
		Tag "Super Shotgun";
		Weapon.AmmoType1 "D4Shells";
		Weapon.AmmoGive1 8;
		Weapon.AmmoUse1 1;
		Weapon.SelectionOrder 10;
		Weapon.SlotNumber 3;
		Weapon.SlotPriority 1.0;
		+DONTGIB
	}
	
	action Class<Inventory> ShotToken(int OID)
	{
		Class<Inventory> item = "SSRSecondShotToken"; 
		if (OID == LGun) item = "SSLSecondShotToken";
		return item;
	}
	
	action Class<Actor> ShellSide(int OID)
	{
		Class<Actor> shell = 		"SSGCasingSpawnerRight";
		if (OID == LGun) shell = 	"SSGCasingSpawnerLeft";
		return shell;
	}
	
	action void CheckDeath()
	{
		let i = invoker; let o = i.owner; if (!o || !o.player)	return;
		let p = o.player;
		if (p.health < 1)
		{
			A_Overlay(RGun, "Null");
			A_Overlay(LGun, "Null");
		}
	}
	
	States
	{
	Spawn:
		WPPK D -1;
		Stop;

	//First off, set up the overlays before raising it up. I'm going to use the
	//entire weapon as one massive overlay -- the actual listening layer you're
	//used to calling the weapon itself, is actually permanently invisible from
	//here on out.
	
	// Layer numbers that are off limits:
	// -1, 0, and 1. DO NOT USE A_OVERLAY ON THOSE LAYERS. EVER.
	// You have from
	// -2147483647
	//  to
	//  2147483644 [1]
	// layers at your disposal.
	
	// [1]-The very last three are also reserved. Don't use them.
	
	// How do they behave? Like layers in photoshop or gimp. The higher the
	// number, the higher the drawing priority. Easy enough to understand so far,
	// yes?
	
	// Now, first thing you'll want to do is, right after spawning the overlay,
	// give it flags. See http://zdoom.org/wiki/A_OverlayFlags for more information.
	// I've included some handy 'See Also' links at the bottom there too.
	Select:
		M666 A 0
		{
			if (CountInv("SSGUpgrade4"))						//Two shotguns?
			{
				//SSR = Super Shotgun Right
				//SSL = Super Shotgun Left
				A_SetInventory("SSLSecondShotToken",0);
				A_SetInventory("SSRSecondShotToken",0);
				A_Overlay(RGun,"SSR.Ready");
				A_Overlay(LGun,"SSL.Ready");							//Draw the second one.
			}
			else
			{
				A_Overlay(RGun,"SSN.Ready"); //Normal shotgun with hands.
			}
			A_OverlayFlags(LGun,WeapMoveBob,true);			//Make it follow the 'weapon'. See ZD4D/Constants.txt.
			A_OverlayFlags(RGun,WeapMoveBob,true);
		}
	// We should now have one or both shotguns raising up.
		---- A 1 A_Raise(WeaponSwitchSpeed);
		Wait;
	Deselect:
		---- A 0 
		{
			CheckDeath();
			A_Overlay(GunWatcher,"Null");
			A_Overlay(SwitchWatcher,"Null");
			A_SetInventory("SSLSecondShotToken",0);
			A_SetInventory("SSRSecondShotToken",0);
		}
	DesLoop:
		---- A 1 A_Lower;
		---- A 1 A_Lower(WeaponSwitchSpeed);
		Wait;
		
		
	Ready:
		M666 A 0 
		{
			A_Overlay(SwitchWatcher,"SwitchWatcher",true);
			if (CountInv("SSGUpgrade4"))
			{	
				A_Overlay(RGun,"SSD.Ready",true);
				A_Overlay(LGun,"SSD.Ready",true);
				A_OverlayFlags(LGun, PSPF_FLIP|PSPF_MIRROR, true);
				A_Overlay(GunWatcher,"SSG.Listen");
				return ResolveState("ReadyLoop"); 
			}
			return ResolveState(1);
		}
		DW4A A 0;
		Goto ReadyLoop;
	ReadyLoop:
		"----" A 1 
		{
			if (!CountInv("SSGUpgrade4"))
			{	A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWUSER2);	}
			else
			{	
			
				// Follow the weapon.
				A_OverlayFlags(LGun,WeapMove|PSPF_FLIP|PSPF_MIRROR,true);
				A_OverlayFlags(RGun,WeapMove,true);
				A_Overlay(GunWatcher,"SSG.Listen",true);
				
				bool SSL = (CountInv("SSLSecondShotToken") < 2);
				bool SSR = (CountInv("SSRSecondShotToken") < 2);
				A_OverlayFlags(LGun,WeapBob,SSL);
				A_OverlayFlags(RGun,WeapBob,SSR);	
				
				int WeapFlags = WRF_NOFIRE;
				if (!SSL && !SSR) 
					WeapFlags |= (CountInv("BusyToken")) ? WRF_NOSWITCH : WRF_ALLOWUSER2;
					
				A_WeaponReady(WeapFlags);
			}
		}
		Goto Ready;
	SwitchWatcher:
		M666 A 1
		{
			let plr = invoker.owner.player;
			if (plr)
			{
				if ((plr.cmd.buttons & BT_RELOAD) &&
					!(plr.oldbuttons & BT_RELOAD))
					ToggleWeaponMode();
			}
		}
		Loop;
	//==========================================================================
	//==========================================================================
	// The other states.
			
	SSN.Ready:
		DW4A A -1;
		Stop;
	SSR.Ready:
	SSD.Ready:
	SSL.Ready:
		M665 A -1
		{
			int OID = OverlayID();
			A_OverlayFlags(OID, PSPF_MIRROR|PSPF_FLIP, (OID == LGun));
		}
		Stop;
	//-------------------------------------------------------
	// Primary Fire
	//-------------------------------------------------------
	Fire:
		M666 A 1
		{
			if (!CountInv("SSGUpgrade4"))
			{
				A_Overlay(LGun,"Null");
				A_Overlay(RGun,"Null");
			}
			
			if (!A_JumpIfNoAmmo("Null")) 
			{	return ResolveState("FireSingle");	}
			return ResolveState("HoldNoRef");
		}
	Hold:
	HoldNoRef:
		M666 A 1;
		M666 A 0 A_Refire("HoldNoRef");
		M666 A 0 A_SetInventory("FireToken",0);
		Goto Ready;
	FireSingle:
		DW4A B 1 Bright
		{	
			uint ssr = CountInv("SSRSecondShotToken");
			if (CheckWeaponMode() || ssr > 0)
			{	
				A_Overlay(RGunFire,"SSG.FireBullets");
				int chan = (ssr) ? 7 : CHAN_WEAPON;
				A_PlaySound("Doom4/Weapon/SuperShotgun/Fire",chan);
				A_GiveInventory("SSRSecondShotToken",1);	ssr++;
			}
			else								
			{
				A_Overlay(RGunFire,"SSG.FireBullets");
				A_Overlay(RGunFire2,"SSG.FireBullets");
				A_PlaySound("Doom4/Weapon/SuperShotgun/Fire",CHAN_WEAPON);
				A_GiveInventory("SSRSecondShotToken",2);	ssr += 2;
			}
			
			if (ssr > 1)
			{
				StateLabel next = "NormalReload";
				if (CountInv("SSGUpgrade1"))
					next = "FastReload";
				return ResolveState(next);
			}
			A_WeaponOffset(12, 36);
			return ResolveState(1);
		}
	NoReload:
		DW4A B 1 Bright { A_WeaponOffset(24,39); A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY); A_SetTics(IsDoubleSpeed() ? 0 : 1);}
		DW4A B 1 Bright { A_WeaponOffset(32,46); A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY); }
		DW4A C 1        { A_WeaponOffset(20,41); A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY); A_SetTics(IsDoubleSpeed() ? 0 : 1);}
		DW4A C 1        { A_WeaponOffset(16,38); A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY); }
		DW4A C 1        { A_WeaponOffset( 8,36); A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY); A_SetTics(IsDoubleSpeed() ? 0 : 1);}
		DW4A D 1        { A_WeaponOffset( 4,34); A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY); }
		DW4A D 1        { A_WeaponOffset( 1,33); A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY); }
		DW4A D 1 A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
		Goto Ready;
		
		
	NormalReload:	//49 tics total, exactly as in v1.01.4
		DW4A B 0 A_Overlay(5000, "Overlay.ReloadSoundNormal");
		DW4A B 1 Bright { A_WeaponOffset(24,39); A_TakeInventory("SSRSecondShotToken",32767); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A B 1 Bright { A_WeaponOffset(32,46); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A C 1        { A_WeaponOffset(28,45); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);}
		DW4A C 1        { A_WeaponOffset(24,43); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A C 1        { A_WeaponOffset(20,41); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);}
		DW4A D 1        { A_WeaponOffset(18,39); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A D 1        { A_WeaponOffset(16,38); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);}
		DW4A E 1 { A_WeaponOffset(-2,3,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A E 1 { A_WeaponOffset(-2,3,WOF_ADD); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);}
		DW4A E 1 { A_WeaponOffset(-2,3,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A E 1 { A_WeaponOffset(-2,3,WOF_ADD); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);}
		DW4A F 2	//13
		{
			A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
			A_WeaponOffset(-2,2,WOF_ADD);
			if (D4D_Casings)
			{
				A_SpawnItemEx("SSGCasingSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
				A_SpawnItemEx("SSGCasingSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
			}
		}	
		DW4A GHIJ 2 { A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A KKLL 2 { A_WeaponOffset(-1,2,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A MMNN 2 { A_WeaponOffset(1,-2,WOF_ADD);	A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A O 2 { A_WeaponOffset(8,-2,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A O 2 { A_WeaponOffset(0,36); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A PP 1 { A_WeaponOffset(2,-2,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A P 1 { A_WeaponOffset(0,32); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A QQ 2 { A_WeaponOffset(3,3,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A A 1 { A_WeaponReady(WRF_NOFIRE); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		Goto Ready;
		
		

	FastReload:	//34 tics total, exactly as in v1.01.4
		DW4A B 0 A_Overlay(5001, "Overlay.ReloadSoundFast");
		DW4A B 1 Bright { A_WeaponOffset(24,39); A_TakeInventory("SSRSecondShotToken",32767); }
		DW4A B 1 Bright { A_WeaponOffset(32,46); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A C 1        { A_WeaponOffset(28,45); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);}
		DW4A C 1        { A_WeaponOffset(24,43); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A C 1        { A_WeaponOffset(20,41); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);}
		DW4A D 1        { A_WeaponOffset(18,39); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A D 1        { A_WeaponOffset(16,38); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);}
		DW4A E 1 		{ A_WeaponOffset(-4,6,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);}
		DW4A E 1		{ A_WeaponOffset(-4,6,WOF_ADD); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);}
		DW4A F 1	//10
		{
			A_WeaponOffset(-2,2,WOF_ADD);
			A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
			if (D4D_Casings)
			{
				A_SpawnItemEx("SSGCasingSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
				A_SpawnItemEx("SSGCasingSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
			}
		}
		
		DW4A GHIJ 2; // From 8 to 4
		DW4A K 1 { A_WeaponOffset(-1,2,WOF_ADD); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A K 1 { A_WeaponOffset(-1,2,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A L 1 { A_WeaponOffset(-1,2,WOF_ADD); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A L 1 { A_WeaponOffset(-1,2,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);}
		DW4A M 1 { A_WeaponOffset(1,-2,WOF_ADD); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A M 1 { A_WeaponOffset(1,-2,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A N 1 { A_WeaponOffset(1,-2,WOF_ADD); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A N 1 { A_WeaponOffset(1,-2,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A O 1 { A_WeaponOffset(8,-2,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A O 1 { A_WeaponOffset(0,36); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A P 1 { A_WeaponOffset(2,-2,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A P 1 { A_WeaponOffset(2,-2,WOF_ADD); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A P 1 { A_WeaponOffset(0,32); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A Q 1 { A_WeaponOffset(3,3,WOF_ADD); A_SetTics(IsDoubleSpeed() ? 0 : 1); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A Q 1 { A_WeaponOffset(3,3,WOF_ADD); A_WeaponReady(WRF_NOFIRE|WRF_NOBOB); }
		DW4A A 0 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	SSL.FireBullets:
	SSR.FireBullets:
	SSG.FireBullets:
		M666 A 0
		{
			double newspread = (CountInv("SSGUpgrade3") ? 7.1 : 11.2);
			//-----------------------------------------------------
			// Double trouble section
			A_TakeInventory("D4Shells",1,TIF_NOTAKEINFINITE);
			A_GunFlash("Null");
			
			Class<Actor> PuffRail = "D4BulletPuffExtreme";
			
			int OID = OverlayID();
			double sign = (OID == RGunFire || OID == RGunFire2) ? 1 : -1;
			double offs = 5 * sign;
			if (CountInv("SSGUpgrade2"))	//if you have piercing shots
			{
				
				for (int i = 0; i < 12; i++)
				{
					A_RailAttack(0,offs,0,-1,-1,RGF_SILENT|RGF_EXPLICITANGLE,0,'D4BulletPuffSSG',frandom(-newspread,newspread),frandom(-SpreadZ,SpreadZ));
				}
				A_FireBullets(CountInv("SSGUpgrade3") ? SpreadZ : SpreadXY,SpreadZ,7,DmgBase,"PacifistPuff",FBF_NORANDOMPUFFZ|FBF_PUFFTRACER,0,"BulletSmallTracer",0,offs);
			}
			else
			{
				A_FireBullets(CountInv("SSGUpgrade3") ? SpreadZ : SpreadXY,SpreadZ,7,DmgBase,PuffRail,FBF_PUFFTRACER,0,"BulletSmallTracer",0,offs);
				A_FireBullets(CountInv("SSGUpgrade3") ? SpreadZ : SpreadXY,SpreadZ,5,DmgBase,PuffRail,FBF_PUFFTRACER,0,null,0,offs);
			}
			
			if (GetCvar("HQEffectsWeapons"))
			{
				for (int i = 4; i < 7; i++)
					A_FireProjectile("SGSmokeSpawner",0,0,i * sign,2);
			}
			
			//-----------------------------------------------------
			// Rest of the animation
			A_AlertMonsters();
			
			for (int i = P1; i <= P4; i++)
			{
				if (A_Overlay(i, "SSG.PitchKicker", true))
					break;
			}
		}
		Stop;
	/*
	SSG.PitchKicker:
		M666 A 1 
		{
			A_ZoomFactor(0.985,ZOOM_NOSCALETURNING);
			A_SetViewPitch(viewpitch-PitchKick,SPF_INTERPOLATE);
		}
		M666 AA 1 A_SetViewPitch(viewpitch+(PitchKick / 4.0));
		M666 A 1 { A_SetViewPitch(viewpitch+(PitchKick / 4.0)); A_ZoomFactor(1,ZOOM_NOSCALETURNING); }
		M666 A 1 A_SetViewPitch(viewpitch+(PitchKick / 4.0));
		Stop;
	*/
	SSG.PitchKicker:
		M666 A 1 
		{
			A_ZoomFactor(0.985,ZOOM_NOSCALETURNING);
			A_SetPitch(pitch-PitchKick,SPF_INTERPOLATE);
		}
		M666 AA 1 A_SetPitch(pitch+(PitchKick / 4.0));
		M666 A 1 { A_SetPitch(pitch+(PitchKick / 4.0)); A_ZoomFactor(1,ZOOM_NOSCALETURNING); }
		M666 A 1 A_SetPitch(pitch+(PitchKick / 4.0));
		Stop;
	
	SSG.Listen:
		M666 A 1
		{
			// Have ammo?
			if (!A_JumpIfNoAmmo("Null") && !CountInv("BusyToken"))
			{
				int inp = GetPlayerInput(MODINPUT_BUTTONS);
				int old = GetPlayerInput(MODINPUT_OLDBUTTONS);
				// Primary shoots left
				if ((CountInv("SSLSecondShotToken") < 2) &&
					(inp & BT_ATTACK) &&
					!(old & BT_ATTACK))
				{	
					A_Overlay(LGun,"SSD.Fire");
					A_OverlayFlags(LGun, PSPF_MIRROR|PSPF_FLIP, true);
				}
				
				// Secondary shoots right
				if ((CountInv("SSRSecondShotToken") < 2) &&
					(inp & BT_ALTATTACK) &&
					!(old & BT_ALTATTACK))
				{	A_Overlay(RGun,"SSD.Fire");	}
			}
		}
		Loop;
		
	//==========================================================================
	//==========================================================================
	// Dual SSGs
	
	// Now here's the thing about offsets when setting them on overlays with 
	// PSPF_ADDWEAPON flags: They inherit the base weapon's offsets, which means
	// it already has the y value. So take 32 from the y value on all
	// A_OverlayOffset calls for those layers. (Does not apply for the weapon
	// layer itself!)
	SSD.Fire:
		M665 B 1 Bright 
		{
			//A_OverlayOffset(RGun,12,36-32);
			int OID = OverlayID();
			Class<Inventory> item = ShotToken(OID);
			bool leftgun = (OID == LGun);
			int DGunFire = RGunFire;
			int DGunFire2 = RGunFire2;
			
			A_OverlayFlags(OID, PSPF_MIRROR|PSPF_FLIP, leftgun);
			A_OverlayOffset(OID,12,5,WOF_ADD);
			
			if (leftgun)
			{
				DGunFire = LGunFire;
				DGunFire2 = LGunFire2;
			}
			
			if (CheckWeaponMode() || CountInv(item))
			{
				A_Overlay(DGunFire,"SSG.FireBullets");
				A_StartSound("Doom4/Weapon/SuperShotgun/Fire",CHAN_WEAPON,CHANF_OVERLAP);
				A_GiveInventory(item,1);
			}
			else
			{
				A_Overlay(DGunFire,"SSG.FireBullets");
				A_Overlay(DGunFire2,"SSG.FireBullets");
				A_StartSound("Doom4/Weapon/SuperShotgun/Fire",CHAN_WEAPON, CHANF_OVERLAP);
				A_GiveInventory(item,2);
			}
		}
		
	
		M665 B 1 bright
		{
			//A_OverlayOffset(RGun,24,39-32);
			int OID = OverlayID();
			A_OverlayOffset(OID,12,5,WOF_ADD);
			return A_JumpIfInventory(ShotToken(OID),2,"SSD.Reload");
		}
		M665 C 1 A_OverlayOffset(OverlayID(),6,3,WOF_ADD);	//30 13
		M665 C 1 A_OverlayOffset(OverlayID(),-8,-5,WOF_ADD);
		M665 C 1 A_OverlayOffset(OverlayID(),-7,-3,WOF_ADD);
		M665 C 1 A_OverlayOffset(OverlayID(),-6,-2,WOF_ADD);
		M665 C 1 A_OverlayOffset(OverlayID(),-5,-2,WOF_ADD);
		M665 A 1 A_OverlayOffset(OverlayID(),-4,-1,WOF_ADD);
		M665 A 0 A_OverlayOffset(OverlayID(),0,32-32);
		Goto SSD.Ready;
	SSD.Reload:
		M665 C 0 
		{
			StateLabel next = null;	
			int OID = OverlayID();
			if (IsDoubleSpeed())
			{
				next = "SSD.ReloadFast";
				A_Overlay((OID == LGun) ? 5004 : 5002, "Overlay.ReloadSoundSanicDaHedgahmahoggy");
			}
			else
				A_Overlay((OID == LGun) ? 5003 : 5001, "Overlay.ReloadSoundFast");
			
			return ResolveState(next);
		}
		M665 C 1 A_OverlayOffset(OverlayID(),6,3,WOF_ADD);
		M665 C 1 A_OverlayOffset(OverlayID(),-8,-5,WOF_ADD);
		M665 C 1 A_OverlayOffset(OverlayID(),-7,-3,WOF_ADD);
		M665 C 1 A_OverlayOffset(OverlayID(),-6,-2,WOF_ADD);
		M665 C 1 A_OverlayOffset(OverlayID(),-5,-2,WOF_ADD);
		M665 A 1 A_OverlayOffset(OverlayID(),-4,-1,WOF_ADD); //6
		
		M665 DDDDEE 1 A_OverlayOffset(OverlayID(),2,6,WOF_ADD); //6
		
	
		---- A 0
		{
			if (D4D_Casings)
			{
				Class<Actor> shell = ShellSide(OverlayID());
				for (int i = 0; i < 2; i++)
					A_SpawnItemEx(shell, flags: SXF_NOCHECKPOSITION|SXF_SETMASTER);
			}
		}
		M665 FFFF 1 A_OverlayOffset(OverlayID(),-2,2,WOF_ADD);
		M665 FFFF 1 A_OverlayOffset(OverlayID(),2,-2,WOF_ADD);	//8
		M665 GGGGG 1; 	//5

		
		M665 HHII 1 A_OverlayOffset(OverlayID(),2,-8,WOF_ADD);
		M665 JJJJ 1 A_OverlayOffset(OverlayID(),1,3,WOF_ADD);	//8
		M665 A 0 
		{
			int OID = OverlayID();
			A_OverlayOffset(OID,0,32-32);
			A_SetInventory(ShotToken(OID),0);
		}
		Goto SSD.Ready;
		
	SSD.ReloadFast:
		M665 C 1 A_OverlayOffset(OverlayID(),6,3,WOF_ADD);
		M665 C 0 A_OverlayOffset(OverlayID(),-8,-5,WOF_ADD);
		M665 C 1 A_OverlayOffset(OverlayID(),-7,-3,WOF_ADD);
		M665 C 0 A_OverlayOffset(OverlayID(),-6,-2,WOF_ADD);
		M665 C 1 A_OverlayOffset(OverlayID(),-5,-2,WOF_ADD);
		M665 A 0 A_OverlayOffset(OverlayID(),-4,-1,WOF_ADD); //6
		
		M665 DDE 1 A_OverlayOffset(OverlayID(),2*2,6*2,WOF_ADD); //6
		---- A 0
		{
			if (D4D_Casings)
			{
				Class<Actor> shell = ShellSide(OverlayID());
				for (int i = 0; i < 2; i++)
					A_SpawnItemEx(shell, flags: SXF_NOCHECKPOSITION|SXF_SETMASTER);
			}
		}
		M665 FF 1 A_OverlayOffset(OverlayID(),-4,4,WOF_ADD);
		M665 FF 1 A_OverlayOffset(OverlayID(),4,-4,WOF_ADD);	//8
		M665 GG 1 ;	//5

		
		M665 HI 1 A_OverlayOffset(OverlayID(),4,-16,WOF_ADD);
		M665 JJ 1 A_OverlayOffset(OverlayID(),2,6,WOF_ADD);	//8
		M665 A 0 
		{
			int OID = OverlayID();
			A_OverlayOffset(OID,0,32-32);
			A_SetInventory(ShotToken(OID),0);
		}
		Goto SSD.Ready;
	//==========================================================================
	
	Overlay.ReloadSoundNormal:
		TNT1 A 5;
		TNT1 A 1 A_PlaySound("Doom4/Weapon/SuperShotgun/Open",5,SSGReloadVolume);
		TNT1 A 10 A_PlaySound("Doom4/Weapon/SuperShotgun/EjectShell",6,SSGReloadVolume);
		TNT1 A 6 A_PlaySound("Doom4/Weapon/SuperShotgun/ShellClick",5,SSGReloadVolume); 
		TNT1 A 16 A_PlaySound("Doom4/Weapon/SuperShotgun/Load",6,SSGReloadVolume);
		TNT1 A 1 A_PlaySound("Doom4/Weapon/SuperShotgun/Close",5,SSGReloadVolume);
		Stop;
	Overlay.ReloadSoundFast:
		TNT1 A 11;
	Overlay.ReloadSoundSanicDaHedgahmahoggy: // Crackhead getting crack, speed, and cocaine at once makes him super.
		TNT1 A 1
		{
			A_PlaySound("Doom4/Weapon/SuperShotgun/FullReload", 5, SSGReloadVolume);
		}
		Stop;
	}
}

Class D4BulletPuffSSG : D4BulletPuffGibDistance
{
	Default
	{
		Damage D4SuperShotgun.DmgBase;
		D4BulletPuffGibDistance.Range D4SuperShotgun.GibRange;
		+ALWAYSPUFF;
		+NOTIMEFREEZE
	}
}

Class SSRSecondShotToken : Inventory	{ 	Default { Inventory.MaxAmount 2;	} }
Class SSLSecondShotToken : Inventory	{	Default { Inventory.MaxAmount 2;	} }

Class D4BulletPuffForRails : D4BulletPuff
{	Default { +ALWAYSPUFF	}	}

Class D4BulletPuffForRailsExtreme : D4BulletPuffForRails
{	Default { +EXTREMEDEATH	}	}

//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
//	Casing
//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
//	Normal SSG
Class SSGCasingSpawner : Actor 
{
	const Flags = WARPF_NOCHECKPOSITION|WARPF_COPYPITCH;
	const Distance = 18;
	const YOff = 11;
	const ZOff = 34;
	double  pos;
	int t;
	
	Default 
	{
		Projectile;
		+NOINTERACTION
		Scale 0.30;
		Alpha 0.0;
		/*RenderStyle "Add";
		Alpha 0.1*/
		RenderStyle "None";
		Translation "0:255=%[0,0,0]:[0,1,0]";
	}
	States
	{
	Spawn:
		TNT1 A 1 NoDelay
		{
			if (master) { pitch = master.pitch; }
			
			if (pitch > 0) //Looking down
			{	pos = (sin(pitch) * 12);	}
			else //Looking up
			{	pos = (sin(pitch) * 6);	}
			
			
			A_Warp(MasterPtr,
				(cos(-pitch)*(Distance)) - pos,
				YOff,
				(ZOff*GetCrouchFactor(MasterPtr))+(sin(-pitch)*(Distance)),0,Flags|WARPF_INTERPOLATE);
			
			if (D4D_Casings)
			{
				if (D4D_SilentCasings)
				{	A_SpawnItemEx("D4ShellCasingSilent",0,0,0,frandom(0.5,-0.5),frandom(2,4),frandom(0.8,1.6),0,SXF_NOCHECKPOSITION);	}
				else
				{	A_SpawnItemEx("D4ShellCasing",0,0,0,frandom(0.5,-0.5),frandom(2,4),frandom(0.8,1.6),0,SXF_NOCHECKPOSITION);	}
			}
		}
		Stop;
	}
}

//	Double SSG, right
Class SSGCasingSpawnerRight : Actor 
{
	const Flags = WARPF_NOCHECKPOSITION|WARPF_COPYPITCH;
	const Distance = 18;
	const YOff = 11;
	const ZOff = 31;
	double  pos;
	int t;
	Default 
	{
		Projectile;
		+NOINTERACTION
		Scale 0.30;
		Alpha 0.0;
		/*RenderStyle "Add";
		Alpha 0.1*/
		RenderStyle "None";
		Translation "0:255=%[0,0,0]:[0,1,0]";
	}
	States
	{
	Spawn:
		TNT1 A 1 NoDelay
		{
			if (master) { pitch = master.pitch; }
			
			if (pitch > 0) //Looking down
			{	pos = (sin(pitch) * 12);	}
			else //Looking up
			{	pos = (sin(pitch) * 6);	}
			
			
			A_Warp(MasterPtr,
				(cos(-pitch)*(Distance)) - pos,
				YOff,
				(ZOff*GetCrouchFactor(MasterPtr))+(sin(-pitch)*(Distance)),0,Flags|WARPF_INTERPOLATE);
			
			if (D4D_Casings)
			{
				if (D4D_SilentCasings)
				{	A_SpawnItemEx("D4ShellCasingSilent",0,0,0,frandom(0.5,-0.5),frandom(2,4),frandom(0.8,1.6),0,SXF_NOCHECKPOSITION);	}
				else
				{	A_SpawnItemEx("D4ShellCasing",0,0,0,frandom(0.5,-0.5),frandom(2,4),frandom(0.8,1.6),0,SXF_NOCHECKPOSITION);	}
			}
		}
		Stop;
	}
}

//	Double SSG, Left
Class SSGCasingSpawnerLeft : Actor 
{
	const Flags = WARPF_NOCHECKPOSITION|WARPF_COPYPITCH;
	const Distance = 18;
	const YOff = -11;
	const ZOff = 31;
	double  pos;
	int t;
	Default 
	{
		Projectile;
		+NOINTERACTION
		Scale 0.30;
		Alpha 0.0;
		RenderStyle "None";
		Translation "0:255=%[0,0,0]:[0,1,0]";
	}
	States
	{
	Spawn:
		TNT1 A 1 NoDelay
		{
			if (master) { pitch = master.pitch; }
			
			if (pitch > 0) //Looking down
			{	pos = (sin(pitch) * 12);	}
			else //Looking up
			{	pos = (sin(pitch) * 6);	}
			
			A_Warp(MasterPtr,
				(cos(-pitch)*(Distance)) - pos,
				YOff,
				(ZOff*GetCrouchFactor(MasterPtr))+(sin(-pitch)*(Distance)),0,Flags|WARPF_INTERPOLATE);
				
			if (GetCvar("D4D_Casings"))
			{
				if (GetCvar("D4D_SilentCasings"))
				{	A_SpawnItemEx("D4ShellCasingSilent",0,0,0,frandom(0.5,-0.5),-frandom(2,4),frandom(0.8,1.6),0,SXF_NOCHECKPOSITION);	}
				else
				{	A_SpawnItemEx("D4ShellCasing",0,0,0,frandom(0.5,-0.5),-frandom(2,4),frandom(0.8,1.6),0,SXF_NOCHECKPOSITION);	}
			}
		}
		Stop;
	}
}