//======================================================================================
//======================================================================================
//		Grenades
//======================================================================================
//======================================================================================

//------------------------------------------------------
// tokens to indicate that the grenade exists in the player inventory

Class D4DGrenadeItem : D4DKeeperItem 
{
	Class<D4EquipmentProjectile> Grenade;
	bool Selected;
	String IconReady, IconCooldown, IconActive; 
	double SX, SY, PX, PY;
	
	int Cooldown, CooldownMax;
	
	Property CooldownMax: CooldownMax;			
	Property Grenade: Grenade;					// Actual grenade name that's launched
	Property Icons: IconReady, IconCooldown,	// Icons to use between ready, charging,
					IconActive;					// and prepared for activation.
	Property Scales: SX, SY;					// Scales for status bars
	Property Positions: PX, PY;					// Position for status bars (within 1024x768)
	
	Default
	{
		D4DGrenadeItem.CooldownMax 350;
		D4DGrenadeItem.Scales 1, 1;
	}
	
	override void DepleteOrDestroy()
	{
		Selected = false;
		Super.DepleteOrDestroy();
	}
	
	override void DoEffect()
	{
		Super.DoEffect();
		if (!Owner)	return;
		
		// Prevent overriding sounds by making the item play it instead.
		if (Cooldown > 0 && --Cooldown == 0)
		{
			SetOrigin(Owner.pos, false);
			A_PlaySound("Doom4/Weapon/Grenade/GrenadeRegen");			
		}
	}
	
	void SetCooldown(int amt = -1)
	{
		if (amt < 0 || amt > CooldownMax)	
			amt = CooldownMax;
		cooldown = amt;
	}
	
	void SetCooldownCap(int amt = -1)
	{
		CooldownMax = (amt < 1) ? Default.CooldownMax : amt;
	}
}

Class FlameThrowerPickup : D4DGrenadeItem	{ Default { D4DGrenadeItem.Grenade "D4FlamePoint";		D4DGrenadeItem.Icons "FlameThr", "FlameTh0", "";			Tag "Flame";	D4DGrenadeItem.Positions 829, 681;	}}
Class FragGrenadePickup : D4DGrenadeItem	{ Default { D4DGrenadeItem.Grenade "D4FragGrenade";		D4DGrenadeItem.Icons "FragGren", "FragGre0", "";			Tag "Frag";		D4DGrenadeItem.Positions 826, 680;	}}
Class HoloGrenadePickup : D4DGrenadeItem	{ Default { D4DGrenadeItem.Grenade "D4PlayerHolograph";	D4DGrenadeItem.Icons "HoloGren", "HoloGre0", "";			Tag "Holo";		D4DGrenadeItem.Positions 827, 681;	}}
Class SyphonGrenadePickup : D4DGrenadeItem	{ Default { D4DGrenadeItem.Grenade "D4SyphonGrenade";	D4DGrenadeItem.Icons "SyphGren", "SyphGre0", "";			Tag "Syphon";	D4DGrenadeItem.Positions 827, 679;	}}
Class PersonalPortalPickup : D4DGrenadeItem	{ Default { D4DGrenadeItem.Grenade "D4PersonalPortal";	D4DGrenadeItem.Icons "PersTele", "PersTel0", "PersTel1";	Tag "Portal";	D4DGrenadeItem.Positions 827, 681;	}}
Class TeslaRocketPickup : D4DGrenadeItem	{ Default { D4DGrenadeItem.Grenade "D4TeslaRocket";		D4DGrenadeItem.Icons "FragGren", "FragGre0", "";			Tag "Tesla";	D4DGrenadeItem.Positions 828, 682;	}}
Class ThreatSensorPickup : D4DGrenadeItem	{ Default { D4DGrenadeItem.Grenade "D4FragGrenade";		D4DGrenadeItem.Icons "FragGren", "FragGre0", "";			Tag "Sensor";	D4DGrenadeItem.Positions 828, 682;	}}
Class ThreatScannerPickup : D4DGrenadeItem	{ Default { D4DGrenadeItem.Grenade "D4FragGrenade";		D4DGrenadeItem.Icons "FragGren", "FragGre0", "";			Tag "Scanner";	D4DGrenadeItem.Positions 828, 682;	}}
Class KineticMinePickup : D4DGrenadeItem	{ Default { D4DGrenadeItem.Grenade "D4KineticMine";		D4DGrenadeItem.Icons "FragGren", "FragGre0", "";			Tag "Mine";		D4DGrenadeItem.Positions 828, 682;	}}
Class ShieldWallPickup : D4DGrenadeItem		{ Default { D4DGrenadeItem.Grenade "D4ShieldWall";		D4DGrenadeItem.Icons "ShieldWa", "ShieldW0", "";			Tag "Shield";	D4DGrenadeItem.Positions 828, 682;	}}



//------------------------------------------------------
// Actors to be picked up in world
// Flamethrower is the only exception as the player starts
// with it always.

Class D4DUtilityWorld : CustomInventory
{
	meta Class<D4DGrenadeItem> Pickup;
	meta Name SpriteName; 
	meta int SpriteFrame;
	
	Property Pickup: Pickup;
	Property SpawnSprite: SpriteName, SpriteFrame;
	
	Default
	{
		D4DUtilityWorld.SpawnSprite "TNT1", Fr_A;
	}
	
	action void ProcessPickup()
	{
		let pick = invoker.Pickup;
		
		if (!CountInv(pick))
		{			
			A_GiveInventory("GrenadeSelectedStripper",1);
			
			A_SetInventory(pick,1);
			let gren = D4DGrenadeItem(FindInventory(pick));
			if (gren)	gren.Selected = true;
		}		
	}
	
	action void SetupSprite()
	{
		if (invoker.owner)	return;
		
		int s = GetSpriteIndex(invoker.SpriteName);
		if (s)	
		{
			invoker.Sprite = s;
			invoker.Frame = invoker.SpriteFrame;
		}
	}
	
	States
	{
	Precache:
		D4GR EFG 0;
		D4SW A 0;
		D4PP A 0;
	Spawn:
		TNT1 A -1 NoDelay SetupSprite();
		Stop;
	Pickup:
		TNT1 A 0 ProcessPickup();
		Stop;
	}
}

Class FragGrenadeWorld : D4DUtilityWorld
{
	Default 
	{
		Inventory.PickupMessage "Frag Grenade equipment";
		D4DUtilityWorld.SpawnSprite "D4GR", Fr_E;
		D4DUtilityWorld.Pickup "FragGrenadePickup";
		Scale 0.3;
	}
}

Class HoloGrenadeWorld : D4DUtilityWorld
{
	Default 
	{
		Inventory.PickupMessage "Holograph equipment";
		D4DUtilityWorld.SpawnSprite "D4GR", Fr_F;
		D4DUtilityWorld.Pickup "HoloGrenadePickup";
		Scale 0.25;
	}
}

Class SyphonGrenadeWorld : D4DUtilityWorld
{
	Default 
	{
		Inventory.PickupMessage "Syphon Grenade equipment";
		D4DUtilityWorld.SpawnSprite "D4GR", Fr_G;
		D4DUtilityWorld.Pickup "SyphonGrenadePickup";
		Scale 0.3;
	}
}

Class ShieldWallWorld : D4DUtilityWorld
{
	Default 
	{
		Inventory.PickupMessage "Shield Wall equipment";
		D4DUtilityWorld.SpawnSprite "D4SW", Fr_A;
		D4DUtilityWorld.Pickup "ShieldWallPickup";
		Scale 1.0;
	}
}

Class PersonalPortalWorld : D4DUtilityWorld
{
	Default 
	{
		Inventory.PickupMessage "Personal Portal equipment";
		D4DUtilityWorld.SpawnSprite "D4PP", Fr_A;
		D4DUtilityWorld.Pickup "PersonalPortalPickup";
		Scale 1.0;
	}
}



//--------------------------------------------------------------
// To select next/prev grenade
//Next

Class GrenadeSelectedStripper : CustomInventory
{
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
	}
	States
	{
	Pickup:
		TNT1 A 0
		{
			for (int i = 0; i < Doom4Player.GrenadeSelected.Size(); i++)
			{
				let gren = D4DGrenadeItem(FindInventory(Doom4Player.GrenadeSelected[i]));
				if (!gren)	continue;
				
				gren.Selected = false;
			}
		}
		Stop;
	}
}

Class GrenadeSwitchNext : CustomInventory
{
	bool Previous;
	Property Previous: Previous;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
	}
	
	void PrintMessage(String text)
	{
		D4Msg.PlainMsg("smallfont", text, (657.0, 527.0), (800, 600), time: (0,0,1.0), id: 110);
	}
	
	States
	{
	Pickup:
		TNT1 A 0
		{
			// If akimbo system, don't change grenades. Switch the weapon mod instead.
			let plr = player;
			if (plr && plr.ReadyWeapon && plr.ReadyWeapon.GetClassName() == 'AkimboWeapon')
			{
				let ak = AkimboWeapon(plr.ReadyWeapon);
				if (ak)
				{
					ak.CheckMods(1);
					return true;
				}
			}
			
			// No akimbo. Okay to cycle through.
			// Grab all available equipment and assign them to a sorted list.
			Array<D4DGrenadeItem> Grenades; Grenades.Clear();
			for (int i = 0; i < Doom4Player.GrenadeSelected.Size(); i++)
			{
				let gren = D4DGrenadeItem(FindInventory(Doom4Player.GrenadeSelected[i]));
				if (gren && gren.Amount > 0)
				{
					Grenades.Push(gren);
				}
			}
			
			// Find the current selected grenade and unselect it.
			// Then select the next/previous.
			for (int i = 0; i < Grenades.Size(); i++)
			{
				if (Grenades[i].Selected)
				{
					Grenades[i].Selected = false;
					if (invoker.Previous)
					{
						i--;	if (i < 0)					i = Grenades.Size() - 1;
					}
					else
					{
						i++;	if (i >= Grenades.Size())	i = 0;
					}
					Grenades[i].Selected = true;
					invoker.PrintMessage(Grenades[i].GetTag());
					return true;
				}
			}
			// Fallback case: Only have one grenade.
			if (Grenades.Size() > 0 && Grenades[0])
			{
				Grenades[0].Selected = true;
				invoker.PrintMessage(Grenades[0].GetTag());
			}
			// No grenades.
			else invoker.PrintMessage("No grenades available");
			
			return true;
		}
		Stop;
	}
}

Class GrenadeSwitchPrev : GrenadeSwitchNext
{	Default {	GrenadeSwitchNext.Previous true;	}	}
//==============================================================================
//==============================================================================
// Actual grenades/utilities
//==============================================================================
//==============================================================================
// Base class actor
Class D4EquipmentProjectile : D4Actor {}

//------------------------------------------------------------------------------
// Kinetic Mine
//------------------------------------------------------------------------------
Class D4KineticMine : D4EquipmentProjectile
{
}

//------------------------------------------------------------------------------
// Personal Teleporter
//------------------------------------------------------------------------------
Class D4PersonalPortal : D4EquipmentProjectile
{
}

//------------------------------------------------------------------------------
// Tesla Rocket
//------------------------------------------------------------------------------
Class D4TeslaRocket : D4EquipmentProjectile
{
}

//------------------------------------------------------------------------------
// Syphon Grenade
//------------------------------------------------------------------------------
Class D4SyphonGrenade : D4EquipmentProjectile 
{
	MCHandler 		Event;
	Array<Actor> 	Monsters;
	int 			c, d, e, rng;
	double 			dist, UCos;
	
	Default 
	{
		Projectile;
		Speed 28;
		+ROLLSPRITE
		+ROLLCENTER
		-NOGRAVITY
		+FORCERADIUSDMG
		+CANBOUNCEWATER
		+FOILINVUL
		Gravity 0.2;
		Scale 0.09;
		Radius 5;
		Height 10;
		DeathSound "Doom4/Weapon/Grenade/SyphonExplo";
	}
	
	States
	{
	Spawn:
		D4GR C 0 Bright NoDelay { Tracer = Target; }
	Moving:
		D4GR CCCCC 1 Bright A_SetRoll(roll+10,SPF_INTERPOLATE);
		Loop;
	Death:
		TNT1 A 12 
		{
			A_Stop();
			bNOINTERACTION = true;
			//if (pos.z <= floorz + 1)	SetXYZ(pos + (0, 0, floorz + 1));
			A_SpawnItemEx("SyphonShockwaveBig",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("SyphonShockwaveBigFlat",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			A_PlaySound("Doom4/Weapon/Grenade/FragArm",5);
			A_PlaySound("Doom4/Weapon/Grenade/SyphonExplo",CHAN_WEAPON);
			A_AlertMonsters();
		}
		Goto Explode;
	Explode:
		TNT1 A 0
		{
			A_PlaySound("Doom4/Weapon/Grenade/SyphonSuck",((c % 3) + 5),0.5);
			d = A_RadiusGive("PinkShadowSpawner",256,RGF_MONSTERS|RGF_CUBE,1,null,"None",0,5);
		}
	ExplodeLoop:
		TNT1 A 0
		{
			let plr = PlayerPawn(target);
			if (d > 0 && plr && plr.Health > 0)
			{
				
				int MaxSyphonHP = plr.GetMaxHealth(true);
				if (Target.health < MaxSyphonHP)
				{	
					Target.A_SetHealth(Min(Target.Health + 3, MaxSyphonHP));
				}
				else 
				{
					let inv = BasicArmor(target.FindInventory("BasicArmor"));
					if (!inv)	
					{
						target.A_GiveInventory("SyphonArmor", 1);
						inv = BasicArmor(target.FindInventory("BasicArmor"));
						inv.Amount = 3;
					}
					int cap = inv.MaxAmount - (100 - target.CountInv("D4ArmorCapRaiser"));
					cap = Max(cap, ArmorGreenNormal);
					if (inv && inv.Amount < cap)
					{
						inv.Amount = min(inv.Amount + 3, cap);						
					}
				}
				d--;
				return ResolveState("ExplodeLoop");
			}
			
			A_Explode(2,256,0,0,256);
			return ResolveState(null);
		}
		
		TNT1 A 0
		{
			for (int i = 0; i < 10; i++)
			{
				if (random[rng](1,10) <= 4)
				{
					rng = random[rng](1,4);
					dist = frandom[rng](64.0, 128.0);
					if (GetZAt(0,0) >= pos.z - 2)
					{	UCos = frandom[rng](0.0,90.0);	}
					else
					{	UCos = frandom[rng](0.0,180.0);	}
					A_SpawnParticle("FF0033",SPF_FULLBRIGHT|SPF_RELATIVE,10,0.1,random[rng](0,359),
					
					cos(-UCos) * dist,0,sin(UCos) * dist, 
					0,0,0, 
					cos(UCos) * -1.20,0,sin(UCos) * -1.20,1.0,0.0,3.0);
				}
			}
		}
		TNT1 A 8
		{
			c++;
			if (c >= 10)
			{	Destroy(); return;	}
		}
		Goto Explode;
	}
}

Class SyphonDestroyer : CustomInventory
{
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
	}
	States
	{
	Pickup:
		TNT1 A 0
		{
			A_Remove(DefPtr,RMVF_EVERYTHING,"D4SyphonGrenade");
		}
		Stop;
	}
}

Class SyphonHealth : HealthBonus
{
	Default 
	{
		+INVENTORY.IGNORESKILL
		+INVENTORY.ALWAYSPICKUP
		Inventory.Amount 1;
		Inventory.MaxAmount 200;
	}
}

Class SyphonArmor : BasicArmorBonus
{
	Default 
	{
		+INVENTORY.IGNORESKILL
		+INVENTORY.ALWAYSPICKUP
		Armor.SavePercent 100;
		Armor.SaveAmount 1;
		Armor.MaxSaveAmount 200;
	}
}

Class SyphonShockwaveBig : D4DFX 
{
	const RollAmount = 8.0;
	const ScaleMul = 2.0;
	double c;
	double URoll;
	double dist;
	double UCos;
	int UFloor;
	int t;
	int rng;
	
	Default 
	{
		+NOINTERACTION
		+BRIGHT
		+ROLLSPRITE
		+ROLLCENTER
		RenderStyle "Add";
		Alpha 0;
		Scale 0;
		Translation "0:255=%[0,0,0]:[1.0,0.0,0.2]";
	}
	
	States
	{
	Spawn:
		X060 A 0 NoDelay
		{
			URoll = RollAmount * randompick(-1,1);
		}
	Looping:
		X060 A 1
		{
			if (c >= 90)
			{	return ResolveState("SpawnRing");	}
			c += (90/12);
			A_SetRoll((roll + RollAmount) % 360,SPF_INTERPOLATE);
			A_FadeTo(0.99,(1.0/12.0),FTF_CLAMP);
			A_SetScale(sin(c) * ScaleMul);
			return ResolveState(null);
		}
		Loop;
	SpawnRing:
		"####" "#" 0
		{
			if ((t < 80) && ((t % 4) == 0))
			{	A_SpawnItemEx("SyphonShrinkRing",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_TRANSFERPITCH|SXF_TRANSFERROLL|SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);	}
		}
	Shrink:
		"####" "#" 1
		{	
			if (t < 80)
			{	t++;	}
			else
			{
				c += (90.0/12.0);
				A_SetScale(sin(c) * ScaleMul);
				if (Scale.X <= 0)	{	return ResolveState("Null");	}
			}
			
			A_SetRoll((roll + RollAmount) % 360,SPF_INTERPOLATE);
			return ResolveState(null);
		}
		Goto SpawnRing;
	}
}

//Everything handled with the FLATSPRITE flag check.
Class SyphonShockwaveBigFlat : SyphonShockwaveBig
{	Default { +FLATSPRITE		}}

Class SyphonShrinkRing : D4DFX 
{
	double UScale;
	double t;
	
	Default 
	{
		+ROLLCENTER
		+ROLLSPRITE
		+NOINTERACTION
		+BRIGHT
		RenderStyle "Add";
		Alpha 0;
	}
	States
	{
	Spawn:
		X060 B 0 NoDelay
		{
			if (master)
			{	bFlatSprite = master.bFlatSprite;	}
			t = 90;
			UScale = Scale.X;
		}
	Looping:
		X060 B 1
		{
			t -= (90.0/10.0);
			A_SetScale(sin(t) * UScale);
			A_FadeIn(0.1,FTF_CLAMP);
			if (Scale.X <= 0)	{ return ResolveState("Null"); }
			return ResolveState(null);
		}
		Loop;
	}
}

//Class SyphonShrinkRingFlat : SyphonShrinkRing
//{	+FLATSPRITE		}

Class PinkShadowSpawner : CustomInventory
{
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
	}
	States
	{
	Pickup:
		TNT1 A 0 A_SpawnItemEx("PinkShadow",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
		Stop;
	}
}

Class PinkShadow : D4DFX 
{
	Default 
	{
		+NOINTERACTION
		+BRIGHT
		RenderStyle "Stencil";
		StencilColor "Pink";
		Alpha 0.99 ;
	}
	
	States
	{
	Spawn:
		"----" A 1 A_FadeOut(0.12);
		Wait;
	}
}



//======================================================================================
//	Frag grenade
//======================================================================================
Class D4FragGrenade : D4EquipmentProjectile 
{
	int timer;
	int armed;
	Default 
	{
		Projectile;
		Speed 25;
		BounceType "Doom";
		BounceFactor 0.1;
		WallBounceFactor 0.1;
		-NOGRAVITY
		+FORCERADIUSDMG
		+ROLLSPRITE
		+CANBOUNCEWATER
		+EXTREMEDEATH
		Scale 0.06;
		Gravity 0.4;
		Radius 5;
		Height 10;
		ReactionTime 10;	//~1.4 secs to explode
		BounceSound "Doom4/Weapon/Grenade/FragBounce";
	}
	States
	{
	Spawn:
		D4GR D 1 NODELAY 
		{
			A_SetRoll(roll+16,SPF_INTERPOLATE);
			A_SpawnItemEx("GrenadeSmoke",0,0,0,0,0,frandom(0.2,0.7),0,SXF_NOCHECKPOSITION);

			if (timer==75 && armed==0) //if 50 tics passed, plays the arm sound
			{
				A_PlaySound("Doom4/Weapon/Grenade/FragArm",6);
				armed = 1;	//and marks the grenade as "armed"
			}
			if (timer>=100)
			{
				return ResolveState("Explode");
			}
			timer++;
			return ResolveState(null);
		}
		Loop;
	XDeath:
		D4GR D 0;
		Goto Explode;
		
	// THERE IS SUCH A THING AS TOO MANY COMMENTS. 
	Death:
		D4GR D 1
		{
			A_PlaySound("Doom4/Weapon/Grenade/FragBounce",5);
			timer = 0;
			if (A_CheckFloor("Null"))	//if it hits the floor
			{
				return ResolveState("ArmOnFloor");	//arms the grenade
			}
			
			//otherwise
			if (timer == 25 && armed == 0) //if 25 tics passed, plays the arm sound
			{
				A_PlaySound("Doom4/Weapon/Grenade/FragArm",6);
				armed = 1;	//and marks the grenade as "armed"
			}
			if (timer >= 50)
			{
				return ResolveState("Explode");
			}
			timer++;
			return ResolveState(null);
		} 
		Wait;
	ArmOnFloor:
		D4GR D 1 
		{
			A_PlaySound("Doom4/Weapon/Grenade/FragBounce",5);
			if (!armed) //if it hadnt played the sound before
			{
				A_PlaySound("Doom4/Weapon/Grenade/FragArm",6);
				armed = 1;
			}
			if (timer >= 25)
			{
				return ResolveState("Explode");
			}
			timer++;
			return ResolveState(null);
		}
		Wait;
	Explode:
		TNT1 A 0 
		{
			A_PlaySound("Doom4/Weapon/RocketLauncher/HRExplo",6);
			A_AlertMonsters();
			if (CountInv("ProtectionBonus2", TargetPtr))
			{			
				A_Explode(32,128);
				A_Explode(224,192,0,0,96);
			}
			else
			{			
				A_Explode(64,128);	
				A_Explode(192,192,0,0,96);
			}
			A_RadiusThrust(64,128,RTF_THRUSTZ|RTF_AFFECTSOURCE|RTF_NOIMPACTDAMAGE);
			A_RadiusThrust(192,192,RTF_THRUSTZ|RTF_NOIMPACTDAMAGE,96);
			
			if (HQEffectsWeapons)
			{
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("GenericExplosionParticleSpawner",0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			}
		}
		TNT1 A 50
		{
			A_SpawnItemEx("D4RocketExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			if (D4D_quakexplosions)
			{
				A_QuakeEx(2,2,2,15,0,768,"",QF_SCALEDOWN,0,0,0,1);
			}
			bNOINTERACTION = true;
		}
		Stop;
	}
}
Class GrenadeSmoke : D4DFX 
{
	double add;
	
	Default 
	{
		+ROLLSPRITE
		+CLIENTSIDEONLY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		Alpha 0.5;
		Scale 0.1;
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay 
		{
			add = frandom(-3.0,3.0);
			A_SetRoll(random(0,359));
			return A_Jump(256,random(1,18));
		}
		X102 ABCDEFGHIJKLMNOPQRSTUV 0 { return ResolveState("Act"); }
	Act:
		"####" "#" 1
		{
			A_SetRoll(roll + add, SPF_INTERPOLATE);
			A_FadeOut(0.015);
			//A_ScaleVelocity(0.986);
			A_SetScale(Scale.X+0.01);
		}
		Loop;
	}
}
//==============================================================================
//	Player holograph
//==============================================================================
Class D4PlayerHolograph : D4EquipmentProjectile 
{
	const TimeLimit = 35 * 6;
	int countdowntimer;
	MCHandler Event;
	Default 
	{
		Monster;
		Radius 20;
		Height 56;
		Health 300;
		+DONTTHRUST
		+FRIENDLY
		+FORCERADIUSDMG
		+FOILINVUL
		-COUNTKILL
		+OLDRADIUSDMG
		+PAINLESS
		+CAUSEPAIN
		+BLOODLESSIMPACT
		+NODAMAGE
		+INVULNERABLE
		+LOOKALLAROUND
		+NOPAIN
		+NODAMAGETHRUST
		+NOBLOOD
		+NONSHOOTABLE
		+THRUACTORS
		DamageType "HoloAttract";
		DamageFactor 0;
	}
	
	void DrawAttention(bool justmissiles = false)
	{
		if (!Event)	Event = MCHandler.Fetch();
		if (!Event)	return;
		if (!justmissiles)
		{
			for (int i = 0; i < Event.Monsters.Size(); i++)
			{
				Actor mo = Event.Monsters.Actors[i];
				// Skip players, the dead, friendly and infighting monsters.
				if (!mo || mo == self || mo.bFRIENDLY || mo.health < 1 || mo.player ||
					(mo.target && !mo.target.player) ||
					mo.pos.z < mo.floorz ||
					mo.pos.z + height > mo.ceilingz ||
					!mo.CheckSight(self, SF_IGNOREVISIBILITY))
					continue;
				
				if (!mo.LastHeard)
				{
					if (master)	mo.LastHeard = master;
					else		mo.LastHeard = self;
				}
				mo.target = self;
				mo.Threshold = mo.DefThreshold;
			}
		}
		
		// Redirect seeking missiles to this guy instead, if they're in sight.
		for (int i = 0; i < Event.Missiles.Size(); i++)
		{
			Actor mo = Event.Missiles.Actors[i];
			if (!mo || !mo.bSEEKERMISSILE || mo.bNOINTERACTION || 
				mo.pos.z < mo.floorz ||
				mo.pos.z + mo.height > mo.ceilingz ||
				!mo.CheckSight(self, SF_IGNOREVISIBILITY))
				continue;
			
			if (mo.tracer == master)
				mo.tracer = self;
		}
	}
	
	override void Tick()
	{
		Super.Tick();
		
		if (IsFrozen() || bKILLED)
			return;
			
		if (countdowntimer++ >= TimeLimit)
		{
			bKILLED = true;
			SetStateLabel("Death"); // Do not call A_Die or it will spawn health drops.
		}
	}
	
	override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		return (flags & DMG_FORCED) ? Super.DamageMobj(inflictor, source, damage, mod, flags, angle) : 0;
	}
	
	States
	{
	Spawn:
		PLAZ E 0 NoDelay 
		{
			A_PlaySound("Doom4/HoloSpawn",CHAN_AUTO);
			A_SetAngle(random(0,359));
			countdowntimer = 0;
		}
	Idle:
		PLAZ E 2;
		PLAZ E 0 
		{
			A_ClearTarget();
			DrawAttention(true);
			A_LookEx(0,0,0,0,360,"TickTock");
			countdowntimer += 2;
			return A_JumpIf (countdowntimer >= TimeLimit,"Death");
		}
		Loop;
	Missile:
	TickTock:
		PLAZ E 0 A_PlaySound("Doom4/Weapon/PlasmaGun/PlasmaLoop",5,1,true);
	TickTockLoop:
		PLAZ F 0
		{
			DrawAttention();
			if (target && target.health > 0)
			{
				A_FaceTarget();
				A_SpawnProjectile("NewPlasmaBallNoDamage");
			}
		}
		PLAZ FEE 1 bright 
		{
			
			if (!target || target.health < 1)
			{	
				A_Chase(null,null,CHF_DONTMOVE);
				if (!target || target.health < 1)
				{	
					A_ClearTarget();
					A_StopSound(5);
					A_PlaySound("Doom4/Weapon/PlasmaGun/Fire",5,1);
					return ResolveState("Idle");	
				}
			}
			return ResolveState(null);
		}
		Loop;
	Death:
		PLAZ E 1 
		{
			bKILLED = true;
			A_FadeOut();
		}
		Wait;
	}
}

Class NewPlasmaBallNoDamage : Actor 
{
	Default 
	{
		Projectile;
		Scale 0.07;
		Height 8;
		Radius 4;
		Speed 30;
		Damage 0;
		RenderStyle "Add";
		Alpha 0.99;
		+FORCEXYBILLBOARD
		Translation "0:255=%[0,0,0]:[0.7,0.9,1.5]";
		DamageType "HoloAttract";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay 
		{
			A_SetScale(frandom(0.04,0.06));
			return A_Jump(256,random(1,26));
		}
	Moving:
		PLS1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Light("BlueSmallPoint1") Bright;
		PLS2 ABCDEF 1 Light("BlueSmallPoint1") Bright;
		Loop;
	Death:
		TNT1 A 10 A_SpawnItemEx("D4PistolPuffBig1",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		Stop;
	}
}

//==============================================================================
//	Flame Belch
//==============================================================================

Class CarrionFlameFastSmall : CarrionFlameFast
{
	Default
	{
		Scale 0.1;
	}
}

Class D4FlamePoint : D4EquipmentProjectile
{
	const FlameCone = cos(20);
	const IgniteRange = 300.0;
	const IgniteRangeSq = IgniteRange ** 2.0;
	MCHandler Event;
	int count;
	D4DIgniteTrace tr;
	Default
	{
		Projectile;
		+NOINTERACTION
		+INVISIBLE
		+NOTIMEFREEZE
		Radius 1;
		Height 1;
	}
	
	private void IgniteMobj(Actor mo)
	{
		let def = GetDefaultByType('D4FlameTimer');
		mo.SetInventory('D4FlameTimer', def.MaxAmount);
			
		let inv = D4FlameTimer(mo.FindInventory('D4FlameTimer'));
		if (inv)
		{
			inv.Damager = target;
		}
	}
	
	States
	{
	Spawn:
		TNT1 A 1
		{
			// Backup measure. Player dead or time's up.
			if (!target || !target.player || target.health < 1 || ++count > 16)
			{	Destroy(); return;	}
			
			// First tic, annoy monsters.
			if (count == 1)
			{
				A_PlaySound("CarrionCannon/BurnEnd", CHAN_AUTO);
				A_AlertMonsters();
			}
			
			// Perform delimited offsetting. This code makes the flame belcher
			// stay with the shoulder cannon.
			Vector3 x, y, z, origin;
			[x, y, z] = MC_CoordUtil.GetAxes(target.pitch,target.angle,target.roll);
			origin = target.Vec2OffsetZ(0,0,target.player.viewz) + -1.0 * x + -15.0 * y + 5.0 * z;
			SetOrigin(origin, true);
			
			angle = target.angle;
			pitch = target.pitch;
			Vector2 orig = (angle, pitch);
			
			// Flaming dopefish belch. Tartarus sauce is spiciest, hands down.
			int i = 0;
			for (; i < IgniteRange; i += random(5,15))
			{
				double p = frandom(-10.0, 10.0);
				double dist = frandom(0.0, IgniteRange);
				A_SpawnItemEx("CarrionFlameFastSmall", 
					cos(pitch) * i, frandom(-2.0, 2.0), -4.0 - sin(pitch) * i + frandom(-2.0, 2.0), 
					frandom(-1.0, 1.0), frandom(-1.0, 1.0), 0, 0, SXF_NOCHECKPOSITION);
			}
			
			// Actual ignition code. Once every X tics, set fire to any monster
			// in the way of the flames.
			
			// TO DO: Rework.
			/*
			if (count % 2 == 0)
			{
				if (!Event)	Event = MCHandler.Fetch();
				for (int i = 0; i < Event.Monsters.Size(); i++)
				{
					// Root out all those that don't apply, such as dead and beyond
					// sight/range of the jet.
					Actor mo = Event.Monsters.Actors[i];
					if (!mo || mo.health < 1 || Distance3DSquared(mo) > IgniteRangeSq ||
						!CheckSight(mo, SF_IGNOREVISIBILITY|SF_IGNOREWATERBOUNDARY))
						continue;
				
					// Visible to the flames, so turn towards it within a cone.
					angle = orig.x;
					pitch = orig.y;
					TurnAng(mo, 10.0); // Delimited turning.
					
					// Set up a tracer to nab the hitbox. 
					if (!tr)	tr = new("D4DIgniteTrace");
					tr.Owner =	target;
					tr.Wanted =	mo;
					Vector3 viewVector = (cos(angle)*cos(pitch),sin(angle)*cos(pitch),-sin(pitch));
					
					// Lastly, see if it's viably within a cone via hitbox.
					if (tr.Trace(pos, CurSector, viewVector, IgniteRange, 0) && tr.Results.HitType == TRACE_HitActor)
					{
						// Time for the pink slip.
						Actor res = tr.Results.HitActor;
						if (res) IgniteMobj(res);
					}
				}
			}
			*/
			if (!tr)	tr = new("D4DIgniteTrace");
			tr.Ignored.Clear();
			tr.Ignored.Push(target);
			Vector3 viewVector = (cos(angle)*cos(pitch),sin(angle)*cos(pitch),-sin(pitch));
			for (int i = 0; i < 100; i++)
			{
				tr.Trace(pos, CurSector, viewVector, IgniteRange, 0);
				if (tr.Results.HitType == TRACE_HitActor)
				{
					// Time for the pink slip.
					Actor res = tr.Results.HitActor;
					if (res) 
					{
						IgniteMobj(res);
						tr.Ignored.Push(res);
					}
				}
				else break;
			}
			
			/*
			if (count % 4 < 1)
			{
				if (!Event)	Event = MCHandler.Fetch();
				for (int i = 0; i < Event.Monsters.Size(); i++)
				{
					Actor mo = Event.Monsters.Actors[i];
					if (!mo || mo.health < 1 || Distance3DSquared(mo) > IgniteRangeSq ||
						!CheckSight(mo, SF_IGNOREVISIBILITY|SF_IGNOREWATERBOUNDARY))
						continue;
				
					Vector3 forw = ( cos(angle) * cos(pitch), sin(angle) * cos(pitch), -sin(pitch) );
					Vector3 offs = pos; //Vec2OffsetZ(0, 0, player.viewz);		// Account for the view height
					Vector3 epos = mo.pos + (0, 0, mo.height * 0.5);
					Vector3 diff = level.Vec3Diff(offs, epos);	// Internally does v2 - v1.
					double cone = (forw dot diff) / diff.length();
					if (cone > FlameCone)
					{
						IgniteMobj(mo);
					}
				}
			}
			*/
		}
		Wait;
	}
}

class D4DIgniteTrace : LineTracer play
{
    Array<Actor> Ignored;
    override ETraceStatus TraceCallback() 
	{
        if (Results.HitType == TRACE_HitActor)
		{
			Actor mo = Results.HitActor;
			
			if (!mo || mo.bFRIENDLY || mo.health < 1)
				return TRACE_Skip;
			
			int size = Ignored.Size();
			if (size > 0 && Ignored.Find(mo) < size)
				return TRACE_Skip;
				
			return TRACE_Stop;
		}
		else if ((Results.HitType == TRACE_HitWall) && (Results.Tier == TIER_Middle))
		{
			Line ln = Results.Hitline;
			int flags = ln.Flags;
			if ((flags & (Line.ML_BLOCKHITSCAN|Line.ML_BLOCKEVERYTHING)))
				return TRACE_Stop;
				
			if (flags & (Line.ML_BLOCKING) && 
				(!(flags & (Line.ML_TWOSIDED)) && Results.HitTexture))
				return TRACE_Stop;
				
		//	if (!Results.HitTexture && !(ln.Flags & (Line.ML_BlockHitscan)))
			return TRACE_Skip;
		}
		return TRACE_Stop;
    }
}

Class D4FlameTimer : Inventory 
{
	const DamageInterval = 17;
	int tic, _damage;
	Actor Damager;
	
	Default 
	{ 
		Inventory.MaxAmount 175; 
	}
	
	override void DoEffect()
	{
		Super.DoEffect();
		
		// Don't process if the owner or damager is gone.
		if (bDESTROYED)	return;
		if (Amount > MaxAmount)	Amount = MaxAmount;
		if (!Owner || !Damager || --Amount < 1)	{ Destroy(); return; }
		
		// Damage every so often, which will spawn armor shards. The shards give
		// 20% of the damage taken in a single tic. This prevents overwhelming
		// shard spam while rewarding the player appropriately. Capped at 100.
		// Yes, a BFG shot will certainly be rewarding, but only if killed by
		// the big explosion.
		
		// The flames themselves don't do much damage, about 50 in total. 
		if (Owner.Health > 0 && --tic < 1)
		{
			tic = DamageInterval;
			Owner.DamageMobj(self, Damager, 5, 'Normal', DMG_THRUSTLESS|DMG_NO_PAIN);
			_damage += 10; // 2 shards instead of 1, to make it more useful.
		}
		
		// Spawn armor shards.
		if (_damage > 0 && Owner)
		{
			let chip = D4PinataArmor(Owner.Spawn('D4PinataArmor', Owner.Pos + (0, 0, Owner.Height * frandom(0.25, 0.75)), ALLOW_REPLACE));
			if (chip)
			{
				chip.bTHRUACTORS = true;
				double dmg = double(_damage / 5.0);
				chip.SaveAmount = Clamp(int(dmg), 1, 100);
				chip.bTHRUACTORS = false;
				chip.VelFromAngle(frandom[a](3,6),random[a](0,359));
				chip.vel.Z += frandom[a](1,5);
				chip.A_FaceMovementDirection(0,0);
			}
			_damage = 0;
		}
		
		// Spawn flames
		int rng = random(1,4);
		for (int i = 0; i < rng; i++)
		{
			Actor a; bool b;
			[b, a] = Owner.A_SpawnItemEx("CarrionFlameFastNoMove", 
				frandom(0.0, Owner.Radius), 0, frandom(1.0, Owner.Height),
				frandom(0.0, 1.0), 0, frandom(0.0, 1.0), random(0,359), SXF_NOCHECKPOSITION);
				
			if (a && b)
				a.Scale *= 0.40;
		}
	}
}

//------------------------------------------------------------------------------
// Shield Wall
//------------------------------------------------------------------------------
Class D4ShieldWall : D4EquipmentProjectile
{
	// Coordinates for the lasers, simply visual effects.
	static const double OuterPoints[] = 
	{
		// Outer shell spawn points (y/z)
		 256,	0,
		 320,	32,
		 320,	256,
		 256,	288,
		-256,	288,
		-320,	256,
		-320,	32,
		-256,	0
	};
	
	static const double InnerPoints[] =
	{
		// Inner shell spawn points (y/z)
		 128,	16,
		 224,	64,
		 288,	64,
		 288,	224,
		 224,	224,
		 128,	272,
		-128,	272,
		-224,	224,
		-288,	224,
		-288,	64,
		-224,	64,
		-128,	16
	};
	
	Default
	{
		Projectile;
		+THRUACTORS
		+DONTSPLASH
		+EXPLODEONWATER
		-NOGRAVITY
		Speed 4;
		Radius 4;
		Height 4;
		
	}
	States
	{
	Spawn:
		D4SW A 0;
		Stop;
	}
}

/*
Doom4/ShieldWallImpact
Doom4/ShieldWallLoop
Doom4/ShieldWallOff
Doom4/ShieldWallOn
*/

// While the barrier can stop most things,
Class D4ShieldWallBarrier : Actor
{
	private Actor Owner;
	private int Timer;
	Default
	{
		+CANPASS
		-COUNTKILL
		+SOLID
		-SHOOTABLE // Don't add this actor to the global monster list (MC_Utility).
		+INVULNERABLE
		+NODAMAGE
		+NOBLOOD
		+NOPAIN
		+DONTRIP
		+DONTTHRUST
		+DONTBLAST
		+DONTMORPH
		+CANNOTPUSH
		+NOICEDEATH
		+NOGRAVITY
		+NOTELEPORT
		+NOTELESTOMP
		+NOTELEFRAG
		+NOTRIGGER
		+THRUSPECIES
		+BOSS
		+FRIENDLY
		+NEVERTARGET
		+DONTCORPSE
		+CANTSEEK
		+BRIGHT
		Species "Player";
		Radius 32;
		Height 1;
		Tag "Shield Wall";
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		Owner = Target;
	}
	
	// Don't block the owner.
	override bool CanCollideWith(Actor other, bool passive)
	{
		if (!other)	return false;
		if (other == Owner || (other.bMISSILE && other.target == owner))
			return false;
		return true;
	}
	
	override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		if (!(flags & DMG_EXPLOSION) && Timer < 1)
		{
			Timer = 17;
			A_PlaySound("Doom4/ShieldWallImpact");
		}
		return -1;
	}
	
	override void Tick()
	{
		if (IsFrozen())	return;
		
		if (Timer > 0)	Timer--;
		
		if (pos != SpawnPoint)
			SetOrigin(SpawnPoint, false);
		
		
		Super.Tick();
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			bSHOOTABLE = true;
			
			Alpha = 0.0;
			Height = min(128, ceilingz - pos.z);
		}
		PLAY AAAAAAAAAA 1 A_FadeIn(0.1, FTF_CLAMP);
		PLAY AAAAA 35;
		PLAY A 1 A_FadeOut();
		Wait;
	}
}

// This is used by the player to initialize a trace and see if it hits either 
// the player or the shield wall. This is important in order to block rails and
// explosion damage. Rails in particular may have +FOILINVUL, preventing them
// from being stopped, so the only solution is to just not take damage (see
// Doom4player.txt's DamageMobj override).
Class D4ShieldWallTracer : LineTracer
{
	Actor Target;
	override ETraceStatus TraceCallback() 
	{
		switch (Results.HitType) 
		{
			case TRACE_HitActor:
			{
				let mo = Results.HitActor;
				if (mo && (mo is 'D4ShieldWallBarrier' || mo == Target))
					return TRACE_Stop;
					
				return TRACE_Skip;
			}
			case TRACE_HitFloor:
			case TRACE_HitCeiling:
				return TRACE_Stop;

			case TRACE_HitWall:
			{
			//	if (Results.Tier == TIER_Upper || Results.Tier == TIER_Lower) 
			//		return TRACE_Stop;
				
				if (Results.Tier == TIER_Middle)
				{
					let hl = Results.HitLine;
					if (!(hl.flags & (Line.ML_BLOCKING | Line.ML_BLOCKEVERYTHING)))
						return TRACE_Skip;
				}
			}
		}
		return TRACE_Stop;
	}
}