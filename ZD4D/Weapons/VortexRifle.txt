
//===================================================================================================
//===================================================================================================
//
//		Vortex rifle
//
//	Sprites: Neccronixis
//	MuzzleFlash sprite edit: DBThanatos
//	Code: DBThanatos
//
//===================================================================================================
//===================================================================================================
// Primary fire: Regular shot. Pierces enemies
// Secondary fire: Zooms and charges up shot, deals x1.5 as regular primary, doesnt pierce
//   Upgrades:
// ->VortexUpgradeDamage: Increases BASE damage only, that is, unzoomed and base damage when charging
// ->VortexUpgradeAmmo: Decreases ammo usage from 15 to 10
// ->VortexUpgradeAltSpeed: Removes the player slowdown while aiming with the scope
// ->VortexUpgradeAltPierce: Allows alt fire to pierce monsters
//===================================================================================================
const Sparsity = 10.415;
const VortexLength = 2048.0;
enum VortexRifleProps
{
	FlagsNoPierce = RGF_NORANDOMPUFFZ|RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,
	FlagsPierce = 	RGF_NORANDOMPUFFZ|RGF_SILENT|RGF_FULLBRIGHT,
	VortexRifle_AmmoCostN =	12,
	VortexRifle_AmmoCostU =	8,
	VortexRifle_AmmoCostNZ = 15,
	VortexRifle_AmmoCostUZ = 10,
	
	VortexRifle_DamageN = 20,
	VortexRifle_DamageU = 25,
	VortexRifle_DamageMinMulN = 5,
	VortexRifle_DamageMaxMulN = 15,
	VortexRifle_DamageMinMulU = 8,
	VortexRifle_DamageMaxMulU = 16
};

// Damage is calculated as follows:
// Damage(N/U) * random(DamageMinMul(N/U), DamageMaxMul(N/U))
// N = normal
// U = upgraded

Class D4VortexRifle : Doom4Weapon
{
	/*
	enum VortexRifle
	{
		FlagsNoPierce = 	RGF_NORANDOMPUFFZ|RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,
		FlagsPierce = 		RGF_NORANDOMPUFFZ|RGF_SILENT|RGF_FULLBRIGHT,
	};
	const Sparsity = 10.415;
	*/
	Default 
	{
		+FLOORCLIP
		+INVENTORY.UNDROPPABLE
		+INVENTORY.UNTOSSABLE
		+INVENTORY.IGNORESKILL
		+WEAPON.NOAUTOFIRE
		+WEAPON.NOALERT
		Scale 0.6;
		Weapon.AmmoType1 "D4Cells";
		Weapon.AmmoType2 "D4Cells";
		Weapon.AmmoGive1 350;
		Weapon.AmmoUse1 12;	//doesnt matter, since it's taken manually in the weapon code
		Weapon.AmmoUse2 0;
		
		Obituary "%k's mad sniper skills are too much for %o.";
		Inventory.PickupMessage "Vortex Rifle! A sniper's delight!";
		Tag "Vortex Rifle";
	}
	
	States
	{
	Spawn:
		WPPK M -1;
		Wait;
	//-------------------------------------------------------
	// Deselect sequence
	//-------------------------------------------------------
	Deselect:
		VTXG A 0 
		{
			A_StopSound(5);
			A_SetInventory("VortexZoomed",0);
			A_SetInventory("VortexReloadPositionTracker",0);
			A_SetInventory("VortexEnoughAmmo",0);
			A_SetInventory("VortexCharges",0);
			A_ZoomFactor(1.00);
			A_SetSpeed(1.0);
		}
		VTXG A 1 A_Lower;
		VTXG A 0 A_Lower(WeaponSwitchSpeed);
		Loop;
	//-------------------------------------------------------
	// Select sequence
	//-------------------------------------------------------
	Select:
		VTXG A 0 ACS_NamedExecute("RemoveModText",0,0,0,0);
		VTXG A 1 A_Raise;
		VTXG A 0 A_Raise(WeaponSwitchSpeed);
		Goto Select+1;
	//-------------------------------------------------------
	// Ready sequence
	//-------------------------------------------------------
	Ready:
		VTXG A 1 A_WeaponReady(WRF_ALLOWUSER2);
		Loop;
		
	//===========================================================================	
	// Primary fire
	Fire:
		"####" "#" 0 
		{
			// Zoomed
			if (CountInv("VortexZoomed"))
			{
				invoker.AmmoUse1 = CountInv("VortexUpgradeAmmo") ? VortexRifle_AmmoCostUZ : VortexRifle_AmmoCostNZ;
				A_CheckReload();
				
				if (A_JumpIfNoAmmo("Null"))
					return ResolveState("NotEnoughAmmo");
					
				return ResolveState("FireZoomed");
			}
			// Not zoomed
			invoker.AmmoUse1 = CountInv("VortexUpgradeAmmo") ? VortexRifle_AmmoCostU : VortexRifle_AmmoCostN;
			A_CheckReload();
			
			if (A_JumpIfNoAmmo("Null"))
				return ResolveState("NotEnoughAmmo");
				
			return ResolveState("FireNormal");
		}
	NotEnoughAmmo:
		"####" "#" 5 ACS_NamedExecute("NotEnoughAmmo",0,0,0);
		"####" "#" 0
		{
			if(CountInv("VortexZoomed"))
			{	return ResolveState("AltFireHolding");	}
			else
			{	return ResolveState("Ready");	}
		}
		Goto Ready;
		
		
	FireNormal:
		VTXG B 2 Bright
		{
			A_PlaySound("Doom4/Weapon/Vortex/Fire",CHAN_WEAPON);
			
			//normal, min 100, max 300, avg 200
			//upgraded, min 200, max 400, avg 300
			A_GunFlash("Null");
			
			int dmg = (CountInv("VortexUpgradeDamage") ? 
				VortexRifle_DamageU * random(VortexRifle_DamageMinMulU, VortexRifle_DamageMaxMulU) :
				VortexRifle_DamageN * random(VortexRifle_DamageMinMulN, VortexRifle_DamageMaxMulN));
			//	25*random(8,16) : 
			//	20*random(5,15);
			A_RailAttack(dmg,4,true,"","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NORANDOMPUFFZ,0,null,0,0,32767);
			A_FireBullets(0,0,1,0,"PacifistPuff",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_PUFFTRACER,VortexLength,"VortexTrailSpawner",0,5);
			//Ammo, checks for upgrade, takes either 12 or 8
			//A_TakeInventory("D4Cells",CountInv("VortexUpgradeAmmo") ? 8 : 12,TIF_NOTAKEINFINITE);
			
			A_AlertMonsters();
			A_SetPitch(pitch-3);
			A_OverlayOffset(1,24,18,WOF_Add);
		}
		VTXG AAAAAA 1 
		{
			A_SetPitch(pitch+0.5);
			A_OverlayOffset(1,-4,-3,WOF_Add);
			//A_WeaponOffset(-4,-3,WOF_Add);
		}
		VTXG A 27 { A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH); A_SetTics(CountInv("PowerD4DFS") ? 15 : 27); }	//it's a one second delay
		Goto Ready;
		
	FireZoomed:
		R016 N 2 Bright
		{
			A_StopSound(5);
			A_PlaySound("Doom4/Weapon/Vortex/Fire",CHAN_WEAPON);
			//when normal, 
			// min charge -> min 100+(1charges*2)=102, max 300+(1charges*2)=302, avg 202
			// max charge -> min 100+(52charges*2)=204, max 300+(52charges*2)=404, avg 304
			
			//when upgraded,
			// min charge -> min 200+(1charge*2)=202, max 400+(1charges*2)=402, avg 302
			// max charge -> min 200+(52charge*2)=304, max 400+(52charges*2)=504, avg 404
			A_GunFlash("Null");
			
			bool Piercing = (CountInv("VortexUpgradeAltPierce") > 0);
			int toPierce = 0;
			Class<Actor> Puff = null;
			
			if (Piercing)
			{
				toPierce = FlagsPierce;
				Puff = "PacifistPuff";
			}
			else
			{
				toPierce = FlagsNoPierce;
				Puff = "PacifistPuffNoThru";
			}
			//int dmg = (CountInv("VortexUpgradeDamage") ? (25*random(8,16)) : (20*random(5,15))) 
			int dmg = (CountInv("VortexUpgradeDamage") ? 
				VortexRifle_DamageU * random(VortexRifle_DamageMinMulU, VortexRifle_DamageMaxMulU) :
				VortexRifle_DamageN * random(VortexRifle_DamageMinMulN, VortexRifle_DamageMaxMulN))
				+ CountInv("VortexCharges") * 2;
			A_RailAttack(dmg,0,true,"","",toPierce,0,null,0,0,32767);
			A_FireBullets(0,0,1,0,"PacifistPuff",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_PUFFTRACER,VortexLength,"VortexTrailSpawner",0,5);
			//Ammo, checks for upgrade, takes either 15 or 10
			//A_TakeInventory("D4Cells",CountInv("VortexUpgradeAmmo") ? 8 : 12,TIF_NOTAKEINFINITE);
			
			A_AlertMonsters();
			A_SetPitch(pitch-1.5);
			A_TakeInventory("VortexCharges",32768);
			A_TakeInventory("VortexEnoughAmmo",32768); //given during the charge-up
			return A_JumpIf(CountInv("PowerD4DFS"),"FireZoomedFast");
		}
		R016 NNNNNN 1 
		{
			A_SetPitch(pitch+0.25);	//restores pitch
		}
		R016 NNNNNNNNNNNNNNNNNNNNNNNNNNN 1	//27 tics, tracks position via token
		{
			A_GiveInventory("VortexReloadPositionTracker",1);
			if (GetPlayerInput(MODINPUT_BUTTONS|MODINPUT_OLDBUTTONS) & BT_ALTATTACK) //if holding alt fire
			{
				return ResolveState(null);	//does nothing
			}
			else //if alt fire released
			{	return ResolveState("UnzoomWhileReloading"); } //goes to unzoom where it tracks the reload position
		}
		Goto ZoomInSounds;
		
	FireZoomedFast:
		R016 NNN 1 A_SetPitch(pitch+0.5);	//restores pitch
		R016 NNNNNNNNNNNNN 1
		{
			StateLabel next = null;
			
			if (!(GetPlayerInput(MODINPUT_BUTTONS|MODINPUT_OLDBUTTONS) & BT_ALTATTACK)) //if holding alt fire
				A_GiveInventory("VortexReloadPositionTracker",1);	//does nothing
			else //if alt fire released
				next = "UnzoomWhileReloading";
			return ResolveState(next); //goes to unzoom where it tracks the reload position
			
		}
		Goto ZoomInSounds;
			
		UnzoomWhileReloading:
			R016 A 0
			{
				A_StopSound(5);
				A_SetInventory("VortexZoomed",0);
				A_ZoomFactor(1.00);
				A_SetSpeed(1.0);
				A_PlaySound("Doom4/Weapon/Shotgun/CancelAim",6);
			}
			VTXG AAAAAAAAAAAAAAAAAAAAAAAAAAA 1;	//27 tics
			VTXG A 0 A_SetInventory("VortexReloadPositionTracker",0);
			Goto Ready;
		
	//===========================================================================	
	// Alt fire
	AltFire:
		ZoomIn:
			R016 N 0
			{
				A_GiveInventory("VortexZoomed",1);
				if (CountInv("VortexUpgradeAltSpeed"))
				{
					A_ZoomFactor(3.0);
				}
				else
				{
					A_SetSpeed(0.35);
					A_ZoomFactor(2.0);
				}
			}
		ZoomInSounds:
			R016 N 0
			{
				//checks for ammo, to play or not the charging sound
				invoker.AmmoUse1 = CountInv("VortexUpgradeAmmo") ? VortexRifle_AmmoCostUZ : VortexRifle_AmmoCostNZ;
				if (A_JumpIfNoAmmo("Null"))
					A_PlaySound("Doom4/Weapons/Machinegun/ZoomIn",5);
				else
				{
					A_GiveInventory("VortexEnoughAmmo");
					A_PlaySound("Doom4/Weapon/Vortex/ZoomIn",5,1,1);
				}
			}
			Goto AltFireHolding;
				
			AltFireHolding:
				R016 N 1 Bright
				{
					//charges alt fire
					//it has to check if there was ammo, if not, doesnt "charge" the shot
					//I made it this way to not have to check 4 or so "if"s every tic in this sequence
					if (CountInv("VortexEnoughAmmo"))	
					{	A_GiveInventory("VortexCharges",2);	}
					
					//Weapon ready
					A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_NOSECONDARY);
				}	
				R016 N 0
				{
					//checks if alt fire is being held
					if (GetPlayerInput(MODINPUT_BUTTONS|MODINPUT_OLDBUTTONS) & BT_ALTATTACK)
					{
						return ResolveState("AltFireHolding");
					}
					return ResolveState(null);
				}
				Goto AltFireReleaseButton;
					
				AltFireReleaseButton:
					VTXG A 5
					{
						A_StopSound(5);
						A_SetInventory("VortexZoomed",0);
						A_SetInventory("VortexEnoughAmmo",0); //given during the charge-up
						A_SetInventory("VortexCharges",0);
						A_ZoomFactor(1.0);
						A_SetSpeed(1.0);
						A_PlaySound("Doom4/Weapon/Shotgun/CancelAim",6);
					}
					Goto Ready;
	}
}

Class VortexTrailSoftware : Actor 
{	// [DBT] you cant say i dont love you guys, i even recolored to doom pallete!
	// [MC] Yeah well I don't.
	Default 
	{
		+NOINTERACTION
		RenderStyle "Add";
		Scale 0.15;
	}
	States
	{
	Spawn:
		CBA3 BBBBB 1 Bright;
		CBA3 B 1 Bright A_FadeOut(0.05);
		Wait;
	}
}

//--------------------------------------------------------------------------------
//	Weapon tokens

Class VortexCharges : Inventory 	//the charged shot counter
{ Default { Inventory.MaxAmount 52; } }


Class VortexEnoughAmmo : Inventory {}

Class VortexZoomed : Inventory {}

Class VortexReloadPositionTracker : Inventory 
{
	Default 
	{
		Inventory.MaxAmount 27 ;
		Inventory.InterHubAmount 0;
	}
}