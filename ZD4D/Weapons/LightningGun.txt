const LGDmgMin = 3;
const LGDmgMax = 6;
const LGDmgMinUpgrade = 6;
const LGDmgMaxUpgrade = 10;

const LGChainDmgMin = 2;
const LGChainDmgMax = 5;
const LGChainDmgMinUpgrade = 4;
const LGChainDmgMaxUpgrade = 8;

const AltTrailLength = 256; //Indicates how far a beam can zap others with secondary.
const LGHeight = -2;
const LGOff = 2.1;
const LGDist = 10;
const FBFlags = FBF_USEAMMO|FBF_PUFFTRACER|FBF_NORANDOMPUFFZ;
const FBFlagsNoAmmo = FBF_PUFFTRACER|FBF_NORANDOMPUFFZ;


Class D4LightningGun : Doom4Weapon
{
	Default 
	{
		Doom4Weapon.MultiplayerWeapon true;
		+FLOORCLIP
		+DONTGIB
		+INVENTORY.UNDROPPABLE
		+INVENTORY.UNTOSSABLE
		+INVENTORY.IGNORESKILL
		Scale 0.6;
		Weapon.AmmoType1 "D4Cells";
		Weapon.AmmoType2 "D4Cells";
		Weapon.AmmoGive1 40;
		Weapon.AmmoUse1 1;
		Weapon.AmmoUse2 1;
		Weapon.SlotNumber 6;
		Weapon.SlotPriority 0.99;
		
		Obituary "%o was left thunder struck by %k's lightning gun.";
		Inventory.PickupMessage "Lightning gun! The firepower is shocking!";
		Tag "Lightning Gun";
		Decal "D4LGDecal";
		Weapon.SelectionOrder 60;
		//Decal "Q4LGDecal"	//removed because when you are firing this weapon and turn too fast, it looks innacurate
	}
	
	action void A_HoldLightningGun(bool sw = false, bool useammo = true)
	{
		if (sw)	A_WeaponOffset(frandom(1.2,2.2),frandom(33.2,34.2));
		else	A_WeaponOffset(frandom(1.0,2.0),frandom(33.0,34.0));
		A_GunFlash("Null");
		int dmg = CountInv("LGUpgradeDamage") ? random(LGDmgMinUpgrade,LGDmgMaxUpgrade) : random(LGDmgMin,LGDmgMax);
	//	int Flags = ((CountInv("LGUpgradeAmmo") && !random(0,2)) ? FBFlagsNoAmmo : FBFlags);
		int Dist = CountInv("LGUpgradeRange") ? 1024 : 768;
		int dbl = DoubleSpeed();
		
		if (useammo && random[rng](0,1))
			invoker.DepleteAmmo(false, true, invoker.AmmoUse1 * dbl);
			
		A_UpdateBeamTracer("LightningPuffV2",dmg * dbl,0,0,Dist);
	//	A_UpdateBeamTracer("D4BulletPuff",dmg * dbl,0,0,Dist);
	}
	
	States
	{
	Spawn:
		WPPK N -1;
		Wait;
	//-------------------------------------------------------
	// Deselect sequence
	//-------------------------------------------------------
	Deselect:
		LGNI A 1
		{
			A_StopBeam("");
			A_SetInventory("AltFireToken",0);
			ACS_NamedExecute("RemoveModText",0,0,0,0);
			A_StopSound(5);
			A_Lower();
		} 
		---- A 1 A_Lower(WeaponSwitchSpeed);
		Wait;
	//-------------------------------------------------------
	// Select sequence
	//-------------------------------------------------------
	Select: 
		LGNI A 0 
		{
			ACS_NamedExecute("RemoveModText",0,0,0,0);
		}
		---- A 1 A_Raise(WeaponSwitchSpeed);
		Wait;
	//-------------------------------------------------------
	// Ready sequence
	//-------------------------------------------------------
	Ready:
		LGNI A 0 
		{
			A_SetInventory("AltFireToken",0);
			if (CountInv("LGUpgradeAltFire"))
			{ return ResolveState("ReadyWithAltFire"); }
			else
			{ return ResolveState("ReadyNoAltFire"); }
		}
	ReadyWithAltFire:
		LGNI AABBCC 1 A_WeaponReady(WRF_ALLOWUSER2);
		Goto Ready;
	ReadyNoAltFire:
		LGNI AABBCC 1 A_WeaponReady(WRF_ALLOWUSER2|WRF_NOSECONDARY);
		Goto Ready;
		
	//===========================================================================	
	// Primary fire
	Fire:
		TNT1 A 0 
		{
			A_PlaySound("Doom4/Weapon/LightningGun/LightningGunFire", 5, 1, 1);
			A_PlaySound("Doom4/Weapon/LightningGun/LG2Loop", CHAN_WEAPON, 0.5, 1);
			int Dist = CountInv("LGUpgradeRange") ? 1024 : 768;
			A_StartBeam("NewLightningBeamPrimary",(LGDist, LGOff, LGHeight), 1, "LightningPuffV2", 0, 0, 0, Dist);
		}
		
	HoldPt1:
		LGNF B 1 Bright A_HoldLightningGun(false, true);
		
		//the second frame never uses ammo anyway, so no need to add those checks
		LGNF C 1 Bright A_HoldLightningGun(true, false);

		LGNF A 0 A_ReFire("HoldPt1");
		LGNF A 2
		{
			A_StopSound(5);
			A_StopBeam("Doom4/Weapon/LightningGun/LightningGunEnd");
		}
		Goto EndHold;
	EndHold:
		LGNI A 1;
		Goto Ready;		
	Flash:
		TNT1 A 0 A_SetBlend("LightBlue1",0.1,3);
		ZZZZ C 1 A_Light2();
		ZZZZ C 0 A_Light0();
		Stop;
		
	//===========================================================================	
	// Alt Fire
	AltFire:
		LGNG A 3 A_PlaySound("Doom4/Weapon/LightningGun/LightningGunFire", CHAN_WEAPON, 1, 1);
		LGNG B 3 A_GiveInventory("AltFireToken",1);
		Goto AltHold;
		
	AltHold:
		LGNG CD 1 Bright 
		{
			A_GunFlash("Null");
			A_WeaponOffset(frandom(1,3),frandom(33,35)); 
			
			int dmg = CountInv("LGUpgradeDamage") ? random(LGDmgMinUpgrade,LGDmgMaxUpgrade) : random(LGDmgMin,LGDmgMax);
			int Flags = FBFlags; 
			int Dist = CountInv("LGUpgradeRange") ? 1024 : 768;
			
			A_FireBullets(0,0,1,dmg,"LightningPuffV3",Flags,Dist,"LightningBeamSecondarySpawner",LGHeight,LGDist);
			
			if (IsDoubleSpeed())
				A_FireBullets(0,0,1,dmg,"LightningPuffV3",Flags,Dist,"LightningBeamSecondarySpawner",LGHeight,LGDist);
			
		}
		LGNG B 1 A_ReFire("AltHold");	
		Goto AltEndHold;
	AltEndHold:
		LGNI A 0 
		{
			A_PlaySound("Doom4/Weapon/LightningGun/LightningGunEnd", CHAN_WEAPON);
			A_StopSound(5);
		}
		LGNG B 3;
		LGNG A 3 A_TakeInventory("AltFireToken",32767);
		Goto Ready;	
	}
}

//---------------------------------------------------------------------------
//---------------------------------------------------------------------------

Class LightningPuffFX : D4DFX
{
	double FadeTime;
	Default
	{
		+FLATSPRITE
		+ROLLSPRITE
		+FORCEXYBILLBOARD
		+BRIGHT
		+NOINTERACTION
		+NOTIMEFREEZE
		RenderStyle "Add";
		Alpha 0.99;
	}
	override void PostBeginPlay()
	{
		FadeTime = frandom(0.1, 0.3);
		Roll = random(0,359);
		Angle = random(0,359);
		Pitch = random(-90, 90);
		Scale.X = Scale.Y = frandom(0.06, 0.12);
		Super.PostBeginPlay();
	}
	
	States
	{
	Spawn:
		X15A A 1 A_FadeOut(FadeTime);
		Wait;
	}
}
Class LightningPuffV2 : Actor 
{
	Default 
	{
		Projectile;
		+NOINTERACTION
		+NOTONAUTOMAP
		+ALWAYSPUFF
		+PUFFGETSOWNER
		+PUFFONACTORS
		+SKYEXPLODE
		+EXTREMEDEATH
		+DONTSPLASH
		+NOTIMEFREEZE
		RenderStyle "None";
	}
	
	void SpawnSparks(int count, int chance = 0)
	{
		if (count < 1)	return;
		
		for (int i = 0; i < count; i++)
			A_SpawnItemEx("LightningPuffFX",flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: chance);
	}
	
	States
	{
	Spawn:
		TNT1 A 5 NoDelay SpawnSparks(random(4,6), 128);
		Stop;
	}
}

Class LightningPuffV2NoPain : LightningPuffV2	{ Default { +PAINLESS +NOTIMEFREEZE }}

Class LightningPuffV3 : LightningPuffV2
{
	Array<Actor> Monsters;
	Default 
	{
		+HITTRACER
		+EXTREMEDEATH
		MissileHeight 0.5;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay SpawnSparks(random(4,6), 128);
		TNT1 A 5 
		{	
			bEXTREMEDEATH = false;
			int maxcount = Tracer ? 4 : 5;
			let handler = MCHandler.Fetch();
			if (handler && handler.Monsters.Size() > 0)
			{
				int dmg = random(LGChainDmgMin, LGChainDmgMax);
				if (target && target.CountInv("LGUpgradeDamage"))
					dmg = random(LGChainDmgMinUpgrade, LGChainDmgMaxUpgrade);
				int count = 0;
				int size = handler.Monsters.Size();
				for (int i = 0; i < size; i++)
				{
					Actor mo = handler.Monsters.Actors[i];
					// Make sure it exists first and is alive. Don't target enemies directly hit.
					if (!mo || mo.health < 1 || mo.player || mo == Tracer || mo.bFRIENDLY)
						continue;
					
					// Don't use square root versions.
					if (Distance3DSquared(mo) > (AltTrailLength ** 2) || 
						!CheckSight(mo,SF_SEEPASTSHOOTABLELINES|SF_IGNOREVISIBILITY|SF_IGNOREWATERBOUNDARY))
						continue;
					
					A_Face(mo,0,0,0,0,FAF_MIDDLE);					
					mo.DamageMobj(self, target, dmg, 'Electric');
					
					Vector3 SH = mo.pos;
					SH.z += mo.height / 2.0;
					Actor Puff = Spawn("LightningPuffV2", SH);
					if (Puff)
					{
						Class<Actor> lname = "LightningBeamSecondarySpawner";
						let Beamer = LightningBeamSecondarySpawner(Spawn(lname,pos));
						if (Beamer)
						{
							Beamer.MoveWithSource = false;
							Beamer.Target = Target;
							Beamer.Master = Target;
							Beamer.Tracer = Puff;
							Beamer.Angle = Angle;
							Beamer.Pitch = Pitch;
						}
					}
					if (count++ >= maxcount)
						return;
				}
			}
		}
		Stop;
	}
}

Class LGPuff : Actor 
{
	Default 
	{
		+CLIENTSIDEONLY
		Radius 10;
		+NOINTERACTION
		+ALWAYSPUFF
		+PUFFGETSOWNER
		Scale 0.35;
		RenderStyle "Add";
		Obituary "%o was electrocuted by %k's lightning gun.";
		//Alpha 0.6;
		+FORCEXYBILLBOARD
	}
	States
	{
	Spawn:
		//TNT1 A 0 NODELAY A_Jump(256,random(1,24));
		DB08 ABCDEFGHIJLKMNOPQRSTUVWXYZ 0 BRIGHT A_JumpIf(1 == 1,"Play");
	Play:
		"####" "##" 1 Bright;
		Stop;
	}
}

Class LGPuffNoPain : LGPuff
{
	Default 
	{
		+PAINLESS
		+BLOODLESSIMPACT
	}
}

Class ChainToken : Inventory {}

Class LGChainToggle : Inventory
{
	Default
	{
		+INVENTORY.UNDROPPABLE
		+INVENTORY.UNTOSSABLE
		Inventory.InterhubAmount 1;
	}
}

Class LGAltPuff : Nothing {}
Class D4LGTrailAltFire : Nothing {}