
//===================================================================================================
//===================================================================================================
//
//		Grenade Launcher
//
//	Model: iD Software
//	Sprites: Major Cooke
//	Coding:	Major Cooke
//
//===================================================================================================
//===================================================================================================

Class D4GrenadeLauncher : Doom4Weapon
{
	Default
	{
	//	Scale 0.5;
		Doom4Weapon.MultiplayerWeapon true;
		Tag "Grenade Launcher";
		Inventory.PickupMessage "Grenade Launcher! Trickshot time!";
		Weapon.SlotNumber 1;
		Weapon.AmmoType1 "D4Rockets";
		Weapon.AmmoType2 "D4Rockets";
		Weapon.AmmoGive1 4;
		Weapon.AmmoUse1 1;
		Weapon.SelectionOrder 79;
		Weapon.SlotNumber 5;
		Weapon.SlotPriority 0.01;
	}
	States
	{
	Select:
		M202 A 1 A_Raise;
		M202 A 0 A_Raise(WeaponSwitchSpeed);
		Loop;
	Deselect:
		M202 A 1 A_Lower;
		M202 A 0 A_Lower(WeaponSwitchSpeed);
		Loop;
	Ready:
		M202 A 1
		{
			A_WeaponReady(WRF_ALLOWUSER2);
		}
		Wait;
	Fire:
		M202 A 2 bright
		{
			FireProjectile("D4Grenade", (-1.0, 8.0, -7.0));
			A_PlaySound("Doom4/Weapon/GrenadeLauncher/Fire", CHAN_WEAPON);
			if (IsDoubleSpeed())
				A_Overlay(7001, "Overlay.OffsetsFast");
			else
				A_Overlay(7001, "Overlay.Offsets");
			
			A_Overlay(7000, "Overlay.Pitcher");
		}
		M202 A 6;
		M202 BCD 4;
		M202 A 6;
		M202 A 0 A_Refire("Fire");
		Goto Ready;
	Overlay.Offsets:
		TNT1 A 1 A_WeaponOffset(3.0, 12, WOF_ADD);
		TNT1 A 1 A_WeaponOffset(2.5, 8, WOF_ADD); 
		TNT1 A 1 A_WeaponOffset(2.0, 4, WOF_ADD); 
		TNT1 A 1 A_WeaponOffset(1.5, 3, WOF_ADD); 
		TNT1 A 1 A_WeaponOffset(1.0, 2, WOF_ADD); 
		TNT1 A 4 A_WeaponOffset(0.5, 1, WOF_ADD); 
		// ^ 9 tics | 10.5 | 30
		TNT1 A 1 A_WeaponOffset(-0.25, -1, WOF_ADD); //10.25
		TNT1 A 1 A_WeaponOffset(-0.50, -2, WOF_ADD); //09.75
		TNT1 A 1 A_WeaponOffset(-0.75, -3, WOF_ADD); //09.00
		TNT1 A 1 A_WeaponOffset(-1.00, -4, WOF_ADD); //08.00
		TNT1 A 1 A_WeaponOffset(-1.50, -4, WOF_ADD); //06.50
		TNT1 A 1 A_WeaponOffset(-2.00, -4, WOF_ADD); //04.50
		TNT1 A 1 A_WeaponOffset(-1.50, -4, WOF_ADD);
		TNT1 A 1 A_WeaponOffset(-1.00, -3, WOF_ADD);
		TNT1 A 1 A_WeaponOffset(-1.00, -2, WOF_ADD);
		TNT1 A 1 A_WeaponOffset(-1.00, -1.5, WOF_ADD);
		TNT1 A 1 A_WeaponOffset(-0.00, -1.0, WOF_ADD);
		TNT1 A 1 A_WeaponOffset(-0.00, -0.5, WOF_ADD);
		// 12 + 9 = 21
		Stop;
	Overlay.OffsetsFast:
		TNT1 A 0 A_WeaponOffset(3.0, 12, WOF_ADD);
		TNT1 A 1 A_WeaponOffset(2.5, 8, WOF_ADD); 
		TNT1 A 0 A_WeaponOffset(2.0, 4, WOF_ADD); 
		TNT1 A 1 A_WeaponOffset(1.5, 3, WOF_ADD); 
		TNT1 A 0 A_WeaponOffset(1.0, 2, WOF_ADD); 
		TNT1 A 4 A_WeaponOffset(0.5, 1, WOF_ADD); 
		// ^ 9 tics | 10.5 | 30
		TNT1 A 0 A_WeaponOffset(-0.25, -1, WOF_ADD); //10.25
		TNT1 A 1 A_WeaponOffset(-0.50, -2, WOF_ADD); //09.75
		TNT1 A 0 A_WeaponOffset(-0.75, -3, WOF_ADD); //09.00
		TNT1 A 1 A_WeaponOffset(-1.00, -4, WOF_ADD); //08.00
		TNT1 A 0 A_WeaponOffset(-1.50, -4, WOF_ADD); //06.50
		TNT1 A 1 A_WeaponOffset(-2.00, -4, WOF_ADD); //04.50
		TNT1 A 0 A_WeaponOffset(-1.50, -4, WOF_ADD);
		TNT1 A 1 A_WeaponOffset(-1.00, -3, WOF_ADD);
		TNT1 A 0 A_WeaponOffset(-1.00, -2, WOF_ADD);
		TNT1 A 1 A_WeaponOffset(-1.00, -1.5, WOF_ADD);
		TNT1 A 0 A_WeaponOffset(-0.00, -1.0, WOF_ADD);
		TNT1 A 1 A_WeaponOffset(-0.00, -0.5, WOF_ADD);
		Stop;
	Overlay.Pitcher:
		TNT1 A 2 A_ZoomFactor(0.9, ZOOM_NOSCALETURNING);
		TNT1 A 1 A_ZoomFactor(0.92, ZOOM_NOSCALETURNING);
		TNT1 A 1 A_ZoomFactor(0.94, ZOOM_NOSCALETURNING);
		TNT1 A 1 A_ZoomFactor(0.96, ZOOM_NOSCALETURNING);
		TNT1 A 1 A_ZoomFactor(0.98, ZOOM_NOSCALETURNING);
		TNT1 A 1 A_ZoomFactor(1.0, ZOOM_NOSCALETURNING);
		Stop;
	Overlay.PitcherFast:
		TNT1 A 1;
		Stop;
	Spawn:
		M202 Z -1;
		Stop;
	}
}

Class D4Grenade : Actor
{
	Default
	{
		Scale 1.2;
		Speed 30;
		Damage 25;
		Gravity 0.7;
		BounceFactor 0.7;
		WallBounceFactor 0.7;
		Radius 8;
		Height 8;
		Projectile;
		BounceType "Doom";
		+FORCERADIUSDMG
		+EXTREMEDEATH
		+CANBOUNCEWATER
		-BOUNCEAUTOOFF
		+BOUNCEAUTOOFFFLOORONLY
		ReactionTime 99;
		BounceSound "Doom4/Weapon/GrenadeLauncher/Bounce";
		DeathSound "Doom4/Weapon/GrenadeLauncher/Explode";
	}
	
	void ExplodeEffects()
	{
		A_QuakeEx(2,2,2,15,0,256,"",QF_SCALEDOWN,0,0,0,1);
		if(GetCvar("HQEffectsWeapons"))
		{
			A_SpawnItemEx("GenericExplosionParticleSpawner",0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
			A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
			A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
			A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
			A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
			A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
			A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
		}
		A_SpawnItemEx("D4RocketExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);	//Center
	}
	
	States
	{
	Spawn:
		GRNL AAAA 1 A_SpawnItemEx("D4RocketTrailSmoke",-5,random(5,-5),random(5,-5),0,0,0,0,SXF_NOCHECKPOSITION);
		GRNL A 1
		{
			A_SpawnItemEx("D4RocketTrailSmoke",-5,random(5,-5),random(5,-5),0,0,0,0,SXF_NOCHECKPOSITION);
			bNOGRAVITY = false;
			A_CountDown();
		}
		Wait;
	Death:
		TNT1 A 50 
		{
			bNOINTERACTION = true;
			A_Stop();
			A_Explode(130,180,0,0,60);	//Damage to enemies
			A_Explode(30,128);			//Damage to shooter
			ExplodeEffects();
		}
		Stop;
	}
}