Class D4Fog : Actor //replaces TeleportFog
{
	Default
	{
		+NOBLOCKMAP
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		Scale 0.75;
		SeeSound "";
	}
	States
	{
	Spawn:
		DB04 A 1 NoDelay A_Playsound("Doom4/Other/Teleport");
		DB04 ABCDEFGHIJ 1 Bright;
		DB04 KLMNOPQRSTUVWXYZ 2 Bright A_FadeOut(0.03);
		Stop;
	}
}

// Used to trigger telefragging and the likes when the player is invulnerable
// and they have Death Bringer's Shadow.
Class D4PlayerFog : D4Fog
{
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		let o = Doom4Player(target);
		if (o)	o.DoTeleportEffects();
	}
}

Class D4ExplosiveBarrel : ExplosiveBarrel //replaces ExplosiveBarrel
{
	Default
	{
		Health 20;
		Radius 10;
		Height 42;
		+SOLID
		+SHOOTABLE
		+NOBLOOD
		+ACTIVATEMCROSS
		+DONTGIB
		+NOICEDEATH
		+OLDRADIUSDMG
		+EXTREMEDEATH
		DeathSound "world/barrelx";
		Obituary "$OB_BARREL"; // "%o went boom."
	}
	States
	{
	Spawn:
		BAR1 AB 6;
		Loop;
	Death:
		BEXP A 5 Bright;
		BEXP B 5 Bright A_Scream();
		BEXP C 5 Bright A_SpawnItemEx("BarrelExplosionFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
		BEXP R 2 Bright 
		{
			if(GetCvar("D4D_quakexplosions"))
				A_QuakeEx(2,2,2,15,0,768,"",QF_SCALEDOWN,0,0,0,1);
				
			A_Explode();
			A_SpawnItemEx("GenericExplosionParticleSpawner",0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		}
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
		BEXP R 1 A_FadeOut();
		Wait;
	}
}

Class D4EmptyBarrel : Actor 
{
	int user_c;
	
	Default
	{
		Health 20;
		Radius 10;
		Height 42;
		+DONTCORPSE
		+SOLID
		+SHOOTABLE
		+NOBLOOD
		+ACTIVATEMCROSS
		+DONTGIB
		+NOICEDEATH
		Translation "0:255=%[0,0,0]:[1,1,1]";
	}
	States
	{
	Spawn:
		BAR1 A 6;
		Loop;
	Death:
		BEXP R 105
		{
			bTHRUACTORS = true;
			A_PlaySound("Doom4/Other/BarrelBreak");
			user_c = random(8,16);
			while (user_c > 0)
			{
				A_SpawnItemEx("D4EmptyBarrelChunk",frandom(0,10),0,frandom(10.0,42.0),vel.x,vel.y,vel.z,random(0,359),SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY);
				user_c--;
			}
		}
		"####" "#" 1 A_FadeOut(0.05);
		Wait;
	}
}

Class D4EmptyBarrelChunk : Actor 
{
	double URoll;
	double USpeed;
	
	Default
	{
		Projectile;
		+THRUACTORS
		-NOGRAVITY
		+ROLLSPRITE
		BounceType "Doom";
		Translation "0:255=%[0,0,0]:[0.5,0.5,0.5]";
	}
	States
	{
	Spawn:
		ICEC A 0 NoDelay
		{
			A_SetScale(frandom(0.8,1.2));
			A_FaceMovementDirection(0,0,0);
			angle += frandom(-20.0, 20.0);
			pitch += frandom(-20.0, 20.0);
			USpeed = vel.Length() + random(1,4);
			A_ChangeVelocity(cos(-pitch)*USpeed, 0, sin(pitch)*USpeed, CVF_RELATIVE);
			URoll = frandom(2.0, 15.0) * randompick(1,-1);
		}
		ICEC A 1 A_SetRoll(roll + URoll, SPF_INTERPOLATE);
		Wait;
	Death:
		ICEC A 105 A_SetRoll(0);
		ICEC A 1 A_FadeOut();
		Wait;
	}
}

Class BarrelExplosionFX : Actor 
{
	Default
	{
		+NOINTERACTION
		Scale 0.8;
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(128,"Type2");
	Type1:
		DB27 ABCDEFGHIJKKLLMMNNOOPPQQRRSSTTUU 1 Bright
		{
			A_SetScale(Scale.X+0.017);
			A_Warp(MasterPtr,0,0,00,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		}
		DB27 WWXXYY 1 Bright
		{
			A_SetScale(Scale.X+0.021);
			A_FadeOut(0.02);
			A_Warp(MasterPtr,0,0,00,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		}
		Wait;
	Type2:
		DB28 ABCDEFGHIJKKLLMMNNOOPPQQRRSSTTUU 1 Bright
		{
			A_SetScale(Scale.X+0.017);
			A_Warp(MasterPtr,0,0,00,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		}
		DB28 WWXXYY 1 Bright
		{
			A_SetScale(Scale.X+0.021);
			A_FadeOut(0.02);
			A_Warp(MasterPtr,0,0,00,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		}
		Wait;
	}
}













//=============================================================================
//=============================================================================
//	Stuff for the titlemap
//=============================================================================
//=============================================================================
Class titlemapsmokeemitter : Actor 
{
	Default
	{
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 A 12 NODELAY A_SpawnItemEx("TitleMapSmoke",0,random(350,-350),0,  0,random(6,-6),random(8,15), 0,SXF_NOCHECKPOSITIoN);
		Loop;
	}
}

Class TitleMapSmoke : Actor 
{
	Default
	{
		+NOINTERACTION
		+FLATSPRITE
		RenderStyle "Add";
		//Scale 9.0;
		Scale 2.0;
		Alpha 0.3;
		Translation "0:255=%[0,0,0]:[1,0,0]";
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY 
		{
			A_SetAngle(90);
			A_SetPitch(90);
			return A_Jump(200,3,5,7,9,11,13,15,17,19);
		}
		X110 ABCDEFGHIJKLMNOPQRSTUVWXY 2 Bright
		{
			if(Scale.X <= 9)
			{
				A_SetScale(Scale.X+0.8);
			}
			A_FadeOut(0.007);
			A_ScaleVelocity(0.98);
		}
		Goto Spawn+1;
	}
}




Class titlemapfiresmokeemitter : Actor 
{
	Default
	{
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 A 60 NODELAY A_SpawnItemEx("TitleMapSmokeOrangeIsh",0,random(100,-100),500,  0,random(-10,-20),random(1,5), 0,SXF_NOCHECKPOSITIoN);
		Loop;
	}
}
Class TitleMapSmokeOrangeIsh : Actor 
{
	Default
	{
		+NOINTERACTION
		+ROLLSPRITE
		+FLATSPRITE
		RenderStyle "Add";
		//Scale 18.0
		//Scale 2.0
		Alpha 0.3;
		Translation "0:255=%[0,0,0]:[1,0.2,0]";
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY 
		{
			A_SetAngle(90);
			A_SetPitch(90);
			A_SetRoll(roll+random(0,359));
			return A_Jump(200,3,5,7,9,11,13);
		}
		X101 ABCDEFGHIJKLMNOP 2 Bright
		{
			if (Scale.X <= 18)
			{
				A_SetScale(Scale.X+1.2);
			}
			A_FadeOut(0.001);
			A_SetRoll(roll-1);
			//A_ScaleVelocity(0.98);
		}
		Goto Spawn+1;
	}
}





Class titlemapFlaresSpawner : Actor
{
	Default
	{
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 A 8 NODELAY A_SpawnItemEx("TitlemapFlares",0,random(300,-300),0,  0,random(-15,15),random(4,15), 0,SXF_NOCHECKPOSITIoN,96);
		Loop;
	}
}
Class TitlemapFlares : Actor 
{
	Default
	{
		+NOINTERACTIoN
		RenderStyle "Add";
		//Scale 18.0;
		Scale 0.4;
		//Alpha 0.3;
		//Translation "0:255=%[0,0,0]:[1,0.2,0]";
		+ROLLSPRITE
	}
	States
	{
	Spawn:
		TNT1 A 0 NODELAY 
		{
			A_SetRoll(roll+random(0,359));
			return A_Jump(200,3,5,7,9,11,13);
		}
		X029 AABBCCDDEEFFGGHHIIJJKK 2 Bright
		{
			A_FadeOut(0.001);
			A_SetRoll(roll-1);
			if(random(1,5)==5)
			{
				A_ChangeVelocity(0,random(5,-5),0);
			}
		}
		Goto Spawn+1;
	}
}



//=============================================================================
//=============================================================================
//	Stuff for the maps
//=============================================================================
//=============================================================================
Class FireSpawnerSmall : Actor 
{
	Default
	{
		+NOINTERACTION
		Radius 5;
		Height 3;
	}
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("FlmThrwrSmoke",random(5,-5),random(0,10),random(-5,5),frandom(0.3,-0.3),0,frandom(0.2,1.0),0,32,196);
		TNT1 A 0 A_SpawnItemEx("FTFlameTrail2",random(5,-5),random(0,10),random(-5,5),frandom(0.3,-0.3),0,frandom(0,1),0,32,128);
		TNT1 A 2 Light("OrangeMediumFlicker3");
		TNT1 A 0 A_SpawnItemEx("FlmThrwrSmoke",random(5,-5),random(0,10),random(5,5),frandom(0.3,-0.3),0,frandom(0.2,1.0),0,32,196);
		TNT1 A 2 Light("OrangeMediumFlicker3") A_SpawnItemEx("FTFlameTrail2",random(5,-5),random(0,10),random(0,5),frandom(0.3,-0.3),0,frandom(0,1),0,32,128);
		TNT1 A 0 A_SpawnItemEx("FlmThrwrSmoke",random(5,-5),random(0,10),random(-5,5),frandom(0.3,-0.3),0,frandom(0.2,1.0),0,32,196);	//see archvile.aed for this actor
		TNT1 A 0 A_SpawnItemEx("FTFlameTrail2",random(5,-5),random(0,10),random(-5,5),frandom(0.3,-0.3),0,frandom(0,1),0,32,128);
		TNT1 A 2 Light("OrangeMediumFlicker3");
		TNT1 A 0 A_SpawnItemEx("FlmThrwrSmoke",random(5,-5),random(0,10),random(5,5),frandom(0.3,-0.3),0,frandom(0.2,1.0),0,32,196);
		TNT1 A 2 Light("OrangeMediumFlicker3") A_SpawnItemEx("FTFlameTrail2",random(5,-5),random(0,10),random(0,5),frandom(0.3,-0.3),0,frandom(0,1),0,32,128);
		Loop;
	}
}

Class GenericSparkSpawner : Actor 
{
	Default
	{
		+NOINTERACTION
		Radius 5;
		Height 3;
	}
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 1 A_SetTics(random(10,150));
		TNT1 AAA 3
		{
			A_SpawnParticle("FFBE26",SPF_FULLBRIGHT|SPF_RELATIVE,random(20,70),random(2,5),0,0,0,0,frandom(0.1,1),frandom(-1,1),frandom(-1,-3));
			A_SpawnParticle("FFBE26",SPF_FULLBRIGHT|SPF_RELATIVE,random(20,70),random(2,5),0,0,0,0,frandom(0.1,1),frandom(-1,1),frandom(-1,-3));
			
			A_SpawnParticle("F0A800",SPF_FULLBRIGHT|SPF_RELATIVE,random(20,70),random(2,5),0,0,0,0,frandom(0.1,1),frandom(-1,1),frandom(-1,-3));
			A_SpawnParticle("F0A800",SPF_FULLBRIGHT|SPF_RELATIVE,random(20,70),random(2,5),0,0,0,0,frandom(0.1,1),frandom(-1,1),frandom(-1,-3));
			
			A_SpawnParticle("FFD77A",SPF_FULLBRIGHT|SPF_RELATIVE,random(20,70),random(2,5),0,0,0,0,frandom(0.1,1),frandom(-1,1),frandom(-1,-3));
			A_SpawnParticle("FFD77A",SPF_FULLBRIGHT|SPF_RELATIVE,random(20,70),random(2,5),0,0,0,0,frandom(0.1,1),frandom(-1,1),frandom(-1,-3));
			
			A_SpawnParticle("FFA82E",SPF_FULLBRIGHT|SPF_RELATIVE,random(20,70),random(2,5),0,0,0,0,frandom(0.1,1),frandom(-1,1),frandom(-1,-3));
			A_SpawnParticle("FFA82E",SPF_FULLBRIGHT|SPF_RELATIVE,random(20,70),random(2,5),0,0,0,0,frandom(0.1,1),frandom(-1,1),frandom(-1,-3));
		}
		Loop;
	}
}

//=============================================================================
//=============================================================================
//	monster corpse fix - V
//=============================================================================
//=============================================================================
//	Caco
//=============================================================================
Class D4DeadOldCacoDemon : DeadCacodemon
{
	Default { +CORPSE }		
	States
	{
	Spawn:
		HEAD L -1; 
		Stop;
	Raise:
		HEAD LKJIHG 8;
		TNT1 A 0 
		{
			A_SpawnItemEx("D4OldCacodemon",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
			Destroy();
		}
	}
}
Class D4DeadCacoDemon : DeadCacodemon
{
	Default { +CORPSE }		
	States
	{
	Spawn:
		CAC2 L -1; 
		Stop;
	Raise:
		CAC2 LKJIHG 8;
		TNT1 A 0 
		{
			A_SpawnItemEx("D4Cacodemon",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
			Destroy();
		}
	}
}
Class D4CacoCorpse : Actor replaces DeadCacodemon
{
	Default 
	{
		Radius 20;
		Height 56;
	}
	override void PostBeginPlay()
	{
		if (GetCvar("D4D_MonsterType") == 0)
		{
			A_SpawnItemEx("D4DeadOldCacoDemon",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
		}
		else
		{
			A_SpawnItemEx("D4DeadCacoDemon",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
		}
		Destroy();
	}
}
//=============================================================================
//	Demon
//=============================================================================
Class D4DeadOldDemon : DeadDemon
{
	Default { +CORPSE }		
	States
	{
	Spawn:
		SARG N -1; 
		Stop;
	Raise:
		SARG NMLKJI 5;
		TNT1 A 0 
		{
			A_SpawnItemEx("D4OldDemon",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
			Destroy();
		}
	}
}
Class D4DeadDemon : DeadDemon
{
	Default { +CORPSE }		
	States
	{
	Spawn:
		D4DG N -1; 
		Stop;
	Raise:
		D4DG NMLKJI 5;
		TNT1 A 0 
		{
			A_SpawnItemEx("D4Demon",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
			Destroy();
		}
	}
}
Class D4DemonCorpse : Actor replaces DeadDemon
{
	Default 
	{
		Radius 20;
		Height 56;
	}
	override void PostBeginPlay()
	{
		if (GetCvar("D4D_MonsterType") == 0)
		{
			A_SpawnItemEx("D4DeadOldDemon",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
		}
		else
		{
			A_SpawnItemEx("D4DeadDemon",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
		}
		Destroy();
	}
}
//=============================================================================
//	Imp
//=============================================================================
Class D4DeadOldDoomImp : DeadDoomImp
{
  Default { +CORPSE }		
  States
  {
  Spawn:
    TROO M -1; 
	Stop;
  Raise:
    TROO MLKJI 8;
	TNT1 A 0 
	{
	  A_SpawnItemEx("D4OldDoomImp",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
	  Destroy();
	}
  }
}
Class D4DeadDoomImp : DeadDoomImp
{
  Default { +CORPSE }		
  States
  {
  Spawn:
    ROAC M -1; 
	Stop;
  Raise:
    ROAC MLKJI 8;
	TNT1 A 0 
	{
	  A_SpawnItemEx("D4DoomImp",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
	  Destroy();
	}
  }
}
Class D4ImpCorpse : Actor replaces DeadDoomImp
{
	Default 
	{
		Radius 20;
		Height 56;
	}
	override void PostBeginPlay()
	{
		if (GetCvar("D4D_MonsterType") == 0)
		{
			A_SpawnItemEx("D4DeadOldDoomImp",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
		}
		else
		{
			A_SpawnItemEx("D4DeadDoomImp",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
		}
		Destroy();
	}
}
//=============================================================================
//	Shotguy
//=============================================================================
Class D4DeadOldShotgunGuy : DeadShotgunGuy
{
  Default { +CORPSE }		
  States
  {
  Spawn:
    SPOS L -1; 
	Stop;
  Raise:
	SPOS LKJIH 5;
	TNT1 A 0 
	{
	  A_SpawnItemEx("D4OldShotgunGuy",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
	  Destroy();
	}
  }
}
Class D4DeadShotgunGuy : DeadShotgunGuy
{
  Default { +CORPSE }		
  States
  {
  Spawn:
    BBRG N -1; 
	Stop;
  Raise:
	BBRG NMLKJIH 3;
	TNT1 A 0 
	{
	  A_SpawnItemEx("D4ShotgunGuy",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
	  Destroy();
	}
  }
}
Class D4ShotguyCorpse : Actor replaces DeadShotgunGuy
{
	Default 
	{
		Radius 20;
		Height 56;
	}
	override void PostBeginPlay()
	{
		if (D4D_MonsterType == 0)
		{
			A_SpawnItemEx("D4DeadOldShotgunGuy",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
		}
		else
		{
			A_SpawnItemEx("D4DeadShotgunGuy",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
		}
		Destroy();
	}
}
//=============================================================================
//	Zombieman
//=============================================================================
Class D4DeadOldZombieman : DeadZombieman 
{
  Default { +CORPSE }		
  States
  {
  Spawn:
    POSS L -1; 
	Stop;
  Raise:
	POSS KJIH 5;
	TNT1 A 0 
	{
	  A_SpawnItemEx("D4OldZombieman",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
	  Destroy();
	}
  }
}
Class D4DeadZombieman : DeadZombieman
{
  Default { +CORPSE }		
  States
  {
  Spawn:
    RFTR L -1; 
	Stop;
  Raise:
    RFTR LKJIH 3;
	TNT1 A 0 
	{
	  A_SpawnItemEx("D4Zombieman",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
	  Destroy();
	}
  }
}
Class D4ZombiemanCorpse : Actor replaces DeadZombieman
{
	Default 
	{
		Radius 20;
		Height 56;
	}
	override void PostBeginPlay()
	{
		if (D4D_MonsterType == 0)
		{
			A_SpawnItemEx("D4DeadOldZombieman",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
		}
		else
		{
			A_SpawnItemEx("D4DeadZombieman",0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
		}
		Destroy();
	}
}


//=============================================================================
//	The rest
//=============================================================================
Class D4DeadMarine : 		DeadMarine replaces DeadMarine	
{	Default { +CORPSE }		}
Class D4GibbedMarine :		GibbedMarine replaces GibbedMarine	
{	Default { +CORPSE }		}
Class D4GibbedMarineExtra : D4GibbedMarine replaces GibbedMarineExtra 
{}
