//=================================================================================================
//=================================================================================================

//		Revenant

//=================================================================================================
//=================================================================================================
Class RevenantRune : DemonRuneBase
{
	Default 
	{
		DemonRuneBase.Morph "RevenantMorpher", "IamRevenant";
		Inventory.PickupMessage "Revenant rune";
	}
}

Class RevenantMorpher : PowerupGiver
{
	
	Default 
	{
		Inventory.MaxAmount 1;
		Inventory.Amount 1;
		Powerup.Type "PowerRevenantMorph";
		Powerup.Duration -60;
		+Inventory.AutoActivate
	}
	States
	{
	Spawn:
		PLAY A 3;
		Loop;
	}
}

Class PowerRevenantMorph : PowerRuneBase
{
	Default 
	{
		PowerMorph.PlayerClass "PlayerRevenant";
		PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_UNDOBYDEATH|MRF_UNDOBYDEATHSAVES|MRF_UNDOBYTOMEOFPOWER|MRF_LOSEACTUALWEAPON|MRF_NEWTIDBEHAVIOUR|MRF_UNDOALWAYS|MRF_FAILNOTELEFRAG);
		PowerMorph.UnMorphFlash "DemonRuneMorphOff";
		PowerMorph.MorphFlash "DemonRuneMorphOn";
	}
}


Class PlayerRevenant : DemonPlayerBase
{
	double USpeed;
	
	Default 
	{
		Speed 1;
		Health 400;
		Radius 16;
		Height 56;
		Mass 400;
		PainChance 40;
		Gravity 0.5;
		Player.JumpZ 10;
		Player.MorphWeapon "D4DRevenantWeapon";
		Player.ViewHeight 53;
		Player.AttackZOffset 15;
		Player.CrouchSprite "SKEL";
		Player.SoundClass "PlayerDemonCommon";
		PainSound "Doom4/Monsters/Revenant/Pain";
		DeathSound "Doom4/Monsters/Revenant/Death";
		DamageFactor "Normal", 0.5;
	}
	States
	{
	Spawn:
		SKEL A 0 NODELAY 
		{	
			if (!CountInv("OverlaysStarted"))
			{
				A_ClearOverlays();
				A_GiveInventory("JetpackFuel",100);
				A_TakeInventory("JetpackCooling",32768);
				A_Overlay(6004,"Overlay.Jetpack",true);
				A_Overlay(6005,"Overlay.RocketCooldown",true);	
				A_Overlay(6006,"Overlay.JetpackCooldown",true);	
				A_PlaySound("Doom4/Monsters/Revenant/Sight",6);
				A_GiveInventory("OverlaysStarted",1);
			}
		}
		SKEL A -1;
		Stop;
	See:
		SKEL ABCD 4;
		Loop;
	Pain:
		SKEL H 5 A_Pain();
		Goto Spawn;
	Missile:
	Melee:
		SKEL JK 10;
		Goto Spawn;
	Death:
		SKEL LM 7;
		SKEL N 7 A_Scream();
		SKEL O 7 A_NoBlocking();
		SKEL P 7;
		SKEL Q -1;
		Stop;
		
	Overlay.Steps:
		TNT1 A 5
		{
			NoFastOverlay(OverlayID());
			USpeed = vel.XY dot vel.XY;
			if(A_CheckFloor("Null"))
			{
				if(USpeed > 150)
				{
					A_PlaySound("Player/Demon/RevenantStep",CHAN_AUTO,0.85);
					A_SetTics(12);
				}
				else if(USpeed > 20)
				{
					A_PlaySound("Player/Demon/RevenantStep",CHAN_AUTO,0.85);
					A_SetTics(15);
				}
			}
		}
		Loop;
	Overlay.Jetpack:
		M666 A 1
		{
			int input = GetPlayerInput(MODINPUT_BUTTONS);
			if ((input & BT_ALTATTACK) && CountInv("JetpackFuel") && !CountInv("JetpackCooling") /*|| (GetPlayerInput(MODINPUT_BUTTONS) & BT_JUMP  )&&CountInv("JetpackFuel") && !CountInv("JetpackCooling")*/)
			{ 
				A_ChangeVelocity(vel.x,vel.y,3,CVF_REPLACE);
				A_ChangeVelocity(
				((input & BT_FORWARD  ) ? .5 : 0) - 
				((input & BT_BACK     ) ? .5 : 0),
				((input & BT_MOVELEFT ) ? .5 : 0) -
				((input & BT_MOVERIGHT) ? .5 : 0),
				0,CVF_RELATIVE);
				A_ChangeVelocity(vel.x*.97,vel.y*.97,vel.z,CVF_REPLACE);
				A_PlaySound("Player/Demon/Revenant/JetPackStartLoop",7,1,1);
				A_GiveInventory("RevenantFlyingToken",1);
				A_TakeInventory("JetpackFuel",1);
			}
			else if (CountInv("RevenantFlyingToken"))
			{
				A_SetInventory("RevenantFlyingToken",0);
				A_PlaySound("Player/Demon/Revenant/JetPackEndThrust",7);
			}
		}
		Loop;
	Overlay.RocketCooldown:
		M666 A 5 
		{
			A_TakeInventory("RevRcktHeat",1);
		}
		Loop;
	Overlay.JetpackCooldown:
		M666 A 1
		{
			if (CountInv("JetpackFuel")==0 && !CountInv("JetpackCooling"))					//when runs out of fuel
			{	A_GiveInventory("JetpackCooling",210);	}	//will force cool off
		
			if (CountInv("JetpackCooling"))				
			{	A_TakeInventory("JetpackCooling",1);	}
		
			if (CountInv("RevenantFlyingToken")==0 /*&& CountInv("JetpackCooling")==0*/)
			{
				A_GiveInventory("JetpackFuel",1);	
				A_TakeInventory("JetpackCooling",1);
			}
		
		}
		Loop;		
	Overlay.IdleSound:
		TNT1 A 35;
		TNT1 A 0 
		{
			NoFastOverlay(OverlayID());
			if (!GetCvar("D4D_SilentMorphs") && random(1,5)==5)
			{
				A_PlaySound("Doom4/Monsters/Revenant/Idle",5);
			}
		}
		Loop;
	}
}

Class D4DRevenantWeapon : DemonWeaponBase
{
	int count;
	enum RevConsts
	{
		Both = 0,
		Left,
		Right,
		RightOffset = 15,
		LeftOffset = -15,
		GunR = 6030,
		GunL = 6031
	};
	Default 
	{
		Tag "Shoulder-Mounted Rocket Launchers";
	}
	
	action void A_SetGunState (StateLabel start = null, int which = Both, bool nooverride = false)
	{
		Switch (which)
		{
			Default:
				A_Overlay(GunR, start, nooverride);
				A_Overlay(GunL, start, nooverride);
				A_OverlayFlags(GunL, PSPF_FLIP|PSPF_MIRROR, true);
				return;
			Case Right:
				A_Overlay(GunR, start, nooverride);
				return;
			Case Left:				
				A_Overlay(GunL, start, nooverride);
				A_OverlayFlags(GunL, PSPF_FLIP|PSPF_MIRROR, true);
				return;		
		}
	}
	
	States
	{
	Ready:
		M666 A 1;
		M666 A 0 A_SetGunState("Overlay.WeaponIdle");
	VeryReady:
		M666 A 1 
		{
			A_WeaponReady(WRF_DISABLESWITCH|WRF_NOSECONDARY);
		}
		Wait;
	Deselect:
		M666 A 1 A_Lower;
		Loop;
	Select:
		M666 A 1 A_Raise;
		Loop;
	Fire:
		---- A 1 CheckPrimary();
		Goto Ready;
	FireP0:
		M666 A 28
		{
			A_PlaySound("Player/Demon/Revenant/Fire",6);
			A_SetGunState("Overlay.WeaponFire");
			A_FireProjectile("PlayerRevRocket",0,0,LeftOffset,12);
			A_FireProjectile("PlayerRevRocket",0,0,RightOffset,12);
			A_Overlay(6020, "Overlay.ZoomEffect");
		}
		Goto VeryReady;
	FireP1:
		M666 A 12
		{
			bool rightside = (invoker.count % 2 == 0);
			double off = rightside ? RightOffset : LeftOffset;
			A_SetGunState("Overlay.WeaponFire", (rightside ? Right : Left));
			A_PlaySound("Player/Demon/Revenant/Fire",6);
			A_FireProjectile("PlayerRevRocket",0,0,off,12);
			A_Overlay(6020, "Overlay.ZoomEffectHalf");
			invoker.count = (invoker.count + 1) % 2;
		}
		Goto VeryReady;
	FireP2:
		M666 A 16
		{
			A_PlaySound("Player/Demon/Revenant/Fire",6);
			A_SetGunState("Overlay.WeaponFire");
			A_FireProjectile("PlayerRevRocket",0,0,LeftOffset,12);
			A_FireProjectile("PlayerRevRocket",0,0,RightOffset,12);
			A_Overlay(6020, "Overlay.ZoomEffect");
		}
		Goto VeryReady;
	FireP3:
		M666 A 0 { invoker.count = 0; }
		M666 A 4
		{
			bool rightside = (invoker.count % 2 == 0);
			double off = rightside ? RightOffset : LeftOffset;
			A_PlaySound("Player/Demon/Revenant/Fire",6);
			A_SetGunState("Overlay.WeaponFire", (rightside ? Right : Left));
			A_FireProjectile("PlayerRevRocket",0,0,off,12);
			A_Overlay(6020, "Overlay.ZoomEffectHalf");
			invoker.count++;
			if (invoker.count > 7)
				return ResolveState(1);
			return ResolveState(null);
		}
		Wait;
		M666 A 17;
		Goto VeryReady;
	FireP4:
		M666 AAAAAA 4
		{
			int channel = (invoker.count % 2 == 0) ? CHAN_WEAPON : 6;
			A_PlaySound("Player/Demon/Revenant/Fire",channel);
			A_SetGunState("Overlay.WeaponFire");
			A_FireProjectile("PlayerRevRocket",0,0,LeftOffset,12);
			A_FireProjectile("PlayerRevRocket",0,0,RightOffset,12);
			A_Overlay(6020, "Overlay.ZoomEffect");
			invoker.count = (invoker.count + 1) % 2;
		}
		M666 A 25;
		Goto VeryReady;
	FireP5:
		M666 A 16
		{
			bool rightside = (invoker.count % 2 == 0);
			double off = rightside ? RightOffset : LeftOffset;
			A_SetGunState("Overlay.WeaponFire", (rightside ? Right : Left));
			A_FireProjectile("PlayerCyberdemonRocket",0,0,off,12);
			A_Overlay(6020, "Overlay.ZoomEffectHalf");
			invoker.count = (invoker.count + 1) % 2;
		}
		Goto VeryReady;
	FireP6:
		M666 A 0 { invoker.count = 0; }
		M666 A 4
		{
			bool rightside = (invoker.count % 2 == 0);
			double off = rightside ? RightOffset : LeftOffset;
			A_SetGunState("Overlay.WeaponFire", (rightside ? Right : Left));
			A_FireProjectile("PlayerCyberdemonRocket",0,0,off,12);
			A_Overlay(6020, "Overlay.ZoomEffectHalf");
			invoker.count++;
			if (invoker.count > 7)
				return ResolveState(1);
			return ResolveState(null);
		}
		Wait;
		M666 A 35;
		Goto VeryReady;
	FireP7:
		M666 AAAAAA 4
		{
			A_SetGunState("Overlay.WeaponFire");
			A_FireProjectile("PlayerCyberdemonRocket",0,0,LeftOffset,12);
			A_FireProjectile("PlayerCyberdemonRocket",0,0,RightOffset,12);
			A_Overlay(6020, "Overlay.ZoomEffect");
		}
		M666 A 35;
		Goto VeryReady;
	
	//--------------------------------------------------------------------------
	Overlay.ZoomEffectHalf:
		TNT1 A 1 A_ZoomFactor(0.95, ZOOM_INSTANT|ZOOM_NOSCALETURNING);
		TNT1 A 1 A_ZoomFactor(0.975, ZOOM_NOSCALETURNING);
		TNT1 A 0 A_ZoomFactor(1.00, ZOOM_NOSCALETURNING);
		Stop;
	//--------------------------------------------------------------------------
	Overlay.ZoomEffect:
		TNT1 A 1 A_ZoomFactor(0.90, ZOOM_INSTANT|ZOOM_NOSCALETURNING);
		TNT1 A 1 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING);
		TNT1 A 0 A_ZoomFactor(1.00, ZOOM_NOSCALETURNING);
		Stop;
	//--------------------------------------------------------------------------
	Overlay.WeaponFire:
		RVCG BC 1 Bright;
		RVCG DE 3 Bright;
		Goto Overlay.WeaponIdle;
	Overlay.WeaponIdle:
		RVCG A -1;
		Stop;
	//--------------------------------------------------------------------------
	Meh:
		RVCG A 2 A_ChangeVelocity(vel.x,vel.y,5,CVF_REPLACE);
		RVCG A 0 A_WeaponReady(WRF_NOBOB);
		Goto Ready;

	Spawn:
		SHOT A -1;
		Stop;
	}
}	

Class RevRcktHeat : Inventory			{ Default { Inventory.MaxAmount 7; Inventory.InterHubAmount 0; } }
Class RevRocketPlaying : Inventory		{ Default { Inventory.MaxAmount 1; Inventory.InterHubAmount 0; } }
Class RevenantFlyingToken : Inventory 	{ Default { Inventory.InterHubAmount 0; } }


Class JetpackFuel : Inventory 
{
	Default 
	{
		Inventory.MaxAmount 100;
		Inventory.Amount 1;
		Inventory.InterHubAmount 0;
	}
}
Class JetpackCooling : Inventory 
{
	Default 
	{
		Inventory.MaxAmount 210;
		Inventory.Amount 1;
		Inventory.InterHubAmount 0;
	}
}

Class PlayerRevRocket : D4AbstractMissilePlayer 
{
	int user_value1;
	int user_value2;
	double  user_value3;
	double  user_value4;
	
	Default 
	{
		D4AbstractMissile.FXVelMul 0.5;
		Projectile;
		Speed 22;
		Damage 15;
		Radius 2;
		Height 3;
		Scale 0.7;
		+HITTRACER
		+EXTREMEDEATH
	
		//SeeSound "Doom4/Weapon/Machinegun/MiniMissileFire";
		Decal "D4ExploScorchSmallest";
	}
	States
	{
	Spawn:
		SWMS A 0 NoDelay A_SpawnItemEx("D4RocketTrailFire",flags: SXF_NOCHECKPOSITION|SXF_SETTARGET|SXF_ORIGINATOR);
		SWMS A 1 Bright Light("RedSmallPoint1") 
		{
			A_Weave(random(0,2), random(0,2), frandom(0,2), frandom(0,2));
			SpawnRocketFXSmall();
		}
		Wait;
	Death:
		TNT1 A 0 
		{
			A_Explode(64,128,0);
			A_SpawnParticle("FF0000",SPF_FULLBRIGHT,random(20,40),random(2,4),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
			A_SpawnParticle("FF0000",SPF_FULLBRIGHT,random(20,40),random(2,4),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
			A_SpawnParticle("FF0000",SPF_FULLBRIGHT,random(20,40),random(2,4),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
			A_SpawnParticle("BD0000",SPF_FULLBRIGHT,random(20,40),random(2,4),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
			A_SpawnParticle("BD0000",SPF_FULLBRIGHT,random(20,40),random(2,4),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
			A_SpawnParticle("BD0000",SPF_FULLBRIGHT,random(20,40),random(2,4),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
			A_SpawnParticle("FF4D00",SPF_FULLBRIGHT,random(20,40),random(2,4),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
			A_SpawnParticle("FF4D00",SPF_FULLBRIGHT,random(20,40),random(2,4),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
			A_SpawnParticle("FF4D00",SPF_FULLBRIGHT,random(20,40),random(2,4),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
			bINVISIBLE = true;
			A_PlaySound("Doom4/Weapon/RocketLauncher/Explode", volume: 0.4);
			if (GetCvar("HQEffects"))
			{
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
			}
			A_SpawnItemEx("CyberdemonRocketExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		}
		TNT1 A 2 Light("OrangeMediumPoint2");
		TNT1 A 2 Light("OrangeMediumPoint3");
		TNT1 A 2 Light("OrangeSmallPoint1");
		TNT1 A 2 Light("OrangeSmallPoint2");
		TNT1 A 2 Light("OrangeSmallPoint3");
		Stop;
	}
}

Class D4RocketTrailSmokeSmaller : D4RocketTrailSmoke
{
	Default 
	{
		+CLIENTSIDEONLY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Translucent";
		Alpha 0.2;
		Scale 0.0;
	}
	States
	{
	Spawn:
		TNT1 A 0;
		X037 AAAAAAAA 1 
		{
			A_SetScale(Scale.X+0.025);
			A_FadeIn(0.02);
		}
	Continue:
		X037 A 1 A_FadeOut(0.018);
		X037 A 0 A_SetScale(Scale.X+0.01);
		Loop;
	}
}