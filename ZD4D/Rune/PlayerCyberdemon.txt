//=================================================================================================
//=================================================================================================

//		Cyberdemon

//=================================================================================================
//=================================================================================================
Class CyberdemonRune : DemonRuneBase
{
	Default 
	{
		DemonRuneBase.Morph "CyberdemonMorpher", "IamCyberdemon";
		Inventory.PickupMessage "Cyberdemon rune";
		Scale 0.13;
	}
}

Class CyberdemonMorpher : PowerupGiver
{
	Default 
	{
		Inventory.MaxAmount 1;
		Inventory.Amount 1;
		Powerup.Type "PowerCyberdemonMorph";
		Powerup.Duration -60;
		+Inventory.AutoActivate
	}
	States
	{
		Spawn:
			PLAY A 3;
			Loop;
	}
}

Class PowerCyberdemonMorph : PowerRuneBase
{
	Default 
	{
		PowerMorph.PlayerClass "PlayerCyberdemon";
		PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_UNDOBYDEATH|MRF_UNDOBYDEATHSAVES|MRF_UNDOBYTOMEOFPOWER|MRF_LOSEACTUALWEAPON|MRF_NEWTIDBEHAVIOUR|MRF_UNDOALWAYS|MRF_FAILNOTELEFRAG);
		PowerMorph.UnMorphFlash "DemonRuneMorphOff";
		PowerMorph.MorphFlash "DemonRuneMorphOn";
	}
}


Class PlayerCyberdemon : DemonPlayerBase
{
	double USpeed;
	Default 
	{
		Speed 1;
		Health 4000;
		Radius 25;
		Height 95;
		Mass 800;
		PainChance 20;
		Gravity 0.7;
		Player.JumpZ 10;
		Player.MorphWeapon "D4DCyberdemonWeapon";
		Player.ViewHeight 92;
		Player.AttackZOffset 30;
		Player.SoundClass "PlayerDemonCommon";
		Player.CrouchSprite "CYBR";
		PainSound "Doom4/Cyberdemon/Pain";
		DeathSound "Doom4/Cyberdemon/Death";
		Player.ForwardMove 0.8, 0.4;
		Player.SideMove 0.8, 0.4;
		DamageFactor "Normal", 0.5;
	}
	States
	{
	Spawn:
		CYBR A -1;
		Stop;
	See:
		CYBR ABCD 5;
		Loop;
	Pain:
		CYBR G 5 A_Pain();
		Goto Spawn;
	Missile:
	Melee:
		CYBR EF 4;
		Goto Spawn;
	Death:
		TNT1 A 3 A_Scream();
		TNT1 B 3 A_NoBlocking();
		TNT1 CDEFGH 3;
		TNT1 I -1;
		Stop;
	Overlay.IdleSound:
		TNT1 A 35;
		TNT1 A 0 
		{
			NoFastOverlay(OverlayID());
			if (!GetCvar("D4D_SilentMorphs") && random(1,5)==5)
			{
				A_PlaySound("Doom4/Monsters/Cyberdemon/Idle",5);
			}
		}
		Loop;
	Overlay.Steps:
		TNT1 A 5
		{
			NoFastOverlay(OverlayID());
			USpeed = vel.XY dot vel.XY;
			if (A_CheckFloor("Null"))
			{
				if(USpeed > 10 ** 2)
				{
					A_PlaySound("Doom4/Cyberdemon/Step",CHAN_AUTO);
					A_SetTics(14);
				}
				else if(USpeed > 5 ** 2)
				{
					A_PlaySound("Doom4/Cyberdemon/Step",CHAN_AUTO);
					A_SetTics(18);
				}
			}
		}
		Loop;
	}
}

Class D4DCyberdemonWeapon : DemonWeaponBase
{
	Default 
	{
		Tag "Demonic Rocket Launcher";
	}
	
	action void A_KickPitchBig()
	{
		for (int i = 6021; i < 6030; i++)
			if (A_Overlay(i,"Overlay.FirePitch1", true))
				break;
	}
	
	action void A_KickPitchSmall()
	{
		for (int i = 6021; i < 6030; i++)
			if (A_Overlay(i,"Overlay.FirePitch2", true))
				break;
	}
	
	States
	{
	Ready:
		M023 A 1 A_WeaponReady(WRF_DISABLESWITCH);
		Loop;
	Deselect:
		M023 A 1 A_Lower;
		Loop;
	Select:
		M023 A 1 A_Raise;
		Loop;
	Fire:
		---- A 1 CheckPrimary();
		Goto Ready;
	AltFire:
		---- A 1 CheckSecondary();
		Goto Ready;
		
	
	Overlay.FirePitch1:
		TNT1 A 1 A_SetPitch(pitch - 3);
		TNT1 A 4 A_SetPitch(pitch - 3);
		TNT1 AAAAAA 1 A_SetPitch(pitch + 1);
		Stop;
	Overlay.FirePitch2:
		TNT1 A 1 A_SetPitch(pitch-2);
		TNT1 AAAA 1 A_SetPitch(pitch + 0.5);
		Stop;
	Flash:
		M023 A 3 Bright A_Light1();
		M023 B 4 Bright;
		M023 CD 4 Bright A_Light2();
		Goto LightDone;
	FlashMiniRockets:
		M023 A 1 Bright A_Light1();
		M023 B 2 Bright;
		M023 CD 2 Bright A_Light2();
		Goto LightDone;
	
	//==========================================================================
	// Regular, Uncommon, Rare and Epic
	FireP0:
	FireP1:
	FireP2:
	FireP3:
		M023 A 8 A_GunFlash();
		M023 A 12
		{
			A_FireProjectile("PlayerCyberdemonRocket",0,0,0,10);
			int power = GetPowerLevel();
			double spread = 10.0;
			for (int i = 0; i < power; i++) 
				A_FireProjectile("PlayerCyberDemonRocketStream",frandom(-spread, spread),0,0,10,FPF_NOAUTOAIM,frandom(-spread,spread));
				
			A_KickPitchBig();
		}
		M023 A 0 A_ReFire("MiniRockets");
		Goto Ready;
		
	MiniRockets:
		M023 A 3 A_GunFlash("FlashMiniRockets");
		M023 A 5
		{
			A_FireProjectile("PlayerCyberDemonRocketStream",0,0,0,10);
			int power = GetPowerLevel();
			double spread = 10.0;
			for (int i = 0; i < power; i++) 
				A_FireProjectile("PlayerCyberDemonRocketStream",frandom(-spread, spread),0,0,10,FPF_NOAUTOAIM,frandom(-spread,spread));
			A_KickPitchSmall();
		}
		M023 A 0 A_ReFire("MiniRockets");
		Goto Ready;
	
	//==========================================================================
	// Tier 4 (Orange - Legendary)
	FireP4:
		M023 A 6 A_GunFlash();
		M023 A 5
		{
			A_FireProjectile("PlayerCyberDemonRocket",0,0,0,10);
			A_KickPitchSmall();
		}
		M023 A 0 A_ReFire("FireP4");
		Goto Ready;
		
	//==========================================================================
	// Tier 5 (Red - Ancient)
	FireP5:
		M023 A 3 A_GunFlash("FlashMiniRockets");
		M023 A 5
		{
			A_FireProjectile("PlayerCyberDemonRocket",0,0,0,10);
			A_KickPitchSmall();
		}
		M023 A 0 A_ReFire("FireP5");
		Goto Ready;
		
	//==========================================================================
	// Tier 6 (Black)
	FireP6:
		M023 A 3 A_GunFlash("FlashMiniRockets");
		M023 A 5
		{
			A_FireProjectile("PlayerCyberDemonRocket",0,0,0,10);
			for (int i = 0; i < 2; i++) 
				A_FireProjectile("PlayerCyberDemonRocketStream",frandom(-10.0, 10.0),0,0,10,FPF_NOAUTOAIM,frandom(-10.0,10.0));
			A_KickPitchSmall();
		}
		M023 A 0 A_ReFire("FireP6");
		Goto Ready;
		
	//==========================================================================
	// Tier 7 (White)
	FireP7:
		M023 A 3 A_GunFlash("FlashMiniRockets");
		M023 A 5
		{
			A_FireProjectile("PlayerCyberDemonRocket",0,0,0,10);
			A_FireProjectile("PlayerCyberDemonRocket",frandom(-10.0, 10.0),0,0,10,FPF_NOAUTOAIM,frandom(-10.0,10.0));
			A_KickPitchSmall();
		}
		M023 A 0 A_ReFire("FireP7");
		Goto Ready;
	
	AltFireP1:
	AltFireP2:
	AltFireP3:
	AltFireP4:
	AltFireP5:
	AltFireP6:
	AltFireP7:
	AltFireP0:
		M023 A 0 
		{
			A_SpawnItemEx("CyberdemonPlayerMuzzle",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
			A_PlaySound("Doom4/Weapon/GaussGun/Charge2Start",7,1.0);
		}
		---- ABBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 1 A_RailAttack(0,0,0,"","Red",RGF_SILENT|RGF_FULLBRIGHT,0,"NoBloodPuff",0,0,0,1,1.5,1,null,0,0);
		
		M023 A 0 
		{
			int power = GetPowerLevel();
			int dmg = 1000 * (1 + power);
			int a = 255;
			Color Spiral = Color("Yellow");
			
			Switch(power)
			{
				Default:	break;
				Case 1:		Spiral = Color("Green");	break;
				Case 2:		Spiral = Color("Blue");		break;
				Case 3:		Spiral = Color("Purple");	break;
				Case 4:		Spiral = Color("Orange");	break;
				Case 5:		Spiral = Color("Red");		break;
				Case 6:		Spiral = Color(a, 0, 0, 0);	break;
				Case 7:		Spiral = Color("White");	break;
			}
			
			//the attack part
			A_RailAttack(dmg,1,0,"","",RGF_SILENT|RGF_FULLBRIGHT,0,null,0,0,32765,0,100,0,"StaticTrailBig");
			for (int i = 0; i < 4; i++)
				A_RailAttack(0,0,0,Spiral,"",RGF_SILENT|RGF_FULLBRIGHT,0,null,0,0,0,70,1,random(1.0,2.0),null,0,i * 90);
			
			//the other stuff
			A_RemoveChildren(true,RMVF_EVERYTHING,"CyberdemonPlayerMuzzle");
			A_PlaySound("Doom4/Weapon/GaussGun/Charge2Fire",7);
			A_PlaySound("Doom4/Monsters/MastermindLaserFire",8);
			
			A_SetPitch(pitch-7);
		}
		M023 BBCCDDA 1 Bright A_SetPitch(pitch+1);
		Goto Ready;
	Spawn:
		LAUN A -1;
		Stop;
	}
}	

Class CyberdemonPlayerMuzzle : Actor 
{
	const Flags = WARPF_NOCHECKPOSITION|WARPF_COPYPITCH;
	const Distance = 30;
	const YOff = 1;
	const ZOff = 80;
	double  UPos;
	int user_t;
	
	Default 
	{
		Projectile;
		+NOINTERACTION
		RenderStyle "Add";
		Scale 0.0;
		Alpha 0.01;
		+FORCEXYBILLBOARD
		Translation "0:255=%[0,0,0]:[2.0,1.3,0.3]";
	}
	States
	{
	Spawn:
		X060 A 1 BRIGHT NoDelay
		{
			if (master) { pitch = master.pitch; } //Grab the pitch...
			
			// And set a multiplier for taking from the distance so it actually goes well with the gun.
			
			if (pitch > 0) //Looking down
			{	UPos = (sin(pitch) * 12);	}
			else //Looking up
			{	UPos = (sin(pitch) * 6);		}
			
			
			A_Warp(MasterPtr,
				(cos(-pitch)*(Distance)) - UPos,
				YOff,
				(ZOff*GetCrouchFactor(MasterPtr))+(sin(-pitch)*(Distance)),0,Flags|WARPF_INTERPOLATE);
				
			A_SpawnItemEx("CyberdemonPlayerSpark",0,0,-3,sin(-pitch)*frandom(0.1,0.5),frandom(-0.7,-1.3),cos(pitch)*frandom(1,-5.0),0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("CyberdemonPlayerSpark",0,0,-3,sin(-pitch)*frandom(0.1,0.5),frandom(0.7,1.3),cos(pitch)*frandom(1,-5.0),0,SXF_NOCHECKPOSITION);
	
			A_FadeIn(0.014);
			A_SetScale(Scale.X+0.013);
		}
		Loop;
	Nothing:
		TNT1 A 100;
		Stop;
	}
}
Class CyberdemonPlayerSpark : Actor 
{
	Default 
	{
		+NOINTERACTION
		Scale 0.05;
		RenderStyle "Add";
		Translation "0:255=%[0,0,0]:[2.0,1.3,0.3]";
		+FORCEXYBILLBOARD
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,1,2,3,4,5,6,7,8);
		X079 ABCDEFGH 0	A_Jump(256,"Perform");
	Perform:
		"####" "#" 1 Bright 
		{
			A_SetScale(Scale.X+0.01);
			A_FadeOut(0.05);
		}
		Loop;
	}
}
//-----------------------------------------------
// Regular rockets
Class PlayerCyberdemonRocket : D4Rocket
{
	Default 
	{
		Projectile;
		+EXTREMEDEATH
		Speed 23;
		Radius 6;
		Height 12;
		Scale 0.6;
		Damage 40;
		FastSpeed 30;
		SeeSound "Doom4/Weapon/RocketLauncher/Fire";
		DeathSound "Doom4/Weapon/RocketLauncher/HRExplo";
		Decal "D4ExploScorch";
	}
	States
	{
	Death:
		TNT1 A 0 
		{
			bINVISIBLE = true;
			bNOINTERACTION = true;
			A_Explode(192,256,0);
			A_QuakeEx(2,2,2,15,0,384,"",QF_SCALEDOWN, falloff: 1);
			if (HQ)
			{
				for (int i = 0; i < 5; i++)
					A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
			}
			A_SpawnItemEx("D4RocketExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		}
		TNT1 A 35;
		Stop;
	}
}

//-----------------------------------------------
// Stream of rockets
Class PlayerCyberDemonRocketStream : D4AbstractMissilePlayer
{
	Default 
	{
		D4AbstractMissile.FXVelMul 0.5;
		Projectile;
		+EXTREMEDEATH
		Speed 21;
		Radius 6;
		Height 12;
		Scale 0.4;
		Damage 18;
		SeeSound "Doom4/Weapon/RocketLauncher/Fire";
		DeathSound "Doom4/Weapon/RocketLauncher/HRExplo";
		Decal "D4ExploScorchSmall";
	}
	States
	{
	Spawn:
		MICR A 1 Light("RedMediumPoint3") Bright NoDelay A_PlaySound("Doom4/Monsters/RocketFlyLoop",7,0.7,1);
	Moving:
		MICR A 1 Light("RedMediumPoint3") Bright SpawnRocketFXSmall();
		Loop;
	Death:
		TNT1 A 0 
		{
			DestroyFX();
			bNoInteraction = true;
			A_Explode(92,128,0);
			A_QuakeEx(2,2,2,10,0,192,"",QF_SCALEDOWN, falloff: 1);
			A_SpawnItemEx("CyberdemonRocketExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		}
		TNT1 A 50;
		Stop;
	}
}