//=================================================================================================
//=================================================================================================

//		Baron of Hell

//=================================================================================================
//=================================================================================================
Class BaronRune : DemonRuneBase
{
	Default 
	{
		DemonRuneBase.Morph "BaronMorpher", "IamBaronOfHell";
		Inventory.PickupMessage "Baron of Hell rune";
		Scale 0.115;
	}
}

Class BaronMorpher: PowerupGiver
{
	Default 
	{
		Inventory.MaxAmount 1;
		Inventory.Amount 1;
		Powerup.Type "PowerBaronMorph";
		Powerup.Duration -60;
		+Inventory.AutoActivate
	}
	States
	{
		Spawn:
			PLAY A 3;
			Loop;
	}
}

Class PowerBaronMorph : PowerRuneBase
{
	Default 
	{
		PowerMorph.PlayerClass "PlayerBaron";
		PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_UNDOBYDEATH|MRF_UNDOBYDEATHSAVES|MRF_UNDOBYTOMEOFPOWER|MRF_LOSEACTUALWEAPON|MRF_NEWTIDBEHAVIOUR|MRF_UNDOALWAYS|MRF_FAILNOTELEFRAG);
		PowerMorph.UnMorphFlash "DemonRuneMorphOff";
		PowerMorph.MorphFlash "DemonRuneMorphOn";
	}
}

Class PlayerBaron : DemonPlayerBase
{
	double USpeed;
	Default 
	{
		Speed 1;
		Health 1000;
		Radius 18;
		Height 64;
		Mass 1000;
		PainChance 40;
		Gravity 0.5;
		Player.JumpZ 10;
		Player.MorphWeapon "D4DBaronWeapon";
		Player.ViewHeight 63;
		Player.AttackZOffset 25;
		Player.SoundClass "PlayerDemonCommon";
		Player.CrouchSprite "E01I";
		PainSound "Doom4/Monsters/Baron/Pain";
		DeathSound "Doom4/Monsters/Baron/Death";
		Scale 0.72;
		DamageFactor "Normal", 0.5;
		
	}
	States
	{
	Spawn:
		E01I A -1;
		Stop;
	See:
		E01W ABCDEFGH 3;
		Loop;
	Pain:
		E01P A 5 A_Pain();
		Goto Spawn;
	Missile:
	Melee:
		E01M A 0 A_Jump(256,"Missile1","Missile2");
	Missile1:
		E01M ABCDEFG 3;
		Goto Spawn;
	Missile2:
		E01N ABCDEFG 3;
		Goto Spawn;
	Death:
		E01D A 3 A_Scream();
		E01D B 3 A_NoBlocking();
		E01D CDEFGH 3;
		E01D I -1;
		Stop;
	Overlay.IdleSound:
		TNT1 A 35;
		TNT1 A 0
		{
			NoFastOverlay(OverlayID());
			if (!GetCvar("D4D_SilentMorphs") && random(1,7)==7)
			{
				PlaySound("Doom4/Monsters/Baron/Idle",5);
			}
		}
		Loop;
	Overlay.Steps:
		TNT1 A 5
		{
			NoFastOverlay(OverlayID());
			USpeed = (vel.x*vel.x + vel.y*vel.y);
			if (A_CheckFloor("Null"))
			{
				if(USpeed > 150)
				{
					PlaySound("Player/Demon/HeavyStep",CHAN_AUTO,0.9);
					A_SetTics(12);
				}
				else if(USpeed > 20)
				{
					PlaySound("Player/Demon/HeavyStep",CHAN_AUTO,0.9);
					A_SetTics(15);
				}
			}
		}
		Loop;
		
	}
}

Class D4DBaronWeapon : DemonWeaponBase
{
	const AngPitRNG = 5.0;
	
	bool Punched;
	Default 
	{
		Tag "Huge Red Hands";
	}
	
	action void A_MeleeFireBall(double offset = 0.0)
	{
		int power = GetPowerLevel();
		if (power < 1)	return;
		
		int chance = 0;
		Class<Actor> ball = null;
		
		if (power > 4)
		{
			chance = random(5,7);
			ball = (chance <= power) ? "D4PlayerBaronBallBig" : "D4PlayerBaronBall";
		}
		else
		{
			chance = random(1,4);
			if (chance <= power)	ball = "D4PlayerBaronBall";
		}
		
		if (ball)	A_FireProjectile(ball,0,0,offset,0);
	}
	
	private action void ViewSwing(bool left)
	{
		StateLabel next = "Overlay.SwingLeft";
		if (!left) next = "Overlay.SwingRight";
		for (int i = 1000; i < 2000; i++)
		{
			if (A_Overlay(i, next, true))
				return;
		}
	}
	
	States
	{
	Ready:
		HBRS A 0 A_TakeInventory("PunchedAlready");
		HBRS A 1 A_WeaponReady(WRF_DISABLESWITCH);
		Loop;
	Deselect:
		HBRS A 1 A_Lower;
		HBRS A 0 A_StopSound(CHAN_WEAPON);
		Loop;
	Select:
		HBRS A 1 A_Raise;
		Loop;
	Fire:
		---- A 1 CheckPrimary();
		Goto Ready;
	AltFire:
		---- A 1 CheckSecondary();
		Goto Ready;
	
	FireP1:
	FireP2:
	FireP3:
	FireP4:
	FireP5:
	FireP6:
	FireP7:
	FireP0:
		TNT1 A 0 
		{ 
			if (!random(0,5))	PlaySound("Doom4/Monsters/Baron/Attack",7);
			return A_Jump(256,"SwingLeft", "SwingRight");
		}
	SwingRight:
		HBRM ABC 1;
		HBRM D 1 		{ DemonPunch(random(100,200), 66);	A_MeleeFireBall(15.0);	}
		HBRM EF 1 		DemonPunch(random(100,200), 80);
		HBRM GH 1 		DemonPunch(random(100,200), 66);
		W666 AAAAAA 1 	DemonPunch(random(60,120), 60);
		W666 A 7;
		Goto Ready;
	SwingLeft:
		HBRN ABC 1;
		HBRN D 1 		{ DemonPunch(random(100,200), 66);	A_MeleeFireBall(-15.0);	}
		HBRN EF 1 		DemonPunch(random(100,200), 80);
		HBRN GH 1 		DemonPunch(random(100,200), 66);
		W666 AAAAAA 1 	DemonPunch(random(60,120), 60);
		W666 A 7;
		Goto Ready;
		
	Overlay.SwingLeft:
		TNT1 A 1		A_SetViewAPR((4,2,-3),true);
		TNT1 AA 1		A_SetViewAPR((4,2,0),true);
		TNT1 AA 1		A_SetViewAPR((-1,-.5,0),true);
		TNT1 AAAAAA 1	A_SetViewAPR((-1,-.5,0),true);
		TNT1 A 1		A_SetViewAPR((-1,-.5,3),true);
		TNT1 AAA 1		A_SetViewAPR((-1,-.5,0),true);
		Stop;
	Overlay.SwingRight:
		TNT1 A 1		A_SetViewAPR((-4,2,3),true);
		TNT1 AA 1		A_SetViewAPR((-4,2,0),true);
		TNT1 AA 1		A_SetViewAPR((1,-.5,0),true);
		TNT1 AAAAAA 1	A_SetViewAPR((1,-.5,0),true);
		TNT1 A 1		A_SetViewAPR((1,-.5,-3),true);
		TNT1 AAA 1		A_SetViewAPR((1,-.5,0),true);
		Stop;
	
	AltFireP1:
	AltFireP2:
	AltFireP3:
	AltFireP4:
	AltFireP5:
	AltFireP6:
	AltFireP7:
	AltFireP0:
		HBRS A 0 A_Overlay(6011, "Overlay.AltNoise", true);
		HBRS AAAA 1 A_WeaponOffset(0,20,WOF_ADD);
	AltFireResume:
		HBRM AAAA 1 A_WeaponOffset(0,-20,WOF_ADD);
		---- A 0 
		{
			if (GetCvar("HQEffectsWeapons"))
			{	A_SpawnItemEx("BaronPlayerMuzzle",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);	}
		}
	AltFireLooper:
		HBRM A 2 
		{
			A_GiveInventory("BaronFireBallCounter",1);
		}
		HBRM A 0 A_Refire("AltFireLooper");
		Goto Release;
	Release:
		HBRM A 0 PlaySound("Doom4/Monsters/Generic/ThrowFireball",CHAN_WEAPON,0.7);
		HBRM ABC 1;
		HBRM A 0 
		{
			A_RemoveChildren(true,RMVF_EVERYTHING,"BaronPlayerMuzzle");
			Class<Actor> ball = "D4PlayerBaronBall";
			int power = GetPowerLevel();
			int count = 1 + power;
			
			ball = (CountInv("BaronFireBallCounter") >= 15) ? "D4PlayerBaronBallBig" : "D4PlayerBaronBall";
			
			for (int i = 0; i < count; i++)
			{
				Vector2 ap = (0,0);
				if (i > 0)
					ap = (frandom(-AngPitRNG, AngPitRNG), frandom(-AngPitRNG, AngPitRNG));
				
				A_FireProjectile(ball,ap.x,0,15,0,0,ap.y);
			}
			A_SetInventory("BaronFireBallCounter",0);
			A_Overlay(6010, "Overlay.SwingLeft",true);
		}
		
		HBRM DEFGH 1 ;	
		M666 A 13 ;
		M666 A 0 A_Refire(1);
		Goto Ready;
		M666 A 0 
		{
			A_WeaponOffset(0,80,WOF_ADD);
			A_Overlay(6011, "Overlay.AltNoise", true);
		}
		Goto AltFireResume;
		
	Overlay.AltNoise:
		M666 A 1
		{
			if (random(1,3) == 1)  PlaySound("Doom4/Monsters/Baron/Attack",7);
			PlaySound("Doom4/Monsters/Generic/FireLoop",CHAN_WEAPON,0.4,1); 
		}
		Stop;
	Spawn:
		M666 A 0;
		Stop;
	}
}	

Class BaronFireBallCounter : Inventory
{	Default { Inventory.MaxAmount 15; Inventory.InterHubAmount 0; } }

Class BaronPlayerMuzzle : D4DFX 
{
	const Flags = WARPF_NOCHECKPOSITION|WARPF_COPYPITCH;
	const Distance = 19;
	const YOff = 16;
	const ZOff = 64;
	double  UPos;
	int user_t;
	
	Default 
	{
		Projectile;
		+NOINTERACTION
		+INVISIBLE
		+FORCEXYBILLBOARD
		+NOTONAUTOMAP
		RenderStyle "None";
	}
	States
	{
	Spawn:
		X060 A 1 BRIGHT NoDelay
		{
			if (!master || master.GetClass() != "PlayerBaron") 
				return Destroy();
			
			// Grab the pitch, and set a multiplier for taking from the 
			// distance so it actually goes well with the gun.				
			// Remember: positive pitch means looking up, and vice versa.
				
			pitch = master.pitch;
			UPos = (sin(pitch) * ((pitch > 0) ? 12 : 6));
					
			A_Warp(AAPTR_MASTER,
				(cos(-pitch)*(Distance)) - UPos,
				YOff,
				(ZOff*GetCrouchFactor(AAPTR_MASTER))+(sin(-pitch)*(Distance)),0,Flags|WARPF_INTERPOLATE);
							
			if (master.CountInv("BaronFireBallCounter") >= 15)
				A_SpawnItemEx("BaronOfHellHandFlamelet2",0,0,-3,sin(-pitch)*frandom(0.5,0.5),frandom(-1.4,-0.4),cos(pitch)*frandom(0.9,-4.5),0,SXF_NOCHECKPOSITION);
			else
				A_SpawnItemEx("BaronOfHellHandFlamelet",0,0,-3,sin(-pitch)*frandom(0.25,0.25),frandom(-0.7,-0.7),cos(pitch)*frandom(0.8,-2.5),0,SXF_NOCHECKPOSITION);
		}
		Loop;
	Nothing:
		TNT1 A 1;
		Stop;
	}
}

Class BaronOfHellHandFlamelet2 : D4DFX 
{

	Default 
	{
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle "Add";
		Alpha 0.4;
		Scale 0.28;
	}
	States
	{
	Spawn:
		PROJ D 0 NODELAY A_Jump(220,5,6,7,8,9,10,11,12,13);
		X056 ABCDEFGHIJKLMNOPR 1 Bright;
		Stop;
	}
}






Class D4PlayerBaronBall : D4BaronBallNormal
{
	Default 
	{
		Speed 20;
		Damage 20;
		+EXTREMEDEATH
	}
	states
	{
	Spawn:
		HIB1 AB 2 BRIGHT Light("GreenMediumFlicker2")
		{
			if (GetCvar("HQEffectsWeapons"))
			{
				A_SpawnItemEx("BaronOfHellHandFlamelet",random(-2,-8),random(8,-8),random(8,-8),0,0,frandom(0,0.3),0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("BaronOfHellHandFlamelet",random(-2,-8),random(8,-8),random(8,-8),0,0,frandom(0,0.3),0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("DoomImpBallSmokeTrail",-5,random(2,-2),random(2,-2),0,0,0,0,SXF_NOCHECKPOSITION);
			}
		}
		Loop;
	Death:
		TNT1 A 0
		{
			if (GetCvar("HQEffectsWeapons"))
			{
				A_SpawnParticle("46D200",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnParticle("46D200",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnParticle("46D200",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnParticle("46D200",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnParticle("419900",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnParticle("419900",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnParticle("419900",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnParticle("419900",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnParticle("7BFF00",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnParticle("7BFF00",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnParticle("7BFF00",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnParticle("7BFF00",SPF_FULLBRIGHT,random(45,75),8,random(0,359),0,0,0,frandom(2,-2),frandom(2,-2),frandom(3,-3));
				A_SpawnItemEx("DoomImpExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("DoomImpExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("DoomImpExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("DoomImpExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION,128);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION,128);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION,128);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),0,SXF_NOCHECKPOSITION,128);
			}
			A_Explode(92,128,0);
		}
		
		HIB1 CC 1 BRIGHT Light("GreenMediumFlicker1")	//had to separate
		{
			A_FadeOut(0.088);
			A_SetScale(Scale.X+0.1);
		}
		HIB1 DD 1 BRIGHT Light("GreenMediumFlicker2")
		{
			A_FadeOut(0.088);
			A_SetScale(Scale.X+0.1);
		}
		HIB1 EE 1 BRIGHT Light("GreenMediumFlicker3")
		{
			A_FadeOut(0.088); 
			A_SetScale(Scale.X+0.1);
		}
		HIB1 FF 1 BRIGHT Light("GreenSmallFlicker1")
		{
			A_FadeOut(0.088);
			A_SetScale(Scale.X+0.1);
		}
		HIB1 GG 1 BRIGHT Light("GreenSmallFlicker1")
		{
			A_FadeOut(0.088);
			A_SetScale(Scale.X+0.1);
		}
		Stop;
	}
}





Class D4PlayerBaronBallBig : D4BaronBallBig
{
	Default 
	{
		Speed 40;
		Damage 40;
		+EXTREMEDEATH
	}
	States
	{
	Spawn:
		HIB1 AABB 2 BRIGHT Light("GreenMediumFlicker1")
		{
			if (GetCvar("HQEffectsWeapons"))
			{
				A_SpawnItemEx("BaronBallBigTrail",-5,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("BaronOfHellBallBigFlamelet",random(-2,-8),random(12,-12),random(12,-12),frandom(4,-4),frandom(4,-4),frandom(1.0,5.0),0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("BaronOfHellBallBigFlamelet",random(-2,-8),random(12,-12),random(12,-12),frandom(4,-4),frandom(4,-4),frandom(1.0,5.0),0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("BaronOfHellBallBigFlamelet",random(-2,-8),random(12,-12),random(12,-12),frandom(4,-4),frandom(4,-4),frandom(1.0,5.0),0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("DoomImpBallFlareTrail",-3,random(2,-2),random(2,-2),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);	
			}
		}
		Loop;
	Death:
		TNT1 A 0
		{
			if (GetCvar("HQEffectsWeapons"))
			{
				A_SpawnParticle("46D200",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnParticle("46D200",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnParticle("46D200",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnParticle("46D200",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnParticle("419900",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnParticle("419900",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnParticle("419900",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnParticle("419900",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnParticle("7BFF00",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnParticle("7BFF00",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnParticle("7BFF00",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnParticle("7BFF00",SPF_FULLBRIGHT,random(60,90),8,random(0,359),0,0,0,frandom(3.5,-3.5),frandom(3.5,-3.5),frandom(3,-3));
				A_SpawnItemEx("DoomImpExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("DoomImpExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("DoomImpExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("DoomImpExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3),0,SXF_NOCHECKPOSITION,128);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3),0,SXF_NOCHECKPOSITION,128);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3),0,SXF_NOCHECKPOSITION,128);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3),0,SXF_NOCHECKPOSITION,128);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3),0,SXF_NOCHECKPOSITION,128);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3),0,SXF_NOCHECKPOSITION,128);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3),0,SXF_NOCHECKPOSITION,128);
				A_SpawnItemEx("BaronBallImpactVapor",3,0,0,frandom(-3,3),frandom(-3,3),frandom(-3,3),0,SXF_NOCHECKPOSITION,128);
			}
		}
		
		TNT1 A 0 A_Explode(256,160,0);

		HIB1 CC 1 BRIGHT Light("GreenMediumFlicker1")	//had to separate
		{
			A_FadeOut(0.088);
			A_SetScale(Scale.X+0.1);
		}
		HIB1 DD 1 BRIGHT Light("GreenMediumFlicker2")
		{
			A_FadeOut(0.088);
			A_SetScale(Scale.X+0.1);
		}
		HIB1 EE 1 BRIGHT Light("GreenMediumFlicker3")
		{
			A_FadeOut(0.088); 
			A_SetScale(Scale.X+0.1);
		}
		HIB1 FF 1 BRIGHT Light("GreenSmallFlicker1")
		{
			A_FadeOut(0.088);
			A_SetScale(Scale.X+0.1);
		}
		HIB1 GG 1 BRIGHT Light("GreenSmallFlicker1")
		{
			A_FadeOut(0.088);
			A_SetScale(Scale.X+0.1);
		}
		Stop;
	}
}