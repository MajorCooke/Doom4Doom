//=================================================================================================
//=================================================================================================

//		Fatso

//=================================================================================================
//=================================================================================================
Class FatsoRune : DemonRuneBase
{
	Default 
	{
		Inventory.PickupMessage "Mancubus rune";
	}
	States
	{
	Pickup:
		TNT1 A 0 
		{
			if (CountInv("IamADemonToken") || CountInv("PowerInvulBase") || countinv("D4DoomDollHands") || countinv("BusyToken")) //if already a demon...
			{
				return false;	//...cant pickup
			}
			else	//otherwise..
			{
				A_GiveInventory("SoundStopper",1); //stops any sound (mostly for the looping ones)
				A_GiveInventory("HealthArmorSaver",1);
				A_GiveInventory("IamADemonToken",1); //...gives token and morphs
				A_GiveInventory("IamFatso",1);
				A_GiveInventory("FatsoMorpher",1);
				return true;
			}
		}
		Stop;
	}
}

Class FatsoMorpher: PowerupGiver
{
	Default 
	{
		Inventory.MaxAmount 1;
		Inventory.Amount 1;
		Powerup.Type "PowerFatsoMorph";
		Powerup.Duration -60;
		+Inventory.AutoActivate
	}
	States
	{
		Spawn:
			PLAY A 3;
			Loop;
	}
}

Class PowerFatsoMorph : PowerRuneBase
{
	Default 
	{
		PowerMorph.PlayerClass "PlayerFatso";
		PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_UNDOBYDEATH|MRF_UNDOBYDEATHSAVES|MRF_UNDOBYTOMEOFPOWER|MRF_LOSEACTUALWEAPON|MRF_NEWTIDBEHAVIOUR|MRF_UNDOALWAYS|MRF_FAILNOTELEFRAG);
		PowerMorph.UnMorphFlash "DemonRuneMorphOff";
		PowerMorph.MorphFlash "DemonRuneMorphOn";
	}
}


Class PlayerFatso : DemonPlayerBase
{
	double user_speed;
	
	Default 
	{
		Speed 1;
		Health 800;
		Radius 22;
		Height 64;
		Mass 800;
		PainChance 40;
		Gravity 0.7;
		Player.JumpZ 8;
		Player.MorphWeapon "D4DFatsoWeapon";
		Player.ViewHeight 60;
		Player.AttackZOffset 10;
		Player.ForwardMove 0.4, 0.2;
		Player.SideMove 0.4, 0.2;
		Player.CrouchSprite "FATT";
		Player.SoundClass "PlayerDemonCommon";
		PainSound "Doom4/Monsters/Mancubus/Pain";
		DeathSound "Doom4/Monsters/Mancubus/Death";
		DamageFactor "Normal", 0.5;
		+NOSKIN
		-PICKUP
	}
	States
	{
	Spawn:
		FATT A -1;
		Stop;
	See:
		FATT ABCDEF 3;
		Loop;
	Pain:
		FATT J 5 A_Pain();
		Goto Spawn;
	Missile:
	Melee:
		FATT GHI 5;
		Goto Spawn;
	Death:
		FATT K 4;
		FATT L 4 A_Scream();
		FATT M 4 A_NoBlocking();
		FATT NOPQRS 4;
		FATT T -1;
		Stop;
	Overlay.IdleSound:
		TNT1 A 35;
		TNT1 A 0
		{
			if (!GetCvar("D4D_SilentMorphs") && random(1,8)==8)
			{
				A_PlaySound("Doom4/Monsters/Mancubus/Idle",5);
			}
		}
		Loop;
	Overlay.Steps:
		TNT1 A 5
		{
			user_speed = (vel.x*vel.x + vel.y*vel.y);
			if(A_CheckFloor("Null"))
			{
				if(user_speed > 10 )
				{
					A_PlaySound("Player/Demon/MechStep",CHAN_BODY,0.8);
					A_SetTics(14);
				}
				else if(user_speed > 5)
				{
					A_PlaySound("Player/Demon/MechStep",CHAN_BODY,0.8);
					A_SetTics(18);
				}
			}
		}
		Loop;
	Overlay.DemonTimer:
		TNT1 A 0 
		{
			A_TakeInventory("DemonTimerToken", 99999);
			A_GiveInventory("DemonTimerToken", 60*35);
		}
		TNT1 A 1 
		{
			A_TakeInventory("DemonTimerToken", 1);
		}
		Wait;
	}
}





Class D4DFatsoWeapon : DoomWeapon
{
	Default 
	{
		Tag "Organic Flamethrowers";
	}
	States
	{
	Ready:
		MARM A 1 A_WeaponReady(WRF_DISABLESWITCH);
		Loop;
	Deselect:
		MARM A 1 A_Lower;
		Loop;
	Select:
		MARM A 1 A_Raise;
		Loop;
	Fire:
		MARM A 25 A_PlaySound("Doom4/Monsters/Mancubus/Attack2",CHAN_WEAPON);
	FireLooper:
		MARM A 0 
		{
			A_PlaySound("Player/Demon/Fatso/Fire",6);
			A_FireProjectile("D4FatsoPlayerFire",0,0,15,10);
			A_FireProjectile("D4FatsoPlayerFire",0,0,-15,10);
		}
		MARM B 3 A_SetPitch(pitch-5, SPF_INTERPOLATE);
		MARM C 3;
		MARM DDDEE 1 A_SetPitch(pitch+1, SPF_INTERPOLATE);
		MARM E 1;
		MARM A 20;
		MARM A 0 A_Refire("FireLooper");
		Goto Ready;
	
	AltFire:
		MARM D 0 
		{
			A_PlaySound("Doom4/Other/FlamerStart",6);
			A_WeaponOffset(0,12,WOF_ADD);
		}
	AltFireLoop:
		MARM DDDDD 2
		{
			A_FireProjectile("FatsoPlayerFlamethrower",0,0,15,10);
			A_FireProjectile("FatsoPlayerFlamethrower",0,0,-15,10);
		}
		MARM A 0 A_PlaySound("Doom4/Other/FlamerLoop",7);
		MARM D 0 A_Refire("AltFireLoop");
		MARM A 0 
		{
			A_StopSound(7);
			A_WeaponOffset(0,-12,WOF_ADD);
		}
		Goto Ready;
		
	Flash:
		SHTF A 1 Bright A_Light1();
		SHTF B 1 Bright A_Light2();
		Goto LightDone;
	Spawn:
		SHOT A -1;
		Stop;
	}
}	

//----------------------------------------
// flame thrower attack
Class FatsoPlayerFlamethrower : Actor 
{
	int user_counter;
	Default 
	{
		Projectile;
		Radius 5;
		Height 10;
		RenderStyle "Add";
		Scale 0.3;
		Speed 18;
		Damage 3;
	}
	States
	{
	Spawn:
		X029 A 0 NODELAY A_Jump(210,2,4,6,8);
		X029 ABCDEFGHIJK 2 Bright
		{
			A_SetScale(Scale.X+0.13);
			A_FadeOut(0.035);
			if (user_counter==3)
			{
				A_ScaleVelocity(0.7);
				user_counter = 0;
			}
			user_counter++;
		}
		Goto Spawn+1;
	Death:
		X029 ABCDEFGHIJK 2 Bright
		{
			A_SetScale(Scale.X+0.15);
			A_FadeOut(0.07);
		}
		Loop;
	}
}



//----------------------------------------
// fireball attack
Class D4FatsoPlayerFire : Actor 
{
	Default 
	{
		Projectile;
		Speed 25;
		Radius 5;
		Height 10;
		Damage 15; //does some explosion damage too
		Scale 0.6;
		RenderStyle "Add";
		Alpha 0.99;
		//SeeSound "fatso/attack"
		DeathSound "Doom4/Other/FireballHit1";
		Decal "FireballScorchBig";
		+EXTREMEDEATH
	}
	States
	{
	Spawn:
		X029 A 0 NODELAY A_Jump(210,2,4,6,8);
		X029 ABCDEFGHIJK 1 Bright Light("YellowMediumFlicker2")
		{
			if (GetCvar("HQEffectsWeapons"))
			{
				A_SpawnParticle("FFB200",SPF_FULLBRIGHT,random(20,50),6,0,0,0,0,frandom(1.5,-1.5),frandom(1.5,-1.5),frandom(-1.4,1.4),0.03,0.03,-0.5);
				A_SpawnItemEx("FatsoFireTrail",-3,random(2,-2),random(2,-2),0,0,0,0,SXF_NOCHECKPOSITION);
			}
		}
		Goto Spawn+1;
	Death: 
		TNT1 A 0 
		{
			A_Explode(92,128,0);
			if (GetCvar("HQEffectsWeapons"))
			{
				A_SpawnItemEx("FatsoExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("FatsoExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("FatsoExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("FatsoExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("FatsoExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
				A_SpawnItemEx("FatsoExploParticle",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
			}
			A_SpawnItemEx("FatsoExploParticle",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		}
		TNT1 A 2 Light("YellowMediumFlicker1");
		TNT1 A 2 Light("YellowMediumFlicker2");
		TNT1 A 2 Light("YellowMediumFlicker3");
		TNT1 A 2 Light("YellowSmallFlicker1");
		TNT1 A 2 Light("YellowSmallFlicker2");
		TNT1 A 2 Light("YellowSmallFlicker3");
		Stop;
	}
}
